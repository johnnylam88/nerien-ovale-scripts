### Hunter Ovale script based on SimulationCraft profiles
### https://sites.google.com/site/wownerien/ovale/hunter/script
### Version: 4.3.8
#
# BM:
#	talents=http://www.wowhead.com/talent#hunter-2330230311320112121230200000000000000003000000000000000000
#	glyphs=kill_command/arcane_shot/kill_shot/bestial_wrath
#
# MM:
#	talents=http://www.wowhead.com/talent#hunter-0230020000000000000230232003212023122103000000000000000000
#	glyphs=steady_shot/aimed_shot/rapid_fire
#
# SV:
#	talents=http://www.wowhead.com/talent#hunter-0300000000000000000230200000000000000023203003023022121311
#	glyphs=arcane_shot/kill_shot/explosive_shot

## defines ##

# Buffs
Define(CHRONOHUNTER 105919) # 4pT13 buff; 105s ICD(?)
Define(ELEMENTALOATH 51470) # elemental shaman crit buff
Define(FIRE 82926)
Define(FRENZYEFFECT 19615)
Define(HONORAMONGTHIEVES 51701) # subtlety rogue crit buff
Define(IMPROVEDSTEADYSHOT 53244) # Improved Steady Shot (Rank 3)
Define(LEADEROFTHEPACK 17007) # feral druid crit buff
Define(LOCKANDLOAD 56453)
Define(RABIDPOWER 53403) # Rabid (pet talent)
Define(RAMPAGE 29801) # fury warrior crit buff
Define(THEBEASTWITHIN 34692)

# Debuffs
Define(MARKEDFORDEATH 88691)

# Glyphs
Define(GLYPHOFARCANESHOT 56995)
Define(GLYPHOFTRAPLAUNCHER 56996)

# Talents
Define(BLACKARROWTALENT 9480)
Define(IMPROVEDSTEADYSHOTTALENT 9402) # Improved Steady Shot (Rank 3)

# Spells
Define(AIMEDSHOT 19434)
	SpellInfo(AIMEDSHOT mana=50 resetcounter=iss)
	SpellAddBuff(AIMEDSHOT FIRE=0)
Define(ARCANESHOT 3044)
	SpellInfo(ARCANESHOT mana=25 resetcounter=iss)
Define(ASPECTOFTHEFOX 82661)
	SpellAddBuff(ASPECTOFTHEFOX ASPECTOFTHEFOX=1000)
Define(ASPECTOFTHEHAWK 13165)
	SpellAddBuff(ASPECTOFTHEHAWK ASPECTOFTHEHAWK=1000)
Define(BESTIALWRATH 19574)
	SpellInfo(BESTIALWRATH cd=120)
	SpellAddBuff(BESTIALWRATH BESTIALWRATH=10)
Define(BLACKARROW 3674)
	SpellInfo(BLACKARROW cd=30 sharedcd=firetrap mana=35)
	SpellAddTargetDebuff(BLACKARROW BLACKARROW=15)
Define(CALLPETONE 883)
Define(CALLPETTWO 83242)
Define(CALLPETTHREE 83243)
Define(CALLPETFOUR 83244)
Define(CALLPETFIVE 83245)
Define(CHIMERASHOT 53209)
	SpellInfo(CHIMERASHOT cd=10 mana=50 resetcounter=iss)
	SpellAddTargetDebuff(CHIMERASHOT SERPENTSTING=refresh)
Define(COBRASHOT 77767)
	SpellInfo(COBRASHOT mana=-9 resetcounter=iss)
	SpellAddTargetDebuff(COBRASHOT SERPENTSTING=refresh)
Define(EXPLOSIVESHOT 53301)
	SpellInfo(EXPLOSIVESHOT cd=6 mana=50 resetcounter=iss buffnocd=LOCKANDLOAD)
	SpellAddTargetDebuff(EXPLOSIVESHOT EXPLOSIVESHOT=2)
	SpellAddBuff(EXPLOSIVESHOT LOCKANDLOAD=-1)
Define(EXPLOSIVETRAPLAUNCHER 82939)
	SpellInfo(EXPLOSIVETRAPLAUNCHER cd=30 sharedcd=firetrap)
Define(FERVOR 82726)
	SpellInfo(FERVOR mana=-50 cd=120)
Define(FOCUSFIRE 82692)
	SpellInfo(FOCUSFIRE cd=15)
	SpellAddBuff(FOCUSFIRE FOCUSFIRE=15)
Define(KILLCOMMAND 34026)
	SpellInfo(KILLCOMMAND cd=6 mana=40 resetcounter=iss)
Define(KILLSHOT 53351)
	SpellInfo(KILLSHOT cd=10 resetcounter=iss)
Define(HUNTERSMARK 1130)
	SpellAddTargetDebuff(HUNTERSMARK HUNTERSMARK=300)
Define(MULTISHOT 2643)
	SpellInfo(MULTISHOT mana=40 resetcounter=iss)
Define(RAPIDFIRE 3045)
	SpellInfo(RAPIDFIRE cd=300 resetcounter=iss)
	SpellAddBuff(RAPIDFIRE RAPIDFIRE=15)
Define(READINESS 23989)
	SpellInfo(READINESS cd=180 resetcounter=iss)
Define(REVIVEPET 982)
	SpellInfo(REVIVEPET mana=35 resetcounter=iss)
Define(SERPENTSTING 1978)
	SpellInfo(SERPENTSTING duration=15 mana=25 resetcounter=iss)
	SpellAddTargetDebuff(SERPENTSTING SERPENTSTING=15)
Define(SILENCINGSHOT 34490)
	SpellInfo(SILENCINGSHOT cd=20 resetcounter=iss)
Define(STEADYSHOT 56641)
	SpellInfo(STEADYSHOT mana=-9 inccounter=iss)
Define(TRAPLAUNCHER 77769)
	SpellInfo(TRAPLAUNCHER cd=1.5 mana=20)
	SpellInfo(TRAPLAUNCHER addmana=-10 glyph=56996) # Glyph of Trap Launcher
	SpellAddBuff(TRAPLAUNCHER TRAPLAUNCHER=15)

# Pet spells
Define(PETACIDSPIT 55749)
	SpellInfo(PETACIDSPIT cd=25)
	SpellAddDebuff(PETACIDSPIT PETACIDSPIT=25)
Define(PETBITE 17253)
Define(PETCALLOFTHEWILD 53434)
	SpellInfo(PETCALLOFTHEWILD cd=300)
	SpellAddBuff(PETCALLOFTHEWILD PETCALLOFTHEWILD=20)
Define(PETCLAW 16827)
Define(PETCORROSIVESPIT 35387)
	SpellInfo(PETCORROSIVESPIT cd=6)
	SpellAddDebuff(PETCORROSIVESPIT PETCORROSIVESPIT=30) # lowerarmor
Define(PETDEMOROAR 50256)
	SpellInfo(PETDEMOROAR cd=10)
	SpellAddDebuff(PETDEMOROAR PETDEMOROAR=15) # lowerphysicaldamage
Define(PETDEMOSCREECH 24423)
	SpellInfo(PETDEMOSCREECH cd=10)
	SpellAddDebuff(PETDEMOSCREECH PETDEMOSCREEH=10) # lowerphysicaldamage
Define(PETDUSTCLOUD 50285)
	SpellInfo(PETDUSTCLOUD cd=25)
	SpellAddDebuff(PETDUSTCLOUD PETDUSTCLOUD=30) # meleeslow
Define(PETFIREBREATH 34889)
	SpellInfo(PETFIREBREATH cd=30)
	SpellAddDebuff(PETFIREBREATH PETFIREBREATH=45) # magicaldamagetaken
Define(PETFROSTSTORMBREATH 92380)
Define(PETFURIOUSHOWL 24604)
	SpellInfo(PETFURIOUSHOWL cd=45)
	SpellAddBuff(PETFURIOUSHOWL PETFURIOUSHOWL=60) # -- criticalstrike
Define(PETGORE 35290)
	SpellInfo(PETGORE cd=10)
	SpellAddDebuff(PETGORE PETGORE=15) # bleed
Define(PETGROWL 2649)
Define(PETHEARTOFTHEPHOENIX 55709)
	SpellInfo(PETHEARTOFTHEPHOENIX cd=480)
Define(PETLAVABREATH 58604)
	SpellInfo(PETLAVABREATH cd=8)
	SpellAddDebuff(PETLAVABREATH PETLAVABREATH=10) # castslow
Define(PETLIGHTNINGBREATH 24844)
	SpellInfo(PETLIGHTNINGBREATH cd=30)
	SpellAddDebuff(PETLIGHTNINGBREATH PETLIGHTNINGBREATH=45) # magicaldamagetaken
Define(PETMONSTROUSBITE 54680)
	SpellInfo(PETMONSTROUSBITE cd=8)
	SpellAddDebuff(PETMONSTROUSBITE PETMONSTROUSBITE=8)
Define(PETNETHERSHOCK 50479)
	SpellInfo(PETNETHERSHOCK cd=40) # interrupt
Define(PETPUMMEL 26090)
	SpellInfo(PETPUMMEL cd=30) # interrupt
Define(PETQIRAJIFORTITUDE 90364)
	SpellAddBuff(PETQIRAJIFORTITUDE PETQIRAJIFORTITUDE=1000) # stamina
Define(PETRABID 53401)
	SpellInfo(PETRABID cd=45)
	SpellAddBuff(PETRABID PETRABID=20)
Define(PETRAVAGE 50518)
	SpellInfo(PETRAVAGE cd=15)
	SpellAddDebuff(PETRAVAGE PETRAVAGE=25) # -- physicaldamagetaken
Define(PETROAROFCOURAGE 93435)
	SpellInfo(PETROAROFCOURAGE cd=45)
	SpellAddBuff(PETROAROFCOURAGE PETROAROFCOURAGE=60) # strengthagility
Define(PETROAROFRECOVERY 53517)
	SpellInfo(PETROAROFRECOVERY cd=180)
	SpellAddBuff(PETROAROFRECOVERY PETROAROFRECOVERY=9)
Define(PETSERENITYDUST 50318)
	SpellInfo(PETSERENITYDUST cd=60) # interrupt
Define(PETSMACK 49966)
Define(PETSPORECLOUD 50274)
	SpellInfo(PETSPORECLOUD cd=8)
	SpellAddDebuff(PETSPORECLOUD PETSPORECLOUD=10) # castslow
Define(PETSTAMPEDE 57386)
	SpellInfo(PETSTAMPEDE cd=15)
	SpellAddDebuff(PETSTAMPEDE PETSTAMPEDE=30) # bleed
Define(PETTAILSPIN 90314)
	SpellInfo(PETTAILSPIN cd=25)
	SpellAddDebuff(PETTAILSPIN PETTAILSPIN=30) # meleeslow
Define(PETTEARARMOR 50498)
	SpellInfo(PETTEARARMOR cd=6)
	SpellAddDebuff(PETTEARARMOR PETTEARARMOR=30) # lowerarmor
Define(PETTENDONRIP 50271)
	SpellInfo(PETTENDONRIP cd=10)
	SpellAddDebuff(PETTENDONRIP PETTENDONRIP=15) # bleed
Define(PETTERRIFYINGROAR 90309)
	SpellInfo(PETTERRIFYINGROAR cd=45)
	SpellAddBuff(PETTERRIFYINGROAR PETTERRIFYINGROAR=60) # -- criticalstrike
Define(PETWOLVERINEBITE 53508)
	SpellInfo(PETWOLVERINEBITE cd=10)

# Racials & Professions
Define(ARCANETORRENTFOCUS 80483) # blood elf
	SpellInfo(ARCANETORRENTFOCUS cd=120)
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(BLOODFURY 33697) # orc
	SpellInfo(BLOODFURY duration=15 cd=120)
	SpellAddBuff(BLOODFURY BLOODFURY=15)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellInfo(STONEFORM STONEFORM=8)

# Items
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)

## end defines ##

ScoreSpells(FOCUSFIRE KILLCOMMAND ARCANESHOT KILLSHOT STEADYSHOT SERPENTSTING CHIMERASHOT AIMEDSHOT
			BLACKARROW EXPLOSIVESHOT)

AddCheckBox(aoe L(AOE))
AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL))
AddCheckBox(petbuff "Pet raid buffs & debuffs")

AddFunction SummonPet
{
	if PetPresent(no) Texture(ability_hunter_beastcall)
	if pet.Life(equal 0)
	{
		Spell(PETHEARTOFTHEPHOENIX)
		Spell(REVIVEPET)
	}
}

AddFunction PetMaintainBuffDebuff
{
	# Maintain raid buffs and debuffs.
	if BuffExpires(stamina 3) Spell(PETQIRAJIFORTITUDE)
	if BuffExpires(strengthagility 3) Spell(PETROAROFCOURAGE)
	unless BuffPresent(ELEMENTALOATH) or BuffPresent(HONORAMONGTHIEVES) or BuffPresent(LEADEROFTHEPACK) or BuffPresent(RAMPAGE)
	{
		if BuffExpires(PETFURIOUSHOWL 2) or BuffExpires(PETTERRIFYINGROAR 2)
		{
			Spell(PETFURIOUSHOWL)
			Spell(PETTERRIFYINGROAR)
		}
	}
	if TargetDebuffExpires(bleed 2)
	{
		Spell(PETGORE)
		Spell(PETSTAMPEDE)
		Spell(PETTENDONRIP)
	}
	if TargetDebuffExpires(castslow 2)
	{
		Spell(PETLAVABREATH)
		Spell(PETSPORECLOUD)
	}
	if TargetDebuffExpires(lowerarmor 2)
	{
		Spell(PETTEARARMOR)
		Spell(PETCORROSIVESPIT)
	}
	if TargetDebuffExpires(lowerphysicaldamage 0) or TargetDebuffExpires(PETDEMOSCREECH 0)
	{
		Spell(PETDEMOROAR)
		Spell(PETDEMOSCREECH)
	}
	if TargetDebuffExpires(magicaldamagetaken 2)
	{
		Spell(PETFIREBREATH)
		Spell(PETLIGHTNINGBREATH)
	}
	if TargetDebuffExpires(meleeslow 2) or TargetDebuffExpires(PETTAILSPIN 2)
	{
		Spell(PETDUSTCLOUD)
		Spell(PETTAILSPIN)
	}
}

AddFunction PetActions
{
	#/call_of_the_wild
	Spell(PETCALLOFTHEWILD)
	#/roar_of_recovery,if=!buff.bloodlust.up
	unless BuffPresent(heroism) Spell(PETROAROFRECOVERY)
	#/rabid
	Spell(PETRABID)
	#/wolverine_bite
	Spell(PETWOLVERINEBITE usable=1)
	#if pet.Mana(more 25)
	#{
	#	Spell(PETBITE)
	#	Spell(PETCLAW)
	#	Spell(PETSMACK)
	#}
}

AddFunction PetInterrupt
{
	if TargetInRange(PETNETHERSHOCK) Spell(PETNETHERSHOCK)
	if TargetInRange(PETPUMMEL) Spell(PETPUMMEL)
	if TargetInRange(PETSERENITYDUST) Spell(PETSERENITYDUST)
}

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	if Mana(less 81) Spell(ARCANETORRENTFOCUS)
	Spell(BERSERKING)
	Spell(BLOODFURY)
}

AddIcon help=main mastery=1
{
	#/hunters_mark,if=target.time_to_die>=21
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 21) Spell(HUNTERSMARK nored=1)
	#/serpent_sting,if=!ticking
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/kill_command
	if TargetInRange(GROWL) and PetPresent(yes) Spell(KILLCOMMAND)
	#/arcane_shot,if=focus>=59|buff.beast_within.up
	if Mana(more 59) or BuffPresent(THEBEASTWITHIN) Spell(ARCANESHOT)
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX)
	{
		#/cobra_shot,if=target.adds>5
		Spell(COBRASHOT)
		#/steady_shot
		Spell(STEADYSHOT)
	}
}

AddIcon help=offgcd mastery=1
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	if PetPresent(yes)
	{
		if TargetIsInterruptible() PetInterrupt()
		if CheckBoxOn(petbuff) PetMaintainBuffDebuff()
		#/focus_fire,five_stacks=1
		if pet.BuffPresent(FRENZYEFFECT stacks=5) Spell(FOCUSFIRE)
		#/bestial_wrath,if=focus>60
		if Mana(more 60) Spell(BESTIALWRATH)
		PetActions()
	}
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/serpent_sting,if=!ticking
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX)
	{
		#/cobra_shot,if=target.adds>5
		Spell(COBRASHOT)
		#/steady_shot
		Spell(STEADYSHOT)
	}
}

AddIcon help=cd mastery=1
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#init_use_item_actions()
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#init_use_racial_actions()
	UseRacialActions()
	#/rapid_fire,if=!buff.bloodlust.up&!buff.beast_within.up
	unless BuffPresent(heroism) or BuffPresent(THEBEASTWITHIN) Spell(RAPIDFIRE)
	#/fervor,if=focus<=37
	if Mana(less 37) Spell(FERVOR)
}

AddIcon help=main mastery=2
{
	#/hunters_mark,if=target.time_to_die>=21
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 21) Spell(HUNTERSMARK nored=1)
	#/serpent_sting,if=!ticking&target.health_pct<=90
	unless TargetDebuffPresent(SERPENTSTING) if TargetLifePercent(less 90) and Mana(more 24) Spell(SERPENTSTING)
	#/chimera_shot,if=target.health_pct<=90
	if TargetLifePercent(less 90) and Mana(more 49) Spell(CHIMERASHOT)
	#/steady_shot,if=buff.pre_improved_steady_shot.up&buff.improved_steady_shot.remains<3
	if TalentPoints(IMPROVEDSTEADYSHOTTALENT more 0) and BuffExpires(IMPROVEDSTEADYSHOT 3)
	{
		if Counter(iss equal 1) Spell(STEADYSHOT)
		if Counter(iss equal 3) Spell(STEADYSHOT)
		unless ArmorSetParts(T13 more 1) if Counter(iss equal 5) Spell(STEADYSHOT)
		# if you're shooting more than 5 Steady Shots in a row, you're capping your focus and doing it wrong
	}
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/aimed_shot,if=buff.master_marksman_fire.react
	if BuffPresent(FIRE) Spell(AIMEDSHOT)
	if ArmorSetParts(T13 more 3)
	{
		#/arcane_shot,if=(focus>=66|cooldown.chimera_shot.remains>=4)&\
		#	(target.health_pct<90&!buff.rapid_fire.up&!buff.bloodlust.react&!buff.berserking.up&\
		#		!buff.tier13_4pc.react&cooldown.buff_tier13_4pc.remains<=0)
		unless Mana(less 66) and 4s before Spell(CHIMERASHOT)
			if TargetLifePercent(less 90)
				unless BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or BuffPresent(BERSERKING) or BuffPresent(CHRONOHUNTER)
					if BuffGain(CHRONOHUNTER 105) Spell(ARCANESHOT)
		#/aimed_shot,if=(cooldown.chimera_shot.remains>5|focus>=80)&(buff.bloodlust.react|buff.tier13_4pc.react|cooldown.buff_tier13_4pc.remains>0)|\
		#	buff.rapid_fire.up|target.health_pct>90
		unless 5s before Spell(CHIMERASHOT) and Mana(less 80)
		{
			if BuffPresent(heroism) or BuffPresent(CHRONOHUNTER) if Mana(more 49) Spell(AIMEDSHOT)
			unless BuffGain(CHRONOHUNTER 105) if Mana(more 49) Spell(AIMEDSHOT)
		}
		if {BuffPresent(RAPIDFIRE) or TargetLifePercent(more 90)} and Mana(more 49) Spell(AIMEDSHOT)
	}
	unless ArmorSetParts(T13 more 3)
	{
		#if ( ! glyphs.arcane_shot -> ok() )
		unless Glyph(GLYPHOFARCANESHOT)
		{
			#/aimed_shot,if=cooldown.chimera_shot.remains>5|focus>=80|buff.rapid_fire.up|buff.bloodlust.react|target.health_pct>90
			unless 5s before Spell(CHIMERASHOT) if Mana(more 49) Spell(AIMEDSHOT)
			if Mana(more 79) Spell(AIMEDSHOT)
			if BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or TargetLifePercent(more 90) and Mana(more 49) Spell(AIMEDSHOT)
		}
		#else
		if Glyph(GLYPHOFARCANESHOT)
		{
			#/aimed_shot,if=target.health_pct>90|buff.rapid_fire.up|buff.bloodlust.react|buff.berserking.up
			if BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or TargetLifePercent(more 90) and Mana(more 49) Spell(AIMEDSHOT)
			#/arcane_shot,if=(focus>=66|cooldown.chimera_shot.remains>=5)&\
			#	(target.health_pct<90&!buff.rapid_fire.up&!buff.bloodlust.react&!buff.berserking.up)
			unless Mana(less 66) and 5s before Spell(CHIMERASHOT)
				if TargetLifePercent(less 90)
					unless BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or BuffPresent(BERSERKING)
						Spell(ARCANESHOT)
		}
	}
	#/steady_shot
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX) Spell(STEADYSHOT)
}

AddIcon help=offgcd mastery=2
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	if PetPresent(yes)
	{
		if TargetIsInterruptible() PetInterrupt()
		if CheckBoxOn(petbuff) PetMaintainBuffDebuff()
		PetActions()
	}
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	#/steady_shot,if=target.adds>5
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX) Spell(STEADYSHOT)
}

AddIcon help=cd mastery=2
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	if TargetIsInterruptible() and TargetInRange(SILENCINGSHOT) Spell(SILENCINGSHOT)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60|buff.rapid_fire.react
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#init_use_item_actions()
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#init_use_racial_actions()
	UseRacialActions()
	#/rapid_fire,if=!buff.bloodlust.up|target.time_to_die<=30
	unless BuffPresent(heroism) and TargetDeadIn(more 30) Spell(RAPIDFIRE)
	#/readiness,wait_for_rapid_fire=1
	if BuffPresent(RAPIDFIRE) Spell(READINESS)
}

AddIcon help=main mastery=3
{
	#/hunters_mark,if=target.time_to_die>=21
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 21) Spell(HUNTERSMARK nored=1)
	#/serpent_sting,if=!ticking&target.time_to_die>=10
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) and TargetDeadIn(more 10) Spell(SERPENTSTING)
	#/explosive_shot,if=(remains<2.0)
	if TargetDebuffExpires(EXPLOSIVESHOT 2 mine=1) and Mana(more 49) Spell(EXPLOSIVESHOT)
	#if ( ! talents.black_arrow -> rank() ) { /explosive_trap,not_flying=1,if=target.time_to_die>=11 }
	unless TalentPoints(BLACKARROWTALENT more 0) and TargetDeadIn(more 11)
	{
		if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
		if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
		if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	}
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/black_arrow,if=target.time_to_die>=8
	if TargetDeadIn(more 8) and Mana(more 34) Spell(BLACKARROW)
	#/arcane_shot,if=focus>=67
	if Mana(more 67) Spell(ARCANESHOT)
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX)
	{
		#/cobra_shot
		Spell(COBRASHOT)
		#/steady_shot
		Spell(STEADYSHOT)
	}
}

AddIcon help=offgcd mastery=3
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	if PetPresent(yes)
	{
		if TargetIsInterruptible() PetInterrupt()
		if CheckBoxOn(petbuff) PetMaintainBuffDebuff()
		PetActions()
	}
}

AddIcon help=aoe mastery=3 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/multi_shot,if=target.adds>2
	if Mana(more 39) Spell(MULTISHOT)
	if Speed(equal 0) or BuffPresent(ASPECTOFTHEFOX)
	{
		#/cobra_shot,if=target.adds>2
		Spell(COBRASHOT)
		#/steady_shot
		Spell(STEADYSHOT)
	}
}

AddIcon help=cd mastery=3
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60) or BuffPresent(RAPIDFIRE)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#init_use_racial_actions()
	UseRacialActions()
	#init_use_item_actions()
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#/rapid_fire
	Spell(RAPIDFIRE)
}