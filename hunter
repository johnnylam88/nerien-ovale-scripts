# Hunter Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.2 2012/01/03 - Add Steady Shot for low-level BM hunters.
#                    Add conditions for using Aimed Shot and Arcane Shot as focus dumps depending on 4pT13
#                        and Glyph of Arcane Shot
#                    Check for Glyph of Rapid Fire for using potions.
# 4.3.1 2011/12/22 - Initial version based on default Ovale script and SimulationCraft, Tier 13H profiles:
#                        BM (r10533), MM (r10576), SV (r10526)
#                    Support Silencing Shot and summoning pets.

## defines ##

# Buffs
Define(CHRONOHUNTER 105919) # 4pT13 buff; 110s ICD(?)
Define(FIRE 82926)
Define(FRENZYEFFECT 19615)
Define(IMPROVEDSTEADYSHOT 53244) # Improved Steady Shot (Rank 3)
Define(LOCKANDLOAD 56453)
Define(THEBEASTWITHIN 34692)

# Debuffs
Define(MARKEDFORDEATH 88691)

# Glyphs
Define(GLYPHOFARCANESHOT 56995)
Define(GLYPHOFTRAPLAUNCHER 56996)

# Talents
Define(IMPROVEDSTEADYSHOT 9402) # Improved Steady Shot (Rank 3)

# Spells
Define(AIMEDSHOT 19434)
	SpellInfo(AIMEDSHOT mana=50 resetcounter=iss)
	SpellAddBuff(AIMEDSHOT FIRE=0)
Define(ARCANESHOT 3044)
	SpellInfo(ARCANESHOT mana=25 resetcounter=iss)
Define(ASPECTOFTHEFOX 82661)
	SpellAddBuff(ASPECTOFTHEFOX ASPECTOFTHEFOX=1000)
Define(ASPECTOFTHEHAWK 13165)
	SpellAddBuff(ASPECTOFTHEHAWK ASPECTOFTHEHAWK=1000)
Define(BESTIALWRATH 19574)
	SpellInfo(BESTIALWRATH cd=120)
	SpellAddBuff(BESTIALWRATH BESTIALWRATH=10)
Define(BLACKARROW 3674)
	SpellInfo(BLACKARROW cd=30 sharedcd=firetrap mana=35)
	SpellAddTargetDebuff(BLACKARROW BLACKARROW=15)
Define(CALLOFTHEWILD 53434)
	SpellInfo(CALLOFTHEWILD cd=300)
	SpellAddBuff(CALLOFTHEWILD CALLOFTHEWILD=20)
Define(CALLPETONE 883)
Define(CALLPETTWO 83242)
Define(CALLPETTHREE 83243)
Define(CALLPETFOUR 83244)
Define(CALLPETFIVE 83245)
Define(CHIMERASHOT 53209)
	SpellInfo(CHIMERASHOT cd=10 mana=50 resetcounter=iss)
	SpellAddTargetDebuff(CHIMERASHOT SERPENTSTING=refresh)
Define(COBRASHOT 77767)
	SpellInfo(COBRASHOT mana=-9 resetcounter=iss)
	SpellAddTargetDebuff(COBRASHOT SERPENTSTING=refresh)
Define(EXPLOSIVESHOT 53301)
	SpellInfo(EXPLOSIVESHOT cd=6 mana=50 resetcounter=iss buffnocd=LOCKANDLOAD)
	SpellAddTargetDebuff(EXPLOSIVESHOT EXPLOSIVESHOT=2)
	SpellAddBuff(EXPLOSIVESHOT LOCKANDLOAD=-1)
Define(EXPLOSIVETRAPLAUNCHER 82939)
	SpellInfo(EXPLOSIVETRAPLAUNCHER cd=30 sharedcd=firetrap)
Define(FERVOR 82726)
	SpellInfo(FERVOR mana=-50 cd=120)
Define(FOCUSFIRE 82692)
	SpellInfo(FOCUSFIRE cd=15)
	SpellAddBuff(FOCUSFIRE FOCUSFIRE=15)
Define(KILLCOMMAND 34026)
	SpellInfo(KILLCOMMAND cd=6 mana=40 resetcounter=iss)
Define(KILLSHOT 53351)
	SpellInfo(KILLSHOT cd=10 resetcounter=iss)
Define(HUNTERSMARK 1130)
	SpellAddTargetDebuff(HUNTERSMARK HUNTERSMARK=300)
Define(MULTISHOT 2643)
	SpellInfo(MULTISHOT mana=40 resetcounter=iss)
Define(RAPIDFIRE 3045)
	SpellInfo(RAPIDFIRE cd=300 resetcounter=iss)
	SpellAddBuff(RAPIDFIRE RAPIDFIRE=15)
Define(READINESS 23989)
	SpellInfo(READINESS cd=180 resetcounter=iss)
Define(REVIVEPET 982)
	SpellInfo(REVIVEPET mana=35 resetcounter=iss)
Define(SERPENTSTING 1978)
	SpellInfo(SERPENTSTING duration=15 mana=25 resetcounter=iss)
	SpellAddTargetDebuff(SERPENTSTING SERPENTSTING=15)
Define(SILENCINGSHOT 34490)
	SpellInfo(SILENCINGSHOT cd=20 resetcounter=iss)
Define(STEADYSHOT 56641)
	SpellInfo(STEADYSHOT mana=-9 inccounter=iss)
Define(TRAPLAUNCHER 77769)
	SpellInfo(TRAPLAUNCHER cd=1.5 mana=20)
	SpellInfo(TRAPLAUNCHER addmana=-10 glyph=56996) # Glyph of Trap Launcher
	SpellAddBuff(TRAPLAUNCHER TRAPLAUNCHER=15)

# Pet spells
Define(GROWL 2649)

# Items
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)

## end defines ##

ScoreSpells(FOCUSFIRE KILLCOMMAND ARCANESHOT KILLSHOT STEADYSHOT SERPENTSTING CHIMERASHOT AIMEDSHOT
			BLACKARROW EXPLOSIVESHOT)

AddCheckBox(aoe L(aoe))
AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL))
AddCheckBox(blackarrow SpellName(BLACKARROW) mastery=3)

AddFunction SummonPet
{
	if PetPresent(no) Texture(ability_hunter_beastcall)
	if pet.Life(equal 0) Spell(REVIVEPET)
}

AddIcon help=main mastery=1
{
	#/serpent_sting,if=!ticking
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/kill_command
	if TargetInRange(GROWL) and PetPresent(yes) Spell(KILLCOMMAND)
	#/arcane_shot,if=focus>=59|buff.beast_within.up
	if Mana(more 59) or BuffPresent(THEBEASTWITHIN) Spell(ARCANESHOT)
	#/cobra_shot
	Spell(COBRASHOT)
	Spell(STEADYSHOT)
}

AddIcon help=offgcd mastery=1
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	Spell(CALLOFTHEWILD usable=1)
	#/focus_fire,five_stacks=1
	if PetPresent(yes) and pet.BuffPresent(FRENZYEFFECT stacks=5) Spell(FOCUSFIRE)
	#/bestial_wrath,if=focus>60
	if Mana(more 60) and PetPresent(yes) Spell(BESTIALWRATH)
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/serpent_sting,if=!ticking
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	#/cobra_shot,if=target.adds>5
	Spell(COBRASHOT)
}

AddIcon help=cd mastery=1
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	if TargetIsInterruptible() and TargetInRange(SILENCINGSHOT) Spell(SILENCINGSHOT)
	#/hunters_mark
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 20) Spell(HUNTERSMARK nored=1)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#/rapid_fire,if=!buff.bloodlust.up&!buff.beast_within.up
	unless BuffPresent(heroism) or BuffPresent(THEBEASTWITHIN) Spell(RAPIDFIRE)
	#/fervor,if=focus<=37
	if Mana(less 37) Spell(FERVOR)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=2
{
	#/serpent_sting,if=!ticking&target.health_pct<=90
	unless TargetDebuffPresent(SERPENTSTING) if TargetLifePercent(less 90) and Mana(more 24) Spell(SERPENTSTING)
	#/chimera_shot,if=target.health_pct<=90
	if TargetLifePercent(less 90) and Mana(more 49) Spell(CHIMERASHOT)
	#/steady_shot,if=buff.pre_improved_steady_shot.up&buff.improved_steady_shot.remains<3
	if TalentPoints(IMPROVEDSTEADYSHOT more 0) and BuffExpires(IMPROVEDSTEADYSHOT 3)
	{
		if Counter(iss equal 1) Spell(STEADYSHOT)
		if Counter(iss equal 3) Spell(STEADYSHOT)
		if Counter(iss equal 5) Spell(STEADYSHOT)
		if Counter(iss equal 7) Spell(STEADYSHOT)
		if Counter(iss equal 9) Spell(STEADYSHOT)
		# if you're shooting more than 9 Steady Shots in a row, you're capping your focus and doing it wrong
	}
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/aimed_shot,if=buff.master_marksman_fire.react
	if BuffPresent(FIRE) Spell(AIMEDSHOT)
	if ArmorSetParts(T13 more 3)
	{
		#/arcane_shot,if=(focus>=66|cooldown.chimera_shot.remains>=4)&\
		#	(target.health_pct<90&!buff.rapid_fire.up&!buff.bloodlust.react&!buff.berserking.up&\
		#		!buff.tier13_4pc.react&cooldown.buff_tier13_4pc.remains<=0)
		if ArmorSetParts(T13 more 3)
		{
			unless Mana(less 66) and 4s before Spell(CHIMERASHOT)
				unless TargetLifePercent(more 90) or BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or BuffPresent(CHRONOHUNTER)
					if BuffGain(CHRONOHUNTER 110) Spell(ARCANESHOT)
		}
		#/aimed_shot,if=(cooldown.chimera_shot.remains>5|focus>=80)&(buff.bloodlust.react|buff.tier13_4pc.react|cooldown.buff_tier13_4pc.remains>0)|\
		#	buff.rapid_fire.up|target.health_pct>90
		unless 5s before Spell(CHIMERASHOT) and Mana(less 80)
		{
			if BuffPresent(heroism) or BuffPresent(CHRONOHUNTER) if Mana(more 49) Spell(AIMEDSHOT)
			unless BuffGain(CHRONOHUNTER 110) if Mana(more 49) Spell(AIMEDSHOT)
		}
		if {BuffPresent(RAPIDFIRE) or TargetLifePercent(more 90)} and Mana(more 49) Spell(AIMEDSHOT)
	}
	unless ArmorSetParts(T13 more 3)
	{
		#if ( ! glyphs.arcane_shot -> ok() )
		unless Glyph(GLYPHOFARCANESHOT)
		{
			#/aimed_shot,if=cooldown.chimera_shot.remains>5|focus>=80|buff.rapid_fire.up|buff.bloodlust.react|target.health_pct>90
			unless 5s before Spell(CHIMERASHOT) if Mana(more 49) Spell(AIMEDSHOT)
			if Mana(more 79) Spell(AIMEDSHOT)
			if BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or TargetLifePercent(more 90) and Mana(more 49) Spell(AIMEDSHOT)
		}
		#else
		if Glyph(GLYPHOFARCANESHOT)
		{
			#/aimed_shot,if=target.health_pct>90|buff.rapid_fire.up|buff.bloodlust.react
			if BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or TargetLifePercent(more 90) and Mana(more 49) Spell(AIMEDSHOT)
			#/arcane_shot,if=(focus>=66|cooldown.chimera_shot.remains>=5)&(target.health_pct<90&!buff.rapid_fire.up&!buff.bloodlust.react)
			unless Mana(less 66) and 5s before Spell(CHIMERASHOT)
				if TargetLifePercent(less 90) unless BuffPresent(RAPIDFIRE) or BuffPresent(heroism) Spell(ARCANESHOT)
		}
	}
	#/steady_shot
	Spell(STEADYSHOT)
}

AddIcon help=offgcd mastery=2
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	Spell(CALLOFTHEWILD usable=1)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	#/steady_shot,if=target.adds>5
	Spell(STEADYSHOT)
}

AddIcon help=cd mastery=2
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	if TargetIsInterruptible() and TargetInRange(SILENCINGSHOT) Spell(SILENCINGSHOT)
	#/hunters_mark
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 20) Spell(HUNTERSMARK nored=1)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60|buff.rapid_fire.react
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#/rapid_fire,if=!buff.bloodlust.up|target.time_to_die<=30
	unless BuffPresent(heroism) and TargetDeadIn(more 30) Spell(RAPIDFIRE)
	#/readiness,wait_for_rapid_fire=1
	if BuffPresent(RAPIDFIRE) Spell(READINESS)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=3
{
	#/serpent_sting,if=!ticking
	unless TargetDebuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/explosive_shot,if=(remains<2.0)
	if TargetDebuffExpires(EXPLOSIVESHOT 2 mine=1) and Mana(more 49) Spell(EXPLOSIVESHOT)
	#/explosive_trap
	if CheckBoxOff(blackarrow)
	{
		if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
		if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
		if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	}
	if CheckBoxOn(blackarrow) unless TargetDebuffPresent(BLACKARROW mine=1) if Mana(more 34) Spell(BLACKARROW)
	#/kill_shot
	Spell(KILLSHOT usable=1)
	unless BuffPresent(LOCKANDLOAD)
	{
		#/arcane_shot,if=focus>=50&!buff.lock_and_load.react
		if ArmorSetParts(T13 more 1) Mana(more 50) Spell(ARCANESHOT)
		#/arcane_shot,if=focus>=70&!buff.lock_and_load.react (Hunter_SV_T11H, r10100)
		unless ArmorSetParts(T13 more 1) if Mana(more 70) Spell(ARCANESHOT)
	}
	#/cobra_shot
	Spell(COBRASHOT)
}

AddIcon help=offgcd mastery=3
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	Spell(CALLOFTHEWILD usable=1)
}

AddIcon help=aoe mastery=3 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER) and Mana(more 9) Spell(TRAPLAUNCHER)
	if Glyph(GLYPHOFTRAPLAUNCHER no) and Mana(more 19) Spell(TRAPLAUNCHER)
	#/multi_shot,if=target.adds>2
	if Mana(more 39) Spell(MULTISHOT)
	#/cobra_shot,if=target.adds>2
	Spell(COBRASHOT)
}

AddIcon help=cd mastery=3
{
	unless InCombat() SummonPet()

	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	if TargetIsInterruptible() and TargetInRange(SILENCINGSHOT) Spell(SILENCINGSHOT)
	#/hunters_mark
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 20) Spell(HUNTERSMARK nored=1)
	#/summon_pet
	SummonPet()
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60
	#if ( glyphs.rapid_fire -> ok() ) { |buff.rapid_fire.react }
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60) or BuffPresent(RAPIDFIRE)} Item(POTIONOFTHETOLVIR)
	if CheckBoxOn(potions) and Glyph(GLYPHOFRAPIDFIRE) and BuffPresent(RAPIDFIRE) Item(POTIONOFTHETOLVIR)
	#/rapid_fire
	Spell(RAPIDFIRE)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}