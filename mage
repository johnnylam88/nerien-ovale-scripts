# Mage Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3 16 2012/01/18 - Use a +/- icon instead of green/red icon to note whether the DPS(+) or DPM(-) rotation
#                         is currently optimal while in Arcane spec. Move the +/- icon between the DPS and DPM
#                         rotation icons to make it easier to see when to switch to the other rotation.
#                     Correct the conditions for using Mirror Image when in Arcane spec.
# 4.3.15 2012/01/17 - Check number of charges on Mana Gem before conjuring it in combat in Arcane spec.
#                     Suggest Evocation in the Arcane hover cycle as well.
# 4.3.14 2012/01/17 - Use Arcane Barrage appropriately by checking the number of stacks of Arcane Blast correctly.
#                     Fix when Arcane Blast is used in the hover cycle depending on 4pT13.
# 4.3.13 2012/01/17 - Use new item() function in Ovale 4.3.4 to replace InstantConjureManaGem() function.
# 4.3.12 2012/01/04 - manaPercent() returns a percent, not a percentage, so don't scale it.
# 4.3.11 2012/01/04 - Tighten up the conditions for when to cast Conjure Mana Gem in Arcane spec.
#                   - Minor bug fixes to Arcane rotation.
# 4.3.10 2012/01/03 - Add more checks for if player has 4pT13.
#                   - Fix the suggestions for what an Arcane mage casts while moving.
#                   - Mirror certain actions from the Arcane burn cycle to the hover cycle that were missed previously.
# 4.3.9  2011/12/21 - Fix condition for minor burn phase in Arcane rotation.
#                   - Update Frost rotation to SC, T13H (r10506).
# 4.3.8  2011/12/18 - 4.3.2 fixes the maxMana() problem, so put back the exact condition for Mana Gem usage.
#                     Checkbox for having just one main icon or separate icons for burn and hover phases with a green/red
#                     visual aid for which cycle to use.
#                     Use Item() as the check condition for when an item cooldown is up.
# 4.3.7  2011/12/16 - maxMana() causes Lua errors, so use ManaPercent() instead until it's fixed.
# 4.3.6  2011/12/16 - Don't suggest hardcasting Arcane Blast while moving.
# 4.3.5  2011/12/16 - Use the proper name for the potion being used in the checkbox.
# 4.3.4  2011/12/08 - Move Combustion to the cooldown icon.
# 4.3.3  2011/12/08 - Checkbox for using potions.
#                     Coding style changes.
# 4.3.2  2011/12/08 - Fix error in how MANAGEMITEM was used, which was causing it to never be suggested for arcane mages.
# 4.3.1  2011/12/06 - Initial version based on Hinalover's contributed script.
#                     Use mage profiles from SimulationCraft, Tier 13H:
#                     Arcane (r10466): 4 icons - green/red for burn/conserve phase, burn rotation, conserve rotation, DPS CDs
#                     Fire (r10467): 3 icons - main rotation, DPS CDs, mana CDs
#                     Frost (r10466): 3 icons - main rotation, DPS CDs, mana CDs; checkbox for using Kuni's Frost PvE rotation

## defines ##

# Buffs
Define(ARCANEBLASTDEBUFF 36032)
Define(ARCANEMISSILEBUFF 79683)
Define(BRAINFREEZE 57761) #frost (instant fireball/frostfire bolt)
Define(CLEARCASTING 12536)
Define(FINGERSOFFROST 44544) #frost boost ice lance/deep freeze
	SpellInfo(FINGERSOFFROST duration=14)
Define(IMPROVEDMANAGEM 83098) #arcane
Define(HOTSTREAK 48108) #fire instant pyroblast
Define(PRESENCEOFMIND 12043) #arcane next spell instant
Define(STOLENTIME 105785) # 2pT13 buff stacks up to 10 times

# Debuffs
Define(CRITICALMASS 22959)
Define(IGNITE 12654)
Define(SHADOWANDFLAME 17800)

# Glyphs
Define(GLYPHOFFROSTBOLT 56370)
Define(GLYPHOFFROSTFIRE 61205)

# Talents
Define(ARCANEPOWERTALENT 9186)
Define(CRITICALMASSTALENT 10541)
Define(FIRESTARTERTALENT 11431)
Define(IMPROVEDSCORCHTALENT 10547)

# Spells
Define(ARCANEBARRAGE 44425) #arcane instant
	SpellInfo(ARCANEBARRAGE cd=4)
	SpellAddDebuff(ARCANEBARRAGE ARCANEBLASTDEBUFF=0)
Define(ARCANEBLAST 30451) #arcane stacks*4 cost increased
	SpellAddDebuff(ARCANEBLAST ARCANEBLASTDEBUFF=10)
Define(ARCANEMISSILES 5143) #arcane channel
	SpellAddDebuff(ARCANEMISSILES ARCANEBLASTDEBUFF=0 ARCANEMISSILEBUFF=0)
Define(ARCANEPOWER 12042) #arcane cd
	SpellInfo(ARCANEPOWER cd=84)
	SpellAddBuff(ARCANEPOWER ARCANEPOWER=15)
Define(COLDSNAP 11958) #frost reset cd
	SpellInfo(COLDSNAP cd=384)
Define(COMBUSTION 11129) #fire cd consume dot
	SpellInfo(COMBUSTION cd=120)
Define(CONJUREMANAGEM 759)
	SpellInfo(CONJUREMANAGEM cd=10) #fake
Define(COUNTERSPELL 2139)
	SpellInfo(COUNTERSPELL cd=24)
Define(DEEPFREEZE 44572) #frost instant
	SpellInfo(DEEPFREEZE cd=30)
	SpellAddBuff(DEEPFREEZE FINGERSOFFROST=-1)
Define(EVOCATION 12051)
	SpellInfo(EVOCATION cd=240)
Define(FIREBALL 133) #fire 2.5
Define(FIREBLAST 2136) #fire instant
	SpellInfo(FIREBLAST cd=8)
Define(FLAMEORB 82731)
	SpellInfo(FLAMEORB cd=60)
Define(FROSTARMOR 7302)
	SpellAddBuff(FROSTARMOR FROSTARMOR=1800)
Define(FROSTBOLT 116) #frost
Define(FROSTFIREBOLT 44614) #frost+fire
	SpellAddTargetDebuff(FROSTFIREBOLT FROSTFIREBOLT=9)
	SpellAddBuff(FROSTFIREBOLT BRAINFREEZE=-1 FINGERSOFFROST=-1)
Define(ICELANCE 30455) #frost instant
	SpellAddBuff(ICELANCE FINGERSOFFROST=-1)
Define(ICYVEINS 12472) #frost cd
	SpellInfo(ICYVEINS cd=144)
Define(LIVINGBOMB 44457) #fire dot
	SpellAddTargetDebuff(LIVINGBOMB LIVINGBOMB=12)
Define(MAGEARMOR 6117)
	SpellAddBuff(MAGEARMOR MAGEARMOR=1800)
Define(MIRRORIMAGE 55342)
	SpellInfo(MIRRORIMAGE cd=180)
Define(MOLTENARMOR 30482)
	SpellAddBuff(MOLTENARMOR MOLTENARMOR=1800)
Define(PYROBLAST 11366) #fire dot
	SpellAddTargetDebuff(PYROBLAST PYROBLAST=12)
	SpellAddBuff(PYROBLAST HOTSTREAK=0)
Define(PYROBLASTBANG 92315)
	SpellAddTargetDebuff(PYROBLASTBANG PYROBLASTBANG=12)
	SpellAddBuff(PYROBLASTBANG HOTSTREAK=0)
Define(SCORCH 2948) #fire 1.5 (cast while moving with Firestarter talent)
	SpellInfo(SCORCH duration=30 talent=10541) # Critical Mass (Rank 3)
Define(SPELLSTEAL 30449)
Define(SUMMONWATERELEMENTAL 31687) #frost pet
	SpellInfo(SUMMONWATERELEMENTAL cd=180)

Define(PETFREEZE 33395) #Frost pet freeze ability
	SpellInfo(PETFREEZE cd=25)
	SpellAddBuff(PETFREEZE FINGERSOFFROST=2)

# Items
Define(MANAGEMITEM 36799)
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)

## end defines ##

ScoreSpells(SCORCH PYROBLAST LIVINGBOMB FROSTFIREBOLT FIREBALL SUMMONWATERELEMENTAL PETFREEZE FROSTBOLT ARCANEBLAST ARCANEMISSILES ARCANEBARRAGE
			DEEPFREEZE ICELANCE)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL))

# Separate out the full Arcane rotation into "burn" (DPS) rotation and "hover" (DPM)
# rotation to allow the player to choose when to enter each part of the rotation.
# Show a +/- icon between the two rotations to show which rotation is optimal, with
# "+" meaning use the burn (left) rotation, and "-" meaning to use the hover (right)
# rotation.
AddCheckBox(separate "Separate DPS/DPM rotations" mastery=1)

# Frost DPS Rotation from Kuni's Frost Mage PvE Guide:
#    http://www.mmo-champion.com/threads/813785-Kuni-s-Frost-Mage-PVE-Guide
AddCheckBox(kunifrost "Kuni's DPS Rotation" mastery=3)

# Arcane DPS rotation for maximum burst damage.
AddFunction ArcaneDPS
{
	unless InCombat() if BuffExpires(MAGEARMOR 400) and BuffExpires(FROSTARMOR 400) Spell(MAGEARMOR)

	#/mage_armor
	unless BuffPresent(MAGEARMOR) or BuffPresent(FROSTARMOR) Spell(MAGEARMOR)
	#/conjure_mana_gem,invulnerable=1,if=mana_gem_charges<3
	unless InCombat() if ItemCount(MANAGEMITEM less 3 charges=1) Spell(CONJUREMANAGEM)

	#/evocation,if=((max_mana>max_mana_nonproc&mana_pct_nonproc<=40)|(max_mana=max_mana_nonproc&mana_pct<=35))&target.time_to_die>10
	if ManaPercent(less 35) and TargetDeadIn(more 10) Spell(EVOCATION)
	#if ( set_bonus.tier13_4pc_caster() )
	#	/mana_gem,if=buff.arcane_blast.stack=4&buff.tier13_2pc.stack>=7&(cooldown.arcane_power.remains<=0|target.time_to_die<=50)
	#else
	#	/mana_gem,if=buff.arcane_blast.stack=4
	if ArmorSetParts(T13 more 3) and DebuffPresent(ARCANEBLASTDEBUFF stacks=4)
		if BuffPresent(STOLENTIME stacks=7) and Spell(ARCANEPOWER) and TargetDeadIn(less 50)
			Spell(MANAGEMITEM)
	unless ArmorSetParts(T13 more 3) if DebuffPresent(ARCANEBLASTDEBUFF stacks=4) Item(MANAGEMITEM)
	#/conjure_mana_gem,if=buff.presence_of_mind.up&target.time_to_die>cooldown.mana_gem.remains&mana_gem_charges=0
	if BuffPresent(PRESENCEOFMIND) and {target.timeToDie() > item(MANAGEMITEM)} and ItemCount(MANAGEMITEM equal 0 charges=1) Spell(CONJUREMANAGEM)
	if Speed(more 0)
	{
		#/arcane_barrage,moving=1
		Spell(ARCANEBARRAGE)
		#/fire_blast,moving=1
		Spell(FIREBLAST)
		#/ice_lance,moving=1
		Spell(ICELANCE)
	}
	#/arcane_blast,if=buff.presence_of_mind.up
	if BuffPresent(PRESENCEOFMIND) Spell(ARCANEBLAST)
	#/arcane_blast,if=dps=1
	Spell(ARCANEBLAST)
}

# Arcane DPM rotation for mana-efficient damage by keeping mana level high
# to make use of mastery.
AddFunction ArcaneDPM
{
	unless InCombat() if BuffExpires(MAGEARMOR 400) and BuffExpires(FROSTARMOR 400) Spell(MAGEARMOR)

	#/mage_armor
	unless BuffPresent(MAGEARMOR) or BuffPresent(FROSTARMOR) Spell(MAGEARMOR)
	#/conjure_mana_gem,invulnerable=1,if=mana_gem_charges<3
	unless InCombat() if ItemCount(MANAGEMITEM less 3 charges=1) Spell(CONJUREMANAGEM)

	#/evocation,if=((max_mana>max_mana_nonproc&mana_pct_nonproc<=40)|(max_mana=max_mana_nonproc&mana_pct<=35))&target.time_to_die>10
	if ManaPercent(less 35) and TargetDeadIn(more 10) Spell(EVOCATION)
	#if ( set_bonus.tier13_4pc_caster() )
	#	/mana_gem,if=buff.arcane_blast.stack=4&buff.tier13_2pc.stack>=7&(cooldown.arcane_power.remains<=0|target.time_to_die<=50)
	#else
	#	/mana_gem,if=buff.arcane_blast.stack=4
	if ArmorSetParts(T13 more 3) and DebuffPresent(ARCANEBLASTDEBUFF stacks=4)
		if BuffPresent(STOLENTIME stacks=7) and Spell(ARCANEPOWER) and TargetDeadIn(less 50)
			Spell(MANAGEMITEM)
	unless ArmorSetParts(T13 more 3) if DebuffPresent(ARCANEBLASTDEBUFF stacks=4) Item(MANAGEMITEM)
	#/conjure_mana_gem,if=buff.presence_of_mind.up&target.time_to_die>cooldown.mana_gem.remains&mana_gem_charges=0
	if BuffPresent(PRESENCEOFMIND) and {target.timeToDie() > item(MANAGEMITEM)} and ItemCount(MANAGEMITEM equal 0 charges=1) Spell(CONJUREMANAGEM)
	if Speed(more 0)
	{
		#/arcane_barrage,moving=1
		Spell(ARCANEBARRAGE)
		#/fire_blast,moving=1
		Spell(FIREBLAST)
		#/ice_lance,moving=1
		Spell(ICELANCE)
	}
	#/arcane_blast,if=buff.presence_of_mind.up
	if BuffPresent(PRESENCEOFMIND) Spell(ARCANEBLAST)
	#if ( set_bonus.tier13_4pc_caster() )
	#	/arcane_blast,if=target.time_to_die<20|\
	#		((cooldown.evocation.remains<=20|buff.improved_mana_gem.up|cooldown.mana_gem.remains<5)&mana_pct>=22)|\
	#		(buff.arcane_power.up&mana_pct_nonproc>88)
	#else
	#	/arcane_blast,if=target.time_to_die<20|\
	#		((cooldown.evocation.remains<=20|buff.improved_mana_gem.up|cooldown.mana_gem.remains<5)&mana_pct>=22)
	if TargetDeadIn(less 20) Spell(ARCANEBLAST)
	if {20s before Spell(EVOCATION) or BuffPresent(IMPROVEDMANAGEM) or 5s before Item(MANAGEMITEM)} and ManaPercent(more 22)
	{
		if ArmorSetParts(T13 more 3) and BuffPresent(ARCANEPOWER) and ManaPercent(more 88) Spell(ARCANEBLAST)
		unless ArmorSetParts(T13 more 3) Spell(ARCANEBLAST)
	}
	#/arcane_blast,if=buff.arcane_blast.remains<0.8&buff.arcane_blast.stack=4
	unless Speed(more 0) if DebuffExpires(ARCANEBLASTDEBUFF 0.8 stacks=4) Spell(ARCANEBLAST)
	#/arcane_missiles,if=mana_pct_nonproc<92&buff.arcane_missiles.react&mage_armor_timer<=2
	#/arcane_missiles,if=mana_pct_nonproc<93&buff.arcane_missiles.react&mage_armor_timer>2
	if ManaPercent(less 93) and BuffPresent(ARCANEMISSILEBUFF) Spell(ARCANEMISSILES)
	#/arcane_barrage,if=mana_pct_nonproc<87&buff.arcane_blast.stack=2
	if ManaPercent(less 87) and DebuffPresent(ARCANEBLASTDEBUFF stacks=2) unless DebuffPresent(ARCANEBLASTDEBUFF stacks=3) Spell(ARCANEBARRAGE)
	#/arcane_barrage,if=mana_pct_nonproc<90&buff.arcane_blast.stack=3
	if ManaPercent(less 90) and DebuffPresent(ARCANEBLASTDEBUFF stacks=3) unless DebuffPresent(ARCANEBLASTDEBUFF stacks=4) Spell(ARCANEBARRAGE)
	#/arcane_barrage,if=mana_pct_nonproc<92&buff.arcane_blast.stack=4
	if ManaPercent(less 92) and DebuffPresent(ARCANEBLASTDEBUFF stacks=4) Spell(ARCANEBARRAGE)
	#/arcane_blast
	Spell(ARCANEBLAST)
}

# Complete Arcane spell rotation.
AddIcon help=main mastery=1 checkboxoff=separate
{
	#/choose_rotation,cooldown=1,force_dps=1,if=buff.improved_mana_gem.up&dpm=1&\
	#    cooldown.evocation.remains+15<target.time_to_die
	if BuffPresent(IMPROVEDMANAGEM) and {spell(EVOCATION) + 15 < target.timeToDie()}
		ArcaneDPS()

	#/choose_rotation,cooldown=1,force_dpm=1,if=cooldown.evocation.remains<=20&dps=1&mana_pct<22&\
	#    (target.time_to_die>60|burn_mps*((target.time_to_die-5)-cooldown.evocation.remains)>max_mana_nonproc)&\
	#    cooldown.evocation.remains+15<target.time_to_die
	if 20s before Spell(EVOCATION) and ManaPercent(less 22)
		and {TargetDeadIn(more 60) or {spell(EVOCATION) > player.manaPercent()}}
		and {spell(EVOCATION) + 15 < target.timeToDie()}
		ArcaneDPM()

	#/choose_rotation,cooldown=1,evocation_pct=35,if=cooldown.evocation.remains+15>target.time_to_die,final_burn_offset=15
	if 20s before Spell(EVOCATION) and ManaPercent(more 35)
		ArcaneDPS()

	ArcaneDPM()
}

# Separate DPS "burn" rotation
AddIcon help=main mastery=1 checkboxon=separate
{
	ArcaneDPS()
}

# Copy of the complete Arcane spell rotation but with textures instead of functions.
# "+" is for DPS rotation: Texture(Spell_chargepositive)
# "-" is for DPM rotation: Texture(Spell_chargenegative)
AddIcon mastery=1 checkboxon=separate
{
	#/choose_rotation,cooldown=1,force_dps=1,if=buff.improved_mana_gem.up&dpm=1&\
	#    cooldown.evocation.remains+15<target.time_to_die
	if BuffPresent(IMPROVEDMANAGEM) and {spell(EVOCATION) + 15 < target.timeToDie()}
		Texture(Spell_chargepositive)

	#/choose_rotation,cooldown=1,force_dpm=1,if=cooldown.evocation.remains<=20&dps=1&mana_pct<22&\
	#    (target.time_to_die>60|burn_mps*((target.time_to_die-5)-cooldown.evocation.remains)>max_mana_nonproc)&\
	#    cooldown.evocation.remains+15<target.time_to_die
	if 20s before Spell(EVOCATION) and ManaPercent(less 22)
		and {TargetDeadIn(more 60) or {spell(EVOCATION) > player.manaPercent()}}
		and {spell(EVOCATION) + 15 < target.timeToDie()}
		Texture(Spell_chargenegative)

	#/choose_rotation,cooldown=1,evocation_pct=35,if=cooldown.evocation.remains+15>target.time_to_die,final_burn_offset=15
	if 20s before Spell(EVOCATION) and ManaPercent(more 35)
		Texture(Spell_chargepositive)

	Texture(Spell_chargenegative)
}

# Separate DPM "hover" rotation
AddIcon help=main mastery=1 checkboxon=separate
{
	ArcaneDPM()
}

AddIcon help=cd mastery=1
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if TargetBuffStealable(yes) Spell(SPELLSTEAL)
	#/counterspell
	if TargetIsInterruptible(yes) Spell(COUNTERSPELL)
	#/volcanic_potion,if=buff.improved_mana_gem.up|target.time_to_die<=50
	if CheckBoxOn(potions) and {BuffPresent(IMPROVEDMANAGEM) or TargetDeadIn(less 51)} Item(VOLCANICPOTION)
	#/use_item,name=shard_of_woe,if=buff.improved_mana_gem.up|cooldown.evocation.remains>90|target.time_to_die<=50
	#if BuffPresent(IMPROVEDMANAGEM) or {spell(EVOCATION) > 90} or TargetDeadIn(less 51) Item(SHARDOFWOE)
	#/flame_orb,if=target.time_to_die>=10
	if TargetDeadIn(more 9) Spell(FLAMEORB)
	#if ( talents.arcane_power -> rank() )
	#	if ( set_bonus.tier13_4pc_caster() )
	#		/arcane_power,if=(buff.improved_mana_gem.up&buff.tier13_2pc.stack>=9)|\
	#			(buff.tier13_2pc.stack>=10&cooldown.mana_gem.remains>30&cooldown.evocation.remains>10)|\
	#			target.time_to_die<=50
	#	else
	#		/arcane_power,if=buff.improved_mana_gem.up|target.time_to_die<=50
	#	/mirror_image,if=buff.arcane_power.up|(cooldown.arcane_power.remains>20&target.time_to_die>15)
	#else
	#	/mirror_image
	if TalentPoints(ARCANEPOWERTALENT more 0)
	{
		if TargetDeadIn(less 51) Spell(ARCANEPOWER)
		if TalentPoints(ARCANEPOWERTALENT more 0)
		{
			if BuffPresent(IMPROVEDMANAGEM) and BuffPresent(STOLENTIME stacks=9) Spell(ARCANEPOWER)
			if BuffPresent(STOLENTIME stacks=10)
				unless 30s before Item(MANAGEMITEM) or 10s before Spell(EVOCATION) Spell(ARCANEPOWER)
		}
		unless TalentPoints(ARCANEPOWERTALENT more 0) if BuffPresent(IMPROVEDMANAGEM) Spell(ARCANEPOWER)
		if BuffPresent(ARCANEPOWER) Spell(MIRRORIMAGE)
		unless 20s before Spell(ARCANEPPOWER) or TargetDeadIn(less 15) Spell(MIRRORIMAGE)
	}
	unless TalentPoints(ARCANEPOWERTALENT more 0) Spell(MIRRORIMAGE)
	#/presence_of_mind
	Spell(PRESENCEOFMIND)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=2
{
	#/scorch,debuff=1
	if TalentPoints(CRITICALMASSTALENT more 0) and TargetDebuffExpires(magicalcrittaken 6) Spell(SCORCH)
	#/living_bomb,if=!ticking
	if TargetDebuffExpires(LIVINGBOMB 0 mine=1) and TargetDeadIn(more 12) Spell(LIVINGBOMB)
	#/pyroblast_hs,if=buff.hot_streak.react
	if BuffPresent(HOTSTREAK) Spell(PYROBLAST)
	if TalentPoints(FIRESTARTERTALENT more 0) and Speed(more 0) Spell(SCORCH)
	#if ( glyphs.frostfire -> ok() ) { /frostfire_bolt }
	if Glyph(GLYPHOFFROSTFIRE) Spell(FROSTFIREBOLT usable=1)
	#/fireball
	Spell(FIREBALL usable=1)
	#/scorch
	Spell(SCORCH)
}

AddIcon help=cd mastery=2
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if TargetBuffStealable(yes) Spell(SPELLSTEAL)
	#/counterspell
	if TargetIsInterruptible(yes) Spell(COUNTERSPELL)
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 40)} Item(VOLCANICPOTION)
	#/combustion,if=dot.living_bomb.ticking&dot.ignite.ticking&dot.pyroblast.ticking
	if TargetDebuffPresent(LIVINGBOMB mine=1) and TargetDebuffPresent(IGNITE mine=1)
		and {TargetDebuffPresent(PYROBLAST mine=1) or TargetDebuffPresent(PYROBLASTBANG mine=1)}
		Spell(COMBUSTION)
	#/mirror_image,if=target.time_to_die>=25
	if TargetDeadIn(more 24) Spell(MIRRORIMAGE)
	#/flame_orb,if=target.time_to_die>=12
	if TargetDeadIn(more 11) Spell(FLAMEORB)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=mana mastery=2
{
	unless InCombat() if BuffExpires(MAGEARMOR 400) and BuffExpires(MOLTENARMOR 400) and BuffExpires(FROSTARMOR 400) Spell(MOLTENARMOR)

	#/molten_armor,if=buff.mage_armor.down&buff.molten_armor.down
	unless BuffPresent(MAGEARMOR) or BuffPresent(MOLTENARMOR) or BuffPresent(FROSTARMOR) Spell(MOLTENARMOR)
	#/molten_armor,if=mana_pct>45&buff.mage_armor.up
	if BuffPresent(MAGEARMOR) and ManaPercent(more 45) Spell(MOLTENARMOR)
	#/conjure_mana_gem,invulnerable=1,if=mana_gem_charges<3
	unless InCombat() if ItemCount(MANAGEMITEM less 3 charges=1) Spell(CONJUREMANAGEM)

	# If we can't use Scorch as a filler, then use Evocation like other mage specs.
	# /evocation,if=mana_pct<40&buff.bloodlust.react
	if TalentPoints(FIRESTARTERTALENT less 1)
		and ManaPercent(less 40) and {BuffExpires(heroism 1.5) or BuffExpires(ICYVEINS 1.5)}
		Spell(EVOCATION)

	#/mana_gem,if=mana_deficit>12500
	if player.maxMana() - player.mana() > 12500 Item(MANAGEMITEM)
	#/mage_armor,if=mana_pct<5&buff.mage_armor.down
	if BuffExpires(MAGEARMOR 0) and ManaPercent(less 5) Spell(MAGEARMOR)
}

AddIcon help=main mastery=3
{
	if PetPresent(no) Spell(SUMMONWATERELEMENTAL)

	#/deep_freeze
	Spell(DEEPFREEZE usable=1)
	#/frostfire_bolt,if=buff.brain_freeze.react
	if BuffPresent(BRAINFREEZE) {Spell(FROSTFIREBOLT) Spell(FIREBALL)}
	if CheckBoxOff(kunifrost)
	{
		#/ice_lance,if=buff.fingers_of_frost.stack>1
		if BuffPresent(FINGERSOFFROST stacks=2) Spell(ICELANCE)
		#/ice_lance,if=buff.fingers_of_frost.react&pet.water_elemental.cooldown.freeze.remains<gcd
		if BuffPresent(FINGERSOFFROST) and {spell(PETFREEZE) < timeWithHaste(1.5)} Spell(ICELANCE)
		unless BuffPresent(FINGERSOFFROST) Spell(PETFREEZE)
	}
	if CheckBoxOn(kunifrost)
	{
		if BuffPresent(FINGERSOFFROST) Spell(ICELANCE)
		if 1s before Spell(DEEPFREEZE) unless BuffPresent(FINGERSOFFROST) Spell(PETFREEZE)
	}
	if Speed(more 0)
	{
		#/ice_lance,moving=1
		Spell(ICELANCE)
		#/fire_blast,moving=1
		Spell(FIREBLAST)
	}
	#/frostbolt
	if CheckBoxOn(kunifrost) and {castTime(FROSTFIREBOLT) < 1.251} Spell(FROSTFIREBOLT)
	if Glyph(GLYPHOFFROSTBOLT) Spell(FROSTBOLT)
	unless Glyph(GLYPHOFFROSTBOLT)
	{
		#/frostbolt,if=!cooldown.early_frost.remains
		if castTime(FROSTBOLT) < timeWithHaste(1.5) Spell(FROSTBOLT)
		#/frostfire_bolt
		Spell(FROSTFIREBOLT)
		Spell(FROSTBOLT)
	}
}

AddIcon help=cd mastery=3
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if TargetBuffStealable(yes) Spell(SPELLSTEAL)
	#/counterspell
	if TargetIsInterruptible(yes) Spell(COUNTERSPELL)
	#/volcanic_potion,if=buff.bloodlust.react|buff.icy_veins.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or BuffPresent(ICYVEINS) or TargetDeadIn(less 40)} Item(VOLCANICPOTION)
	#/cold_snap,if=cooldown.deep_freeze.remains>15&cooldown.frostfire_orb.remains>30&cooldown.icy_veins.remains>30
	unless 15s before Spell(DEEPFREEZE) or 30s before Spell(FLAMEORB) or 30s before Spell(ICYVEINS) Spell(COLDSNAP)
	#/frostfire_orb,if=target.time_to_die>=12
	if TargetDeadIn(more 11) Spell(FLAMEORB)
	#/mirror_image,if=target.time_to_die>=25
	if TargetDeadIn(more 24) Spell(MIRRORIMAGE)
	#/icy_veins,if=buff.icy_veins.down&buff.bloodlust.down&(buff.tier13_2pc.stack>7|cooldown.cold_snap.remains<22)\
	unless BuffPresent(ICYVEINS) or BuffPresent(heroism)
		if ArmorSetParts(T13 more 3)
			if BuffPresent(STOLENTIME stacks=7) or 22s before Spell(COLDSNAP) Spell(ICYVEINS)
	if BuffExpires(ICYVEINS 0) and BuffExpires(heroism 0) Spell(ICYVEINS)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=mana mastery=3
{
	unless InCombat() if BuffExpires(MAGEARMOR 400) and BuffExpires(MOLTENARMOR 400) and BuffExpires(FROSTARMOR 400) Spell(MOLTENARMOR)

	#/conjure_mana_gem,invulnerable=1,if=mana_gem_charges<3
	unless InCombat() if ItemCount(MANAGEMITEM less 3 charges=1) Spell(CONJUREMANAGEM)
	#/molten_armor,if=buff.mage_armor.down&buff.molten_armor.down
	unless BuffPresent(MAGEARMOR) or BuffPresent(MOLTENARMOR) or BuffPresent(FROSTARMOR) Spell(MOLTENARMOR)
	#/molten_armor,if=mana_pct>45&buff.mage_armor.up (r8877)
	if BuffPresent(MAGEARMOR) and ManaPercent(more 45) Spell(MOLTENARMOR)

	#/evocation,if=mana_pct<40&(buff.icy_veins.react|buff.bloodlust.react)
	if ManaPercent(less 40) and {BuffExpires(heroism 1.5) or BuffExpires(ICYVEINS 1.5)} Spell(EVOCATION)
	#/mana_gem,if=mana_deficit>12500
	if player.maxMana() - player.mana() > 12500 Item(MANAGEMITEM)
	#/mage_armor,if=(mana_pct*12)<target.time_to_die (r8877)
	if Glyph(GLYPHOFFROSTBOLT) and BuffPresent(MOLTENARMOR) and {{manaPercent()*12} < target.timeToDie()} Spell(MAGEARMOR)
	#/mage_armor,if=(mana_pct*15)<target.time_to_die (r8877)
	if Glyph(GLYPHOFFROSTBOLT no) and BuffPresent(MOLTENARMOR) and {{manaPercent()*15} < target.timeToDie()} Spell(MAGEARMOR)
}