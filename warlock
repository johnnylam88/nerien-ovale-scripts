### Warlock Ovale script based on SimulationCraft profiles
### https://sites.google.com/site/wownerien/ovale/warlock/script
### Version: 4.3.1pre
#
# Affliction:
#	talents=http://www.wowhead.com/talent#warlock-22322200301332132103000000000000000003300000000000000000
#	glyphs=haunt/corruption/unstable_affliction/life_tap/shadow_bolt
#
#	- If using Succubus, then macro Demon Soul into Shadow Bolt.
#	- If Bane talent is maxed, then macro Soulburn into Soul Fire.
# 	- If Bane talent isn't maxed, then macro Soulburn into Drain Life.
#
# Demonology:
#	talents=http://www.wowhead.com/talent#warlock-00300000000000000033212223003102122110320200000000000000
#	glyphs=metamorphosis/immolate/incinerate/life_tap/shadow_bolt
#
# Destruction:
#	talents=http://www.wowhead.com/talent#warlock-00300000000000000003202000000000000003320202312201312211
#	glyphs=immolate/imp/conflagrate/life_tap
#
#	- Soulburn should be macroed into Soul Fire.
#
# All specs:
#	- Potions, Demon Soul, and on-use trinkets should be macroed into Summon Doomguard/Infernal.

## defines ##

# Localization: replace the following definitions with the corresponding localized name for warlock pet families.
# English
Define(PETFELGUARD Felguard)
Define(PETFELHUNTER Felhunter)
Define(PETIMP Imp)
Define(PETSUCCUBUS Succubus)

# Buffs
Define(DARKINTENTBUFF 85768)
Define(DECIMATION 63167)
Define(DEMONSOULFELGUARD 79462)
Define(DEMONSOULFELHUNTER 79460)
Define(DEMONSOULIMP 79459)
Define(DEMONSOULSUCCUBUS 79463)
Define(DEMONSOULVOIDWALKER 79464)
Define(EMPOWEREDIMP 47283)
Define(FELSPARK 89937) # 4pT11 proc
Define(IMPROVEDSOULFIREBUFF 85383)
Define(MOLTENCORE 71165)
Define(SHADOWTRANCE 17941)
Define(SHADOWANDFLAMEDEBUFF 17800)

# Buff Procs
SpellList(COMBATMIND 107970 109793 109795) # Will of Unbinding stacking int buff
Define(FOULGIFT 102662) # Foul Gift of the Demon Lord +1149 mastery proc
Define(LIGHTWEAVE 75170) # +580 int tailoring proc
Define(POWERTORRENT 74241) # +500 int weapon enchant proc
SpellList(VELOCITY 107982 109787 109789) # Insignia of the Corrupted Mind haste proc
Define(VOLCANICDESTRUCTION 89091) # DMC: Volcano +1600 int proc

# Talents
Define(BANETALENT 10938)
Define(EMBERSTORMTALENT 11181)
Define(IMPROVEDSOULFIRETALENT 11197)
Define(METAMORPHOSISTALENT ???)
Define(SHADOWANDFLAMETALENT 10936)
Define(SOULSIPHONTALENT 11112)

# Glyphs
Define(GLYPHOFLASHOFPAIN 70947)
Define(GLYPHOFIMP 56248)
Define(GLYPHOFMETAMORPHOSIS 64318)

# Spells
Define(BANEOFAGONY 980)
	Spellinfo(BANEOFAGONY duration=24)
	SpellAddTargetDebuff(BANEOFAGONY BANEOFAGONY=24)
Define(BANEOFDOOM 603)
	SpellInfo(BANEOFDOOM duration=60)
	SpellAddTargetDebuff(BANEOFDOOM BANEOFDOOM=60)
Define(BANEOFHAVOC 80240)
	SpellAddTargetDebuff(BANEOFHAVOC BANEOFHAVOC=300)
Define(CHAOSBOLT 50796)
	SpellInfo(CHAOSBOLT cd=12)
Define(CONFLAGRATE 17962)
	SpellInfo(CONFLAGRATE cd=10)
Define(CORRUPTION 172)
	SpellAddTargetDebuff(CORRUPTION CORRUPTION=18)
Define(CREATEHEALTHSTONE 6201)
Define(CURSEOFELEMENTS 1490)
	SpellAddTargetDebuff(CURSEOFELEMENTS CURSEOFELEMENTS=300)
Define(CURSEOFTONGUES 1714)
	SpellAddTargetDebuff(CURSEOFTONGUES CURSEOFTONGUES=30)
Define(CURSEOFWEAKNESS 702)
	SpellAddTargetDebuff(CURSEOFWEAKNESS CURSEOFWEAKNESS=120)
Define(DARKINTENT 80398)
	SpellAddBuff(DARKINTENT DARKINTENTBUFF=1800)
Define(DEATHCOIL 6789)
	SpellInfo(DEATHCOIL cd=120)
Define(DEMONARMOR 687)
	SpellAddBuff(DEMONARMOR DEMONARMOR=1000)
Define(DEMONICEMPOWERMENT 47193)
	SpellInfo(DEMONICEMPOWERMENT cd=60)
Define(DEMONSOUL 77801)
	SpellInfo(DEMONSOUL cd=120)
Define(DRAINLIFE 689)
	SpellInfo(DRAINLIFE canStopChannelling=3)
	SpellAddTargetDebuff(DRAINLIFE DRAINLIFE=3)
	SpellAddBuff(DRAINLIFE SOULBURN=0)
Define(DRAINSOUL 1120)
	SpellInfo(DRAINSOUL canStopChannelling=5)
	SpellAddTargetDebuff(DRAINSOUL DRAINSOUL=15)
Define(FELARMOR 28176)
	SpellAddBuff(FELARMOR FELARMOR=1000)
Define(FELFLAME 77799)
	SpellAddTargetDebuff(FELFLAME IMMOLATE=refresh UNSTABLEAFFLICTION=refresh)
Define(HANDOFGULDAN 71521)
	SpellInfo(HANDOFGULDAN cd=12)
	SpellAddTargetDebuff(HANDOFGULDAN IMMOLATE=refresh)
Define(HAUNT 48181)
	SpellInfo(HAUNT cd=8)
	SpellAddBuff(HAUNT HAUNT=12)
Define(IMMOLATE 348)
	SpellInfo(IMMOLATE duration=15)
	SpellAddTargetDebuff(IMMOLATE IMMOLATE=15)
	SpellAddBuff(IMMOLATE MOLTENCORE=-1)
Define(IMMOLATIONAURA 50589)
	SpellInfo(IMMOLATIONAURA cd=30)
	SpellAddBuff(IMMOLATIONAURA IMMOLATIONAURA=15)
Define(INCINERATE 29722)
	SpellAddBuff(INCINERATE MOLTENCORE=-1)
Define(LIFETAP 1454)
	SpellInfo(LIFETAP cd=10) #fake
Define(METAMORPHOSIS 47241)
	SpellInfo(METAMORPHOSIS cd=180)
	SpellAddBuff(METAMORPHOSIS METAMORPHOSIS=30)
Define(NETHERWARD 91711)
Define(RITUALOFSOULS 29893)
	SpellInfo(RITUALOFSOULS cd=360)
Define(SEARINGPAIN 5676)
Define(SEEDOFCORRUPTION 27243)
	SpellAddTargetDebuff(SEEDOFCORRUPTION SEEDOFCORRUPTION=18)
Define(SHADOWBOLT 686)
	SpellAddTargetDebuff(SHADOWBOLT SHADOWEMBRACE=12 SHADOWANDFLAMEDEBUFF=30)
Define(SHADOWBURN 17877)
	SpellInfo(SHADOWBURN cd=15)
Define(SHADOWFLAME 47897)
	SpellInfo(SHADOWFLAME cd=12 range=10)
Define(SOULFIRE 6353)
	SpellAddBuff(SOULFIRE IMPROVEDSOULFIREBUFF=15 SOULBURN=0)
Define(SOULBURN 74434)
	SpellInfo(SOULBURN cd=45)
	SpellAddBuff(SOULBURN SOULBURN=15)
Define(SOULHARVEST 79268)
	SpellInfo(SOULHARVEST cd=30 duration=9)
Define(SUMMONINFERNAL 1122)
	SpellInfo(SUMMONINFERNAL cd=600)
Define(SUMMONDOOMGUARD 18540)
	SpellInfo(SUMMONDOOMGUARD cd=600)
Define(SUMMONFELGUARD 30146)
	SpellInfo(SUMMONFELGUARD cd=600) #fake
	SpellAddBuff(SUMMONFELGUARD SOULBURN=0)
Define(SUMMONFELHUNTER 691)
	SpellInfo(SUMMONFELHUNTER cd=600) #fake
	SpellAddBuff(SUMMONFELHUNTER SOULBURN=0)
Define(SUMMONIMP 688)
	SpellInfo(SUMMONIMP cd=600) #fake
	SpellAddBuff(SUMMONIMP SOULBURN=0)
Define(SUMMONSUCCUBUS 712)
	SpellInfo(SUMMONSUCCUBUS cd=600) #fake
	SpellAddBuff(SUMMONSUCCUBUS SOULBURN=0)
Define(UNSTABLEAFFLICTION 30108)
	SpellInfo(UNSTABLEAFFLICTION duration=15)
	SpellAddTargetDebuff(UNSTABLEAFFLICTION UNSTABLEAFFLICTION=15)

# Pet Spells
Define(PETAXETOSS 89766)
	SpellInfo(PETAXETOSS cd=30)
Define(PETFELSTORM 89751)
	SpellInfo(PETFELSTORM cd=45) #lasts 6s
Define(PETLEGIONSTRIKE 30213)
Define(PETPURSUIT 30151)
	SpellInfo(PETPURSUIT cd=15)
Define(PETSINGEMAGIC 89808)
	SpellInfo(PETSINGEMAGIC cd=6)

# Racials & Professions
Define(ARCANETORRENTMANA 28730) # blood elf
	SpellInfo(ARCANETORRENTMANA sharedcd=racial cd=120)
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(BLOODFURY 33697) # orc
	SpellInfo(BLOODFURY duration=15 cd=120)
	SpellAddBuff(BLOODFURY BLOODFURY=15)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellAddBuff(STONEFORM STONEFORM=8)

# Items
Define(BOTTLEDWISHES 77114)
	Define(ULTIMATEPOWER 107948) # +2290 sp
Define(HEALTHSTONE 5512)
Define(MOONWELLCHALICE 70142)
	Define(BLESSINGOFTHEMOONWELL 100403) # +1700 mastery
Define(SOULCASKET 58183)
	Define(SOULPOWER 91019) # +1926 sp
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)
	Define(VOLCANICPOWER 79476) # +1200 int

## end defines ##

ScoreSpells(CURSEOFELEMENTS SHADOWBOLT HAUNT UNSTABLEAFFLICTION IMMOLATE CONFLAGRATE CURSEOFWEAKNESS
			BANEOFAGONY CORRUPTION SOULFIRE DRAINSOUL INCINERATE SHADOWBOLT CHAOSBOLT)

AddListItem(curse none L(None))
AddListItem(curse elements SpellName(CURSEOFELEMENTS) default)
AddListItem(curse tongues SpellName(CURSEOFTONGUES))
AddListItem(curse weakness SpellName(CURSEOFWEAKNESS))
AddListItem(bane none L(None))
AddListItem(bane agony SpellName(BANEOFAGONY))
AddListItem(bane doom SpellName(BANEOFDOOM) default)
AddListItem(bane havoc SpellName(BANEOFHAVOC) mastery=3)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) default)
AddCheckBox(lightweave SpellName(LIGHTWEAVE))
AddCheckBox(powertorrent SpellName(POWERTORRENT))

# Trinkets from T13 raids or Twilight heroics or purchased with Valor or Justice Points.
AddCheckBox(trinket_bottle "Bottled Wishes")
AddCheckBox(trinket_dmcv "DMC: Volcano")
AddCheckBox(trinket_foul "Foul Gift of the Demon Lord")
AddCheckBox(trinket_iotcm "Insignia of the Corrupted Mind")
AddCheckBox(trinket_mwc "Moonwell Chalice")
AddCheckBox(trinket_soul "Soul Casket")
AddCheckBox(trinket_wou "Will of Unbinding")

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	if ManaPercent(less 91) Spell(ARCANETORRENTMANA)
	Spell(BERSERKING)
	Spell(BLOODFURY)
}

AddFunction EnoughBuffsForDoomguard
{
	# "enough buffs" means:
	#	- potion
	#	- at least two proc effects
	#	- at least one on-use trinket effect
	#	- at least 8 stacks from Will of Unbinding
	#
	{CheckBoxOn(potions) and {{ItemCount(VOLCANICPOTION more 0) and Item(VOLCANICPOTION)} or BuffPresent(VOLCANICPOWER)}}
	and {{{CheckBoxOn(lightweave) and BuffPresent(LIGHTWEAVE)} and {CheckBoxOn(powertorrent) and BuffPresent(POWERTORRENT)}}
		or {{CheckBoxOn(lightweave) and BuffPresent(LIGHTWEAVE)} and {CheckBoxOn(trinket_dmcv) and BuffPresent(VOLCANICDESTRUCTION)}}
		or {{CheckBoxOn(lightweave) and BuffPresent(LIGHTWEAVE)} and {CheckBoxOn(trinket_foul) and BuffPresent(FOULGIFT)}}
		or {{CheckBoxOn(lightweave) and BuffPresent(LIGHTWEAVE)} and {CheckBoxOn(trinket_iotcm) and BuffPresent(VELOCITY)}}
		or {{CheckBoxOn(powertorrent) and BuffPresent(POWERTORRENT)} and {CheckBoxOn(trinket_dmcv) and BuffPresent(VOLCANICDESTRUCTION)}}
		or {{CheckBoxOn(powertorrent) and BuffPresent(POWERTORRENT)} and {CheckBoxOn(trinket_foul) and BuffPresent(FOULGIFT)}}
		or {{CheckBoxOn(powertorrent) and BuffPresent(POWERTORRENT)} and {CheckBoxOn(trinket_iotcm) and BuffPresent(VELOCITY)}}
		or {{CheckBoxOn(trinket_dmcv) and BuffPresent(VOLCANICDESTRUCTION)} and {CheckBoxOn(trinket_foul) and BuffPresent(FOULGIFT)}}
		or {{CheckBoxOn(trinket_dmcv) and BuffPresent(VOLCANICDESTRUCTION)} and {CheckBoxOn(trinket_iotcm) and BuffPresent(VELOCITY)}}
		or {{CheckBoxOn(trinket_foul) and BuffPresent(FOULGIFT)} and {CheckBoxOn(trinket_iotcm) and BuffPresent(VELOCITY)}}}
	and {{CheckBoxOn(trinket_bottle) and {Item(BOTTLEDWISHES) or BuffPresent(ULTIMATEPOWER)}}
		or {CheckBoxOn(trinket_mwc) and {Item(MOONWELLCHALICE) or BuffPresent(BLESSINGOFTHEMOONWELL)}}
		or {CheckBoxOn(trinket_soul) and {Item(SOULCASKET) or BuffPresent(SOULPOWER)}}}
	and	{CheckBoxOn(trinket_wou) and BuffPresent(COMBATMIND stacks=8)}
}

###
### Affliction
###

# Pet
AddIcon size=small mastery=1
{
	if PetPresent(no)
	{
		#/summon_felhunter
		if Glyph(GLYPHOFLASHOFPAIN) Spell(SUMMONSUCCUBUS)
		if Glyph(GLYPHOFIMP) Spell(SUMMONIMP)
		Spell(SUMMONFELHUNTER)
		Spell(SUMMONIMP)
	}
	if InCombat(no)
	{
		if SoulShards(less 3) Spell(SOULHARVEST)
		if ItemCount(HEALTHSTONE less 1) Spell(RITUALOFSOULS)
	}
}

# Main rotation.
AddIcon help=main mastery=1
{
	if InCombat(no) and {BuffExpires(DEMONARMOR 400) and BuffExpires(FELARMOR 400)}
	{
		Spell(FELARMOR)
		Spell(DEMONARMOR)
	}

	# Maintain raid buffs and debuffs.
	if List(curse elements) and TargetDebuffExpires(magicaldamagetaken 2) and TargetDeadIn(more 8) Spell(CURSEOFELEMENTS)
	if List(curse tongues) and TargetDebuffExpires(castslow 2) and TargetDeadIn(more 8) Spell(CURSEOFTONGUES)
	if List(curse weakness) and TargetDebuffExpires(lowerphysicaldamage 2) and TargetDeadIn(more 8) Spell(CURSEOFWEAKNESS)

	#/fel_armor
	unless BuffPresent(DEMONARMOR) or BuffPresent(FELARMOR)
	{
		Spell(FELARMOR)
		Spell(DEMONARMOR)
	}
	#/dark_intent
	if BuffExpires(DARKINTENTBUFF) Spell(DARKINTENT)
	#/corruption,if=(!ticking|remains<tick_time)&miss_react
	if TargetDebuffExpires(CORRUPTION 3 mine=1 haste=spell)
		unless TargetDebuffPresent(SEEDOFCORRUPTION mine=1) Spell(CORRUPTION)
	#/unstable_affliction,if=(!ticking|remains<(cast_time+tick_time))&target.time_to_die>=5&miss_react
	if TargetDebuffExpires(UNSTABLEAFFLICTION 5.5 mine=1 haste=spell) and TargetDeadIn(more 5) Spell(UNSTABLEAFFLICTION)
	#/bane_of_doom,if=target.time_to_die>15&!ticking&miss_react
	unless TargetDebuffPresent(BANEOFDOOM mine=1) or TargetDebuffPresent(BANEOFAGONY mine=1)
	{
		if List(bane doom) and TargetDeadIn(more 15) Spell(BANEOFDOOM)
		if List(bane agony) and TargetDeadIn(more 10) Spell(BANEOFAGONY)
	}
	#/haunt
	Spell(HAUNT)
	#/fel_flame,if=buff.tier11_4pc_caster.react&dot.unstable_affliction.remains<8
	if BuffPresent(FELSPARK) and TargetDebuffExpires(UNSTABLEAFFLICTION 8 mine=1) Spell(FELFLAME)
	if TalentPoints(SOULSIPHONTALENT more 0)
	{
		#/drain_soul,interrupt=1,if=target.health_pct<=25,\
		#	interrupt_if=buff.will_of_unbinding.up&cooldown.haunt.remains<tick_time&\
		#		buff.will_of_unbinding.remains<action.haunt.cast_time+tick_time+0.5
		if TargetLifePercent(less 25) Spell(DRAINSOUL)
	}
	#/shadowflame
	if TargetDistance(less 8) Spell(SHADOWFLAME)
	#/soul_fire,if=buff.soulburn.up
	if ArmorSetParts(T13 more 3)
		if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SOULFIRE)
	if TalentPoints(BANETALENT more 2)
	{
		#/life_tap,mana_percentage<=35
		if ManaPercent(less 35) Spell(LIFETAP)
		if ArmorSetParts(T13 less 4)
		{
			#/soulburn,if=buff.demon_soul_succubus.down
			#/soulburn,if=buff.demon_soul_felhunter.down
			#/soul_fire,if=buff.soulburn.up
			unless BuffPresent(DEMONSOULSUCCUBUS) or BuffPresent(DEMONSOULFELHUNTER)
				if SoulShards(more 0) and Spell(SOULBURN) Spell(SOULFIRE)
			if BuffPresent(SOULBURN) Spell(SOULFIRE)
		}
		#/shadow_bolt
		Spell(SHADOWBOLT)
	}
	if TalentPoints(BANETALENT less 3)
	{
		#/shadow_bolt,if=buff.shadow_trance.react
		if BuffPresent(SHADOWTRANCE) Spell(SHADOWBOLT)
		#/life_tap,mana_percentage<=5
		if ManaPercent(less 5) Spell(LIFETAP)
		#/drain_life
		Spell(DRAINLIFE)
	}
	#/life_tap,if=mana_pct_nonproc<100
	if ManaPercent(less 95) Spell(LIFETAP)
}

# Soulburn.
AddIcon help=offgcd mastery=1
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
	{
		#/soulburn
		if ArmorSetParts(T13 more 3) and SoulShards(more 0) Spell(SOULBURN)
		unless {TargetDebuffExpires(CORRUPTION 3 mine=1 haste=spell) and TargetDebuffExpires(SEEDOFCORRUPTION mine=1)}
			or {TargetDebuffExpires(UNSTABLEAFFLICTION 5.5 mine=1 haste=spell) and TargetDeadIn(more 5)}
			or {TargetDebuffExpires(BANEOFDOOM mine=1) and TargetDebuffExpires(BANEOFAGONY mine=1)
				and {{List(bane doom) and TargetDeadIn(more 15)} or {List(bane agony) and TargetDeadIn(more 10)}}}
			or Spell(HAUNT)
			or {BuffPresent(FELSPARK) and TargetDebuffExpires(UNSTABLEAFFLICTION 8 mine=1)}
			or {TalentPoints(SOULSIPHONTALENT more 0) and TargetLifePercent(less 25)}
		{
			#/soul_fire,if=buff.soulburn.up
			if ArmorSetParts(T13 more 3) and SoulShards(more 0) Spell(SOULBURN)
			if TalentPoints(BANETALENT more 2)
			{
				unless ManaPercent(less 35)
				{
					if ArmorSetParts(T13 less 4)
					{
						#/soulburn,if=buff.demon_soul_succubus.down
						#/soulburn,if=buff.demon_soul_felhunter.down
						unless BuffPresent(DEMONSOULSUCCUBUS) or BuffPresent(DEMONSOULFELHUNTER)
							if SoulShards(more 0) Spell(SOULBURN)
					}
				}
			}
			if TalentPoints(BANETALENT less 3)
			{
				unless BuffPresent(SHADOWTRANCE) or ManaPercent(less 5)
				{
					#/soulburn
					if SoulShards(more 0) Spell(SOULBURN)
				}
			}
		}
	}
}

# Long CDs.
AddIcon help=cd mastery=1
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
	{
		#/volcanic_potion,if=!in_combat
		if InCombat(no) and CheckBoxOn(potions) and TargetClassification(worldboss) Item(VOLCANICPOTION)
		#/use_item
		UseItemActions()
		#init_use_profession_actions()
		UseProfessionActions()
		#init_use_racial_actions()
		UseRacialActions()
		#/volcanic_potion,if=buff.bloodlust.react|target.health_pct<=20
		if CheckBoxOn(potions) and TargetClassification(worldboss)
			if BuffPresent(heroism) or TargetLifePercent(less 20) Item(VOLCANICPOTION)
		#/demon_soul
		unless pet.CreatureFamily(PETSUCCUBUS) Spell(DEMONSOUL)
		unless {TargetDebuffExpires(CORRUPTION 3 mine=1 haste=spell) and TargetDebuffExpires(SEEDOFCORRUPTION mine=1)}
			or {TargetDebuffExpires(UNSTABLEAFFLICTION 5.5 mine=1 haste=spell) and TargetDeadIn(more 5)}
			or {TargetDebuffExpires(BANEOFDOOM mine=1) and TargetDebuffExpires(BANEOFAGONY mine=1)
				and {{List(bane doom) and TargetDeadIn(more 15)} or {List(bane agony) and TargetDeadIn(more 10)}}}
			or Spell(HAUNT)
			or {BuffPresent(FELSPARK) and TargetDebuffExpires(UNSTABLEAFFLICTION 8 mine=1)}
			or {TalentPoints(SOULSIPHONTALENT more 0) and TargetLifePercent(less 25)}
			or {ArmorSetParts(T13 more 3) and SoulShards(more 0)}
		{
			if TalentPoints(BANETALENT more 2)
			{
				unless ManaPercent(less 35)
					or {ArmorSetParts(T13 less 4) and SoulShards(more 0)
						and BuffExpires(DEMONSOULSUCCUBUS) and BuffExpires(DEMONSOULFELHUNTER)}
				{
					#/demon_soul
					if pet.CreatureFamily(PETSUCCUBUS) Spell(DEMONSOUL)
				}
			}
			if TalentPoints(BANETALENT less 3)
			{
				#/demon_soul,if=buff.shadow_trance.react
				if pet.CreatureFamily(PETSUCCUBUS) and BuffPresent(SHADOWTRANCE) Spell(DEMONSOUL)
			}
		}
	}
}

###
### Demonology
###

# Demonology tries to line up trinket usage with Metamorphosis.
AddFunction UseItemActionsDemonology
{
	if Spell(METAMORPHOSIS)
	{
		Item(HandsSlot usable=1)
		Item(Trinket0Slot usable=1)
		Item(Trinket1Slot usable=1)
	}
	unless 60s before Spell(METAMORPHOSIS) Item(HandsSlot usable=1)
	unless 90s before Spell(METAMORPHOSIS)
		if CheckBoxOn(trinket_bottle) Item(BOTTLEDWISHES usable=1)
	unless 120s before Spell(METAMORPHOSIS)
	{
		if CheckBoxOn(trinket_mwc) Item(MOONWELLCHALICE usable=1)
		if CheckBoxOn(trinket_soul) Item(SOULCASKET usable=1)
	}
}

# Pet
AddIcon size=small mastery=1
{
	if InCombat(no)
	{
		unless pet.CreatureFamily(PETFELGUARD) Spell(SUMMONFELGUARD)
		if SoulShards(less 3) Spell(SOULHARVEST)
		if ItemCount(HEALTHSTONE less 1) Spell(RITUALOFSOULS)
	}

	if CheckBoxOn(trinket_mwc)
	{
		#/summon_felguard,if=cooldown.demon_soul.remains<5&cooldown.metamorphosis.remains<5&!pet.felguard.active
		unless pet.CreatureFamily(PETFELGUARD)
			if 5s before Spell(DEMONSOUL) and 5s before Spell(METAMORPHOSIS) Spell(SUMMONFELGUARD)
	}
	#/felguard:felstorm
	if pet.CreatureFamily(PETFELGUARD) and TargetInRange(PETLEGIONSTRIKE yes) Spell(PETFELSTORM)
	#/summon_felhunter,if=!pet.felguard.dot.felstorm.ticking&pet.felguard.active
	if pet.CreatureFamily(PETFELGUARD) and 39s before Spell(PETFELSTORM)
		if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SUMMONFELHUNTER)
}

# Main rotation.
AddIcon help=main mastery=2
{
	#/fel_armor
	unless BuffPresent(DEMONARMOR) or BuffPresent(FELARMOR)
	{
		Spell(FELARMOR)
		Spell(DEMONARMOR)
	}
	#/dark_intent
	if BuffExpires(DARKINTENTBUFF) Spell(DARKINTENT)
	if CheckBoxOn(trinket_mwc)
	{
		#/metamorphosis,if=buff.moonwell_chalice.up&pet.felguard.active
		if BuffPresent(BLESSINGOFTHEMOONWELL) and pet.CreatureFamily(PETFELGUARD) Spell(METAMORPHOSIS)
		#/demon_soul,if=buff.metamorphosis.up
		if BuffPresent(METAMORPHOSIS) Spell(DEMONSOUL)
	}
	if CheckBoxOff(trinket_mwc)
	{
		#/metamorphosis
		Spell(METAMORPHOSIS)
		#/demon_soul
		Spell(DEMONSOUL)
	}
	#/bane_of_doom,if=!ticking&time<10
	if TimeInCombat(less 10) and TargetDebuffExpires(BANEOFDOOM mine=1) and TargetDebuffExpires(BANEOFAGONY mine=1)
	{
		if List(bane doom) and TargetDeadIn(more 15) Spell(BANEOFDOOM)
		if List(bane agony) and TargetDeadIn(more 10) Spell(BANEOFAGONY)
	}
	if ArmorSetParts(T13 more 3) and pet.CreatureFamily(PETFELHUNTER)
	{
		if CheckBoxOn(trinket_mwc)
		{
			unless 60s before Spell(METAMORPHOSIS)
			{
				#/soul_fire,if=pet.felhunter.active&buff.soulburn.up&cooldown.metamorphosis.remains>60
				if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SOULFIRE)
			}
		}
		if CheckBoxOff(trinket_mwc)
		{
			#/soul_fire,if=pet.felhunter.active&buff.soulburn.up
			if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SOULFIRE)
		}
	}
	#/immolate,if=!ticking&target.time_to_die>=4&miss_react
	if TargetDebuffExpires(IMMOLATE mine=1) and TargetDeadIn(more 5) Spell(IMMOLATE)
	#/bane_of_doom,if=(!ticking|(buff.metamorphosis.up&remains<45))&target.time_to_die>=15&miss_react
	if TargetDebuffExpires(BANEOFDOOM mine=1) and TargetDebuffExpires(BANEOFAGONY mine=1)
	{
		if List(bane doom) and TargetDeadIn(more 15) Spell(BANEOFDOOM)
		if List(bane agony) and TargetDeadIn(more 10) Spell(BANEOFAGONY)
	}
	if TargetDebuffExpires(BANEOFDOOM 44 mine=1) and BuffPresent(METAMORPHOSIS)
	{
		if List(bane doom) and TargetDeadIn(more 15) Spell(BANEOFDOOM)
	}
	#/corruption,if=(remains<tick_time|!ticking)&target.time_to_die>=6&miss_react
	if TargetDebuffExpires(CORRUPTION 2 mine=1 haste=spell) and TargetDeadIn(more 6) Spell(CORRUPTION)
	#/fel_flame,if=buff.tier11_4pc_caster.react
	if BuffPresent(FELSPARK) Spell(FELFLAME)
	#/shadowflame
	if TargetDistance(less 8) Spell(SHADOWFLAME)
	#/hand_of_guldan
	Spell(HANDOFGULDAN)
	#/immolation_aura,if=buff.metamorphosis.remains>10
    if TargetDistance(less 8) and BuffPresent(METAMORPHOSIS 10) Spell(IMMOLATIONAURA)
	#/shadow_bolt,if=buff.shadow_trance.react
	if BuffPresent(SHADOWTRANCE) Spell(SHADOWBOLT)
	#/incinerate,if=buff.molten_core.react
	if BuffPresent(MOLTENCORE) Spell(INCINERATE)
	#/soul_fire,if=buff.decimation.up
	if BuffPresent(DECIMATION) Spell(SOULFIRE)
	#/life_tap,if=mana_pct<=30&buff.bloodlust.down&buff.metamorphosis.down&buff.demon_soul_felguard.down
	if ManaPercent(less 30) and BuffExpires(heroism) and BuffExpires(METAMORPHOSIS) and BuffExpires(DEMONSOULFELGUARD) Spell(LIFETAP)
	if TalentPoints(BANETALENT more 0)
	{
		#/shadow_bolt
		Spell(SHADOWBOLT)
	}
	if TalentPoints(BANETALENT less 1)
	{
		#/incinerate
		Spell(INCINERATE)
	}
	#/life_tap,if=mana_pct_nonproc<100
	if ManaPercent(less 95) Spell(LIFETAP)
}

# Soulburn.
AddIcon help=main mastery=2
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
		or {TimeInCombat(less 10) and TargetDebuffExpires(BANEOFDOOM mine=1) and TargetDebuffExpires(BANEOFAGONY mine=1)
			and {{List(bane doom) and TargetDeadIn(more 15)} or {List(bane agony) and TargetDeadIn(more 10)}}}
	{
		#/soulburn,if=pet.felguard.active&!pet.felguard.dot.felstorm.ticking
		if pet.CreatureFamily(PETFELGUARD) and 39s before Spell(PETFELSTORM)
			if SoulShards(more 0) Spell(SOULBURN)
		if ArmorSetParts(T13 more 3) and pet.CreatureFamily(PETFELHUNTER)
		{
			if CheckBoxOn(trinket_mwc)
			{
				#/soulburn,if=pet.felhunter.active&cooldown.metamorphosis.remains>60
				unless 60s before Spell(METAMORPHOSIS)
					if SoulShards(more 0) Spell(SOULBURN)
			}
			if CheckBoxOff(trinket_mwc)
			{
				#/soulburn,if=pet.felhunter.active
				if SoulShards(more 0) Spell(SOULBURN)
			}
		}
}

# Long CDs.
AddIcon help=cd mastery=2
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
	{
		#/volcanic_potion,if=!in_combat
		if InCombat(no) and CheckBoxOn(potions) and TargetClassification(worldboss) Item(VOLCANICPOTION)
		#/use_item
		UseItemActionsDemonology()
		#init_use_profession_actions()
		UseProfessionActions()
		#init_use_racial_actions()
		UseRacialActions()
		#/volcanic_potion,if=buff.metamorphosis.up
		if CheckBoxOn(potions) and TargetClassification(worldboss) and BuffPresent(METAMORPHOSIS) Spell(VOLCANICPOTION)
		if CheckBoxOn(trinket_mwc)
		{
			#/metamorphosis,if=buff.moonwell_chalice.up&pet.felguard.active
			if BuffPresent(BLESSINGOFTHEMOONWELL) and pet.CreatureFamily(PETFELGUARD) Spell(METAMORPHOSIS)
			#/demon_soul,if=buff.metamorphosis.up
			if BuffPresent(METAMORPHOSIS) Spell(DEMONSOUL)
		}
		if CheckBoxOff(trinket_mwc)
		{
			#/metamorphosis
			Spell(METAMORPHOSIS)
			#/demon_soul
			Spell(DEMONSOUL)
		}
	}
}

###
### Destruction
###

# Pet
AddIcon size=small mastery=3
{
	#/summon_imp
	if PetPresent(no) Spell(SUMMONIMP)

	if InCombat(no)
	{
		if SoulShards(less 3) Spell(SOULHARVEST)
		if ItemCount(HEALTHSTONE less 1) Spell(RITUALOFSOULS)
	}
}

# Main rotation.
AddIcon help=main mastery=3
{
	if InCombat(no) and {BuffExpires(DEMONARMOR 400) and BuffExpires(FELARMOR 400)}
	{
		Spell(FELARMOR)
		Spell(DEMONARMOR)
	}

	# Maintain raid buffs and debuffs.
	if List(curse elements) and TargetDebuffExpires(magicaldamagetaken 2) and TargetDeadIn(more 8) Spell(CURSEOFELEMENTS)
	if List(curse tongues) and TargetDebuffExpires(castslow 2) and TargetDeadIn(more 8) Spell(CURSEOFTONGUES)
	if List(curse weakness) and TargetDebuffExpires(lowerphysicaldamage 2) and TargetDeadIn(more 8) Spell(CURSEOFWEAKNESS)

	#/fel_armor
	unless BuffPresent(DEMONARMOR) or BuffPresent(FELARMOR)
	{
		Spell(FELARMOR)
		Spell(DEMONARMOR)
	}
	#/dark_intent
	if BuffExpires(DARKINTENTBUFF) Spell(DARKINTENT)
	if InCombat(no)
	{
		if ArmorSetParts(T13 more 3) or TalentPoints(IMPROVEDSOULFIRETALENT more 0)
		{
			#/soul_fire,if=buff.soulburn.up&!in_combat
			if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SOULFIRE)
		}
	}
	#/fel_flame,if=buff.tier11_4pc_caster.react&dot.immolate.remains<8
	if BuffPresent(FELSPARK) and TargetDebuffExpires(IMMOLATE 8 mine=1) Spell(FELFLAME)
	#/immolate,if=(remains<cast_time+gcd|!ticking)&target.time_to_die>=4&miss_react
    if TargetDebuffExpires(IMMOLATE 3 mine=1 haste=spell) and TargetDeadIn(more 4) Spell(IMMOLATE)
	#/conflagrate
	Spell(CONFLAGRATE)
	#/immolate,if=buff.bloodlust.react&buff.bloodlust.remains>32&cooldown.conflagrate.remains<=3&remains<12
	if BuffPresent(heroism 32) and 3s before Spell(CONFLAGRATE) and TargetDebuffExpires(IMMOLATE 12 mine=1) Spell(IMMOLATE)
	#/bane_of_doom,if=!ticking&target.time_to_die>=15&miss_react
	unless TargetDebuffPresent(BANEOFDOOM mine=1) or TargetDebuffPresent(BANEOFAGONY mine=1)
	{
		if List(bane doom) and TargetDeadIn(more 15) Spell(BANEOFDOOM)
		if List(bane agony) and TargetDeadIn(more 10) Spell(BANEOFAGONY)
	}
	#/corruption,if=(!ticking|dot.corruption.remains<tick_time)&miss_react
	if TargetDebuffExpires(CORRUPTION 3 mine=1 haste=spell)
		unless TargetDebuffPresent(SEEDOFCORRUPTION mine=1) Spell(CORRUPTION)
	#/shadowflame
	if TargetDistance(less 8) Spell(SHADOWFLAME)
	#/chaos_bolt,if=cast_time>0.9
	if Spell(CHAOSBOLT) and CastTime(CHAOSBOLT more 0.9) Spell(CHAOSBOLT)
	if ArmorSetParts(T13 more 3)
	{
		#/soul_fire,if=buff.soulburn.up
		if {SoulShards(more 0) and Spell(SOULBURN)} or BuffPresent(SOULBURN) Spell(SOULFIRE)
	}
	if TalentPoints(IMPROVEDSOULFIRETALENT more 0)
	{
		#/soul_fire,if=((buff.empowered_imp.react&buff.empowered_imp.remains<(buff.improved_soul_fire.remains+action.soul_fire.travel_time))|\
		#	buff.improved_soul_fire.remains<(cast_time+travel_time+action.incinerate.cast_time+gcd))&!in_flight
		# Assume the travel time of Soul Fire is 1s.
		if 1s after Spell(SOULFIRE)
		{
			if BuffPresent(EMPOWEREDIMP) and {buffExpires(EMPOWEREDIMP) < buffExpires(IMPROVEDSOULFIREBUFF) + 1} Spell(SOULFIRE)
			if BuffExpires(IMPROVEDSOULFIREBUFF 7.5 haste=spell) Spell(SOULFIRE) 
		}
	}
	#/shadowburn
	if TargetLifePercent(less 20) Spell(SHADOWBURN)
	#/incinerate
	Spell(INCINERATE)
	#/shadow_bolt
	Spell(SHADOWBOLT)
	#/life_tap,if=mana_pct_nonproc<100
	if ManaPercent(less 95) Spell(LIFETAP)
}

# Soulburn.
AddIcon help=offgcd mastery=3
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
	{
		if ArmorSetParts(T13 more 3)
		{
			#/soulburn
			if SoulShards(more 0) Spell(SOULBURN)
		}
		unless ArmorSetParts(T13 more 3)
		{
			#/soulburn,if=buff.bloodlust.down
			unless BuffPresent(heroism) if SoulShards(more 0) Spell(SOULBURN)
		}
	}
}

# Long CDs.
AddIcon help=main mastery=3
{
	unless {BuffExpires(DEMONARMOR) and BuffExpires(FELARMOR)}
		or {BuffExpires(DARKINTENTBUFF) and Spell(DARKINTENT)}
	{
		#/volcanic_potion,if=!in_combat
		if InCombat(no) and CheckBoxOn(potions) and TargetClassification(worldboss) Item(VOLCANICPOTION)
		#/use_item
		UseItemActions()
		#init_use_profession_actions()
		UseProfessionActions()
		#init_use_racial_actions()
		UseRacialActions()
		#/volcanic_potion,if=buff.bloodlust.react|target.health_pct<=20
		if CheckBoxOn(potions) and TargetClassification(worldboss)
			if BuffPresent(heroism) or TargetLifePercent(less 20) Item(VOLCANICPOTION)
		#/demon_soul
		Spell(DEMONSOUL)
	}
}

###
### Summon Doomguard.
###
AddIcon help=cd size=small
{
	#/summon_doomguard,if=time>10
	if TimeInCombat(more 10) and EnoughBuffsForDoomguard() Spell(SUMMONDOOMGUARD)
}