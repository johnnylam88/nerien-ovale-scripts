### Shaman Ovale script based on SimulationCraft profiles
### https://sites.google.com/site/wownerien/ovale/shaman/script
### Version: 4.3.11
#
# Elemental:
#	talents=http://www.wowhead.com/talent#shaman-3232023212231101321203000200000000000000000000000000000000
#	glyphs=fire_nova/thunder/lightning_shield/astral_recall/renewed_life/thunderstorm/lightning_bolt/lava_burst/flame_shock
#
# Enhancement:
#	talents=http://www.wowhead.com/talent#shaman-3022021000000000000232332001300301232100000000000000000000
#	glyphs=ghost_wolf/lightning_shield/chain_lightning/astral_recall/renewed_life/water_walking/windfury_weapon/lava_lash/stormstrike

## defines ##

# Buffs
Define(LIGHTWEAVE 75170) # +580 int proc
Define(MAELSTROMWEAPON 53817)
Define(POWERTORRENT 74241) # +500 int proc
Define(RIPTIDEHOT 61295)
Define(UNLEASHWIND 73681)

# Glyphs
Define(GLYPHOFFLAMESHOCK 57239)
Define(GLYPHOFGROUNDINGTOTEM 55441)
Define(GLYPHOFSTONECLAWTOTEM 63298)
Define(GLYPHOFUNLEASHEDLIGHTNING 101052)

# Talents
Define(FOCUSEDINSIGHTTALENT 5567)
Define(FULMINATIONTALENT 11769)
Define(REVERBERATIONTALENT 575)

# Spells
Define(BLOODLUST 2825)
	SpellInfo(BLOODLUST cd=300)
	SpellAddBuff(BLOODLUST BLOODLUST=40)
Define(CHAINHEAL 1064)
Define(CHAINLIGHTNING 421)
	SpellAddBuff(CHAINLIGHTNING MAELSTROMWEAPON=0)
Define(EARTHLIVINGWEAPON 51730)
Define(EARTHQUAKE 61882)
Define(EARTHSHOCK 8042)
	SpellInfo(EARTHSHOCK cd=6 sharedcd=shock)
	SpellInfo(EARTHSHOCK addcd=-1 talent=REVERBERATIONTALENT)
	SpellAddTargetDebuff(EARTHSHOCK EARTHSHOCK=8)
Define(ELEMENTALMASTERY 16166)
	SpellInfo(ELEMENTALMASTERY cd=180)
	SpellAddBuff(ELEMENTALMASTERY ELEMENTALMASTERY=30)
Define(FERALSPIRIT 51533)
	SpellInfo(FERALSPIRIT cd=120)
Define(FIRENOVA 1535)
	SpellInfo(FIRENOVA cd=10)
Define(FLAMESHOCK 8050)
	SpellInfo(FLAMESHOCK cd=6 sharedcd=shock duration=18 tick=3)
	SpellInfo(FLAMESHOCK addcd=-1 talent=REVERBERATIONTALENT)
	SpellInfo(FLAMESHOCK addduration=9 glyph=GLYPHOFFLAMESHOCK)
	SpellAddTargetDebuff(FLAMESHOCK FLAMESHOCK=18)
Define(FLAMETONGUEWEAPON 8024)
Define(FROSTSHOCK 8056)
	SpellInfo(FROSTSHOCK cd=6 sharedcd=shock)
	SpellInfo(FROSTSHOCK addcd=-1 talent=REVERBERATIONTALENT)
	SpellAddTargetDebuff(FROSTSHOCK FROSTSHOCK=8)
Define(FROSTBRANDWEAPON 8033)
Define(HEALINGRAIN 73920)
Define(HEROISM 32182)
	SpellInfo(HEROISM cd=300)
	SpellAddBuff(HEROISM HEROISM=40)
Define(LAVABURST 51505)
	SpellInfo(LAVABURST cd=8)
Define(LAVALASH 60103)
	SpellInfo(LAVALASH cd=10)
Define(LIGHTNINGBOLT 403)
	SpellAddBuff(LIGHTNINGBOLT MAELSTROMWEAPON=0)
Define(LIGHTNINGSHIELD 324)
	SpellAddBuff(LIGHTNINGSHIELD LIGHTNINGSHIELD=600)
Define(PRIMALSTRIKE 73899)
	SpellInfo(PRIMALSTRIKE cd=8)
Define(RIPTIDE 61295)
Define(ROCKBITERWEAPON 8017)
Define(SHAMANISTICRAGE 30823)
	SpellInfo(SHAMANISTICRAGE cd=60)
	SpellAddBuff(SHAMANISTICRAGE SHAMANISTICRAGE=15)
Define(SPIRITWALKERSGRACE 79206)
	SpellInfo(SPIRITWALKERSGRACE cd=120)
	SpellAddBuff(SPIRITWALKERSGRACE SPIRITWALKERSGRACE=15)
Define(STORMSTRIKE 17364)
	SpellInfo(STORMSTRIKE cd=8)
	SpellAddTargetDebuff(STORMSTRIKE STORMSTRIKE=15)
Define(THUNDERSTORM 51490)
	SpellInfo(THUNDERSTORM cd=45)
Define(UNLEASHELEMENTS 73680)
	SpellInfo(UNLEASHELEMENTS cd=15)
Define(UNLEASHFLAME 73683)
	SpellInfo(UNLEASHFLAME cd=15)
	SpellAddBuff(UNLEASHFLAME UNLEASHFLAME=8)
Define(WATERSHIELD 52127)
	SpellAddBuff(WATERSHIELD WATERSHIELD=600)
Define(WINDFURYWEAPON 8232)
Define(WINDSHEAR 57994)
	SpellInfo(WINDSHEAR cd=15)
	SpellInfo(WINDSHEAR addcd=-10 talent=REVERBERATIONTALENT)

# Fire totems
Define(FIREELEMENTALTOTEM 2894)
Define(FLAMETONGUETOTEM 8227)
Define(MAGMATOTEM 8190)
	SpellInfo(MAGMATOTEM cd=20)
Define(SEARINGTOTEM 3599)
	SpellInfo(SEARINGTOTEM cd=10) # fake CD so it won't be displayed twice
# Water totems
Define(HEALINGSTREAMTOTEM 5394)
Define(MANASPRINGTOTEM 5675)
Define(MANATIDETOTEM 16190)
Define(TOTEMOFTRANQUILMIND 87718)
# Air totems
Define(GROUNDINGTOTEM 8177)
	SpellInfo(GROUNDINGTOTEM cd=25)
	SpellInfo(GROUNDINGTOTEM addcd=35 glyph=GLYPHOFGROUNDINGTOTEM)
Define(SPIRITLINKTOTEM 98008)
Define(WINDFURYTOTEM 8512)
Define(WRATHOFAIRTOTEM 3738)
# Earth totems
Define(STONECLAWTOTEM 5730)
	SpellInfo(STONECLAWTOTEM cd=20)
Define(STONESKINTOTEM 8071)
Define(STRENGTHOFEARTHTOTEM 8075)
Define(TREMORTOTEM 8143)
	SpellInfo(TREMORTOTEM cd=60)

# Racials & Professions
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(BLOODFURY 33697) # orc
	SpellInfo(BLOODFURY duration=15 cd=120)
	SpellAddBuff(BLOODFURY BLOODFURY=15)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellAddBuff(STONEFORM STONEFORM=8)

# Items
Define(BOTTLEDWISHES 77114)
	Define(ULTIMATEPOWER 107948) # +2290 sp
Define(DMCVOLCANO 62047)
	Define(VOLCANICDESTRUCTION 89091) # +1600 int proc
Define(FIERYQUINTESSENCE 69000)
	Define(FIERYQUINTESSENCEBUFF 97008) # +1149 int
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)
	Define(VOLCANICPOWER 79476) # +1200 int
Define(WILLOFUNBINDINGLFR 77975) # LFR
	Define(COMBATMINDLFR 109793) # +78 int (stacks to 10)
Define(WILLOFUNBINDING 77198) # normal
	Define(COMBATMIND 107970) # +88 int (stacks to 10)
Define(WILLOFUNBINDINGHEROIC 77995) # heroic
	Define(COMBATMINDHEROIC 109795) # +99 int (stacks to 10)

## end defines ##

ScoreSpells(LIGHTNINGSHIELD CHAINLIGHTNING FLAMESHOCK LAVABURST EARTHSHOCK LIGHTNINGBOLT
			STORMSTRIKE LAVALASH UNLEASHELEMENTS)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) mastery=1)
AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL) mastery=2)
AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) mastery=3)
AddCheckBox(aoe L(AOE))
AddListItem(shock none L(None) default talent=FOCUSEDINSIGHTTALENT)
AddListItem(shock earth SpellName(EARTHSHOCK) talent=FOCUSEDINSIGHTTALENT)
AddListItem(shock flame SpellName(FLAMESHOCK) talent=FOCUSEDINSIGHTTALENT)
AddListItem(shock frost SpellName(FROSTSHOCK) talent=FOCUSEDINSIGHTTALENT)
AddCheckBox(lightweave SpellName(LIGHTWEAVE) mastery=1)
AddCheckBox(powertorrent SpellName(POWERTORRENT) mastery=1)
AddListItem(trinket0 none "Trinket 0 - None" default mastery=1)
AddListItem(trinket0 bottledwishes "Trinket 0 - Bottled Wishes" mastery=1)
AddListItem(trinket0 dmcvolcano "Trinket 0 - DMC: Volcano" mastery=1)
AddListItem(trinket0 fieryquintessence "Trinket 0 - Fiery Quintessence" mastery=1)
AddListItem(trinket0 willofunbinding "Trinket 0 - Will of Unbinding" mastery=1)
AddListItem(trinket1 none "Trinket 1 - None" default mastery=1)
AddListItem(trinket1 bottledwishes "Trinket 1 - Bottled Wishes" mastery=1)
AddListItem(trinket1 dmcvolcano "Trinket 1 - DMC: Volcano" mastery=1)
AddListItem(trinket1 fieryquintessence "Trinket 1 - Fiery Quintessence" mastery=1)
AddListItem(trinket1 willofunbinding "Trinket 1 - Will of Unbinding" mastery=1)
AddCheckBox(mw2lb SpellName(LIGHTNINGBOLT) mastery=2)

#http://code.google.com/p/simulationcraft/source/browse/trunk/engine/sc_shaman.cpp (r10951):
#// Fire elemental summoning logic
#// 1) Make sure we fully use some commonly used temporary int bonuses of the profile, calculating the following
#// additively as a sum of temporary intellect buffs on the profile during iteration
#//    - Power Torrent or Lightweave Embroidery is up (either being up is counted as a temporary int buff of 500)
#//    - DMC: Volcano (temporary int buff of 1600)
#//    - Will of Unbinding (all versions are a temporary int buff of 700 int)
#// 2) If the profile hase Fiery Quintessence, align Fire Elemental summoning always with FQ (and require an
#//    additional 1100 temporary int)
#// 3) If the profile hase Bottled Wishes, align Fire Elemental summoning always with BW (and require an
#//    additional 2290 temporary spell power)
#// 4) If the profile uses pre-potting, make sure we try to use the FE during potion, so that
#//    all the other temporary int/sp buffs are also up

AddFunction BottledWishesNotEquipped
{
	{List(trinket0 dmcvolcano) or List(trinket0 fieryquintessence) or List(trinket0 willofunbinding)}
		and {List(trinket1 dmcvolcano) or List(trinket1 fieryquintessence) or List(trinket1 willofunbinding)}
}

AddFunction DMCVolcanoNotEquipped
{
	{List(trinket0 bottledwishes) or List(trinket0 fieryquintessence) or List(trinket0 willofunbinding)}
		and {List(trinket1 bottledwishes) or List(trinket1 fieryquintessence) or List(trinket1 willofunbinding)}
}

AddFunction FieryQuintessenceNotEquipped
{
	{List(trinket0 bottledwishes) or List(trinket0 dmcvolcano) or List(trinket0 willofunbinding)}
		and {List(trinket1 bottledwishes) or List(trinket1 dmcvolcano) or List(trinket1 willofunbinding)}
}

AddFunction WillOfUnbindingNotEquipped
{
	{List(trinket0 bottledwishes) or List(trinket0 dmcvolcano) or List(trinket0 fieryquintessence)}
		and {List(trinket1 bottledwishes) or List(trinket1 dmcvolcano) or List(trinket1 fieryquintessence)}
}

AddFunction EnoughSpellpower
{
	{{CheckBoxOff(lightweave) and CheckBoxOff(powertorrent)}
			or {BuffPresent(LIGHTWEAVE) or BuffPresent(POWERTORRENT)}}
		and {DMCVolcanoNotEquipped() or BuffPresent(VOLCANICDESTRUCTION)}
		and {WillOfUnbindingNotEquipped()
			or {BuffPresent(COMBATMINDLFR stacks=10) or BuffPresent(COMBATMIND stacks=10) or BuffPresent(COMBATMINDHEROIC stacks=10)}}
		and {CheckBoxOff(potions) or BuffPresent(VOLCANICPOWER) or {BuffExpires(VOLCANICPOWER 0) and item(VOLCANICPOTION) > 0}}
}

AddFunction SummonFireElemental
{
	if EnoughSpellpower()
	{
		if FieryQuintessenceNotEquipped() and BottledWishesNotEquipped() Spell(FIREELEMENTALTOTEM)
		if FieryQuintessenceNotEquipped() and BuffPresent(ULTIMATEPOWER) Spell(FIREELEMENTALTOTEM)
		if BuffPresent(FIERYQUINTESSENCEBUFF) and BottledWishesNotEquipped() Spell(FIREELEMENTALTOTEM)
		if BuffPresent(FIERYQUINTESSENCEBUFF) and BuffPresent(ULTIMATEPOWER) Spell(FIREELEMENTALTOTEM)
	}
}

AddFunction UseItemActionsElemental
{
	Item(HandsSlot usable=1)
	unless List(trinket0 bottledwishes) or List(trinket0 fieryquintessence) Item(Trinket0Slot usable=1)
	unless List(trinket1 bottledwishes) or List(trinket1 fieryquintessence) Item(Trinket1Slot usable=1)
	if ArmorSetParts(T12 more 1)
	{
		#/use_item,name,if=(cooldown.fire_elemental_totem.remains<25&temporary_bonus.spell_power>=( sp_threshold ))
		if 25s before Spell(FIREELEMENTALTOTEM) and EnoughSpellpower() Item(BOTTLEDWISHES)
		if 15s before Spell(FIREELEMENTALTOTEM) and EnoughSpellpower() Item(FIERYQUINTESSENCE)
	}
	unless ArmorSetParts(T12 more 1)
	{
		#/use_item,name,if=(cooldown.fire_elemental_totem.remains=0&temporary_bonus.spell_power>=( sp_threshold ))
		if Spell(FIREELEMENTALTOTEM) and EnoughSpellpower() Item(BOTTLEDWISHES)
		if Spell(FIREELEMENTALTOTEM) and EnoughSpellpower() Item(FIERYQUINTESSENCE)
	}
}

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	Spell(BERSERKING)
	Spell(BLOODFURY)
}

AddIcon help=main mastery=1
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(FLAMETONGUEWEAPON)
		if BuffExpires(LIGHTNINGSHIELD 400) Spell(LIGHTNINGSHIELD)
	}

	#/flametongue_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	#if ( ! glyph_unleashed_lightning -> ok() ) { /unleash_elements,moving=1 }
	if Speed(more 0) and Glyph(GLYPHOFUNLEASHEDLIGHTNING no) Spell(UNLEASHELEMENTS)
	#/flame_shock,if=!ticking|ticks_remain<2|((buff.bloodlust.react|buff.elemental_mastery.up)&ticks_remain<3)
	if TargetDebuffExpires(FLAMESHOCK 3 haste=spell mine=1) Spell(FLAMESHOCK)
	if BuffPresent(heroism) or BuffPresent(ELEMENTALMASTERY)
		if TargetDebuffExpires(FLAMESHOCK 6 haste=spell mine=1) Spell(FLAMESHOCK)
	#/lava_burst,if=dot.flame_shock.remains>cast_time
	if target.debuffExpires(FLAMESHOCK mine=1) > castTime(LAVABURST) Spell(LAVABURST)
	if TalentPoints(FULMINATIONTALENT more 0)
	{
		#/earth_shock,if=buff.lightning_shield.stack=9
		if BuffPresent(LIGHTNINGSHIELD stacks=9) Spell(EARTHSHOCK)
		#/earth_shock,if=buff.lightning_shield.stack>6&\
		#    dot.flame_shock.remains>cooldown&\
		#    dot.flame_shock.remains<cooldown+action.flame_shock.tick_time
		if BuffPresent(LIGHTNINGSHIELD stacks=7)
		{
			if TalentPoints(REVERBERATIONTALENT equal 0)
			{
				if targetDebuffExpires(FLAMESHOCK mine=1) > 6
					and targetDebuffExpires(FLAMESHOCK mine=1) < 6 + timeWithHaste(3)
					Spell(EARTHSHOCK)
			}
			if TalentPoints(REVERBERATIONTALENT equal 1)
			{
				if targetDebuffExpires(FLAMESHOCK mine=1) > 5.5
					and targetDebuffExpires(FLAMESHOCK mine=1) < 5.5 + timeWithHaste(3)
					Spell(EARTHSHOCK)
			}
			if TalentPoints(REVERBERATIONTALENT equal 2)
			{
				if targetDebuffExpires(FLAMESHOCK mine=1) > 5
					and targetDebuffExpires(FLAMESHOCK mine=1) < 5 + timeWithHaste(3)
					Spell(EARTHSHOCK)
			}
		}
	}
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/lightning_bolt
	Spell(LIGHTNINGBOLT)
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(FLAMETONGUEWEAPON)
		if BuffExpires(LIGHTNINGSHIELD 400) Spell(LIGHTNINGSHIELD)
	}

	#/flametongue_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	if TotemExpires(fire) Spell(MAGMATOTEM)
	if TalentPoints(FULMINATIONTALENT more 0)
	{
		#/earth_shock,if=buff.lightning_shield.stack=9
		if BuffPresent(LIGHTNINGSHIELD stacks=9) Spell(EARTHSHOCK)
	}
	#/chain_lightning,if=target.adds>2
	Spell(CHAINLIGHTNING)
}

AddIcon help=cd mastery=1
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if IsFeared() Spell(TREMORTOTEM)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(VOLCANICPOTION)
	#/use_item
	UseItemActionsElemental()
	#init_use_profession_actions()
	UseProfessionActions()
	#init_use_racial_actions()
	UseRacialActions()
	#/elemental_mastery
	Spell(ELEMENTALMASTERY)
	#/fire_elemental_totem,if=!ticking
	if ArmorSetParts(T12 more 1) and TotemExpires(fire 25) SummonFireElemental()
	unless ArmorSetParts(T12 more 1) if TotemExpires(fire) SummonFireElemental()
	#/earth_elemental_totem,if=!ticking
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1,if=cooldown.lava_burst.remains=0&set_bonus.tier12_4pc_caster=0
	if Speed(more 0) and Spell(LAVABURST) and ArmorSetParts(T12 less 4) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=main mastery=2
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(WINDFURYWEAPON)
		if WeaponEnchantExpires(offhand 400) Spell(FLAMETONGUEWEAPON)
		if BuffExpires(LIGHTNINGSHIELD 400) Spell(LIGHTNINGSHIELD)
	}

	#/windfury_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(WINDFURYWEAPON)
	#/flametongue_weapon,weapon=off
	if WeaponEnchantExpires(offhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/stormstrike
	Spell(STORMSTRIKE)
	#/lava_lash
	Spell(LAVALASH)
	#/lightning_bolt,if=buff.maelstrom_weapon.react=5|(set_bonus.tier13_4pc_melee=1&buff.maelstrom_weapon.react>=4&pet.spirit_wolf.active)
	if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(LIGHTNINGBOLT)
	if ArmorSetParts(T13 more 3) and BuffPresent(MAELSTROMWEAPON stacks=4) and PetPresent(yes) Spell(LIGHTNINGBOLT)
	#/unleash_elements
	Spell(UNLEASHELEMENTS)
	#/flame_shock,if=!ticking|buff.unleash_flame.up
	if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
	#/earth_shock
	Spell(EARTHSHOCK)
	#/lightning_bolt,if=buff.maelstrom_weapon.react>1
	if CheckBoxOn(mw2lb) and BuffPresent(MAELSTROMWEAPON stacks=2) and Speed(equal 0) Spell(LIGHTNINGBOLT)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(WINDFURYWEAPON)
		if WeaponEnchantExpires(offhand 400) Spell(FLAMETONGUEWEAPON)
		if BuffExpires(LIGHTNINGSHIELD 400) Spell(LIGHTNINGSHIELD)
	}

	if WeaponEnchantExpires(mainhand 2) Spell(WINDFURYWEAPON)
	if WeaponEnchantExpires(offhand 2) Spell(FLAMETONGUEWEAPON)
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)

	# Enhancement 4.3 AoE rotation from ElitistJerks:
	#    http://elitistjerks.com/f79/t127416-enhancement_4_3_least_your_old_axe_good_transmog/#post2051733
	if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(CHAINLIGHTNING)
	if TotemExpires(fire) Spell(MAGMATOTEM)
	Spell(UNLEASHELEMENTS)
	if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
	Spell(LAVALASH)
	if TargetDebuffPresent(FLAMESHOCK mine=1) or OtherDebuffPresent(FLAMESHOCK mine=1) Spell(FIRENOVA)
}

AddIcon help=cd mastery=2
{
	#/tolvir_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(POTIONOFTHETOLVIR)
	if IsFeared() Spell(TREMORTOTEM)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(POTIONOFTHETOLVIR)
	#/use_item
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#init_use_racial_actions()
	UseRacialActions()
	#/spirit_wolf
	Spell(FERALSPIRIT)
	Spell(FIREELEMENTALTOTEM)
	#/earth_elemental_totem
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=main mastery=3
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(EARTHLIVINGWEAPON)
	if WeaponEnchantExpires(mainhand 2) Spell(EARTHLIVINGWEAPON)
	unless BuffPresent(WATERSHIELD) Spell(WATERSHIELD)
	if TotemExpires(water) Spell(HEALINGSTREAMTOTEM)
	Spell(RIPTIDE)
}

AddIcon help=aoe mastery=3 checkboxon=aoe
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(EARTHLIVINGWEAPON)
	if WeaponEnchantExpires(mainhand 2) Spell(EARTHLIVINGWEAPON)
	unless BuffPresent(WATERSHIELD) Spell(WATERSHIELD)
	if TotemExpires(water) Spell(HEALINGSTREAMTOTEM)
	if TalentPoints(FOCUSEDINSIGHTTALENT more 0)
	{
		if List(shock earth) and spell(HEALINGRAIN) < castTime(EARTHSHOCK) Spell(EARTHSHOCK)
		if List(shock flame) and spell(HEALINGRAIN) < castTime(FLAMESHOCK) Spell(FLAMESHOCK)
		if List(shock frost) and spell(HEALINGRAIN) < castTime(FROSTSHOCK) Spell(FROSTSHOCK)
	}
	Spell(HEALINGRAIN)
	Spell(CHAINHEAL)
}

AddIcon help=cd mastery=3
{
	#/tolvir_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(POTIONOFTHETOLVIR)
	if IsFeared() Spell(TREMORTOTEM)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(POTIONOFTHETOLVIR)
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
	#/spiritwalkers_grace,moving=1
	Spell(SPIRITLINKTOTEM)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	Spell(FIREELEMENTALTOTEM)
	Spell(EARTHELEMENTALTOTEM)
}

AddIcon help=mana size=small
{
	if ManaPercent(less 80) Spell(MANATIDETOTEM)
	Spell(SHAMANISTICRAGE)
	Spell(THUNDERSTORM)
}

AddIcon help=cd size=small
{
	# survival CD
	if Glyph(GLYPHOFSTONECLAWTOTEM) Spell(STONECLAWTOTEM)

	unless BuffPresent(heroism)
	{
		#/bloodlust,health_percentage<=25
		#/bloodlust,if=target.time_to_die<=60
		if TargetLifePercent(less 25) or TargetDeadIn(less 60)
		{
			Spell(BLOODLUST)
			Spell(HEROISM)
		}
	}
}