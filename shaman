# Shaman Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.3 2012/01/14 - Simplify some conditions using haste=spell modifier for TargetDebuffExpires().
#                  - Fix Glyph of Stoneclaw Totem usage.
#                  - Add Elemental AoE rotation icon.
#                  - Add Restoration icons for healing spells.
# 4.3.2 2012/01/04 - Update shaman profiles to SimulationCraft, Tier 13H:
#                        Elemental (r10707), Enhancement (r10630)
#                    Move Enhancement AoE rotation to a separate icon.
# 4.3.1 2011/12/07 - Initial version based on Ovale default script.
#                    Use shaman profiles from SimulationCraft, Tier 13H:
#                        Elemental (r10348), Enhancement (r10526)

## defines ##

# Buffs
Define(MAELSTROMWEAPON 53817)
Define(RIPTIDEHOT 61295)
Define(UNLEASHWIND 73681)

# Glyphs
Define(GLYPHOFGROUNDINGTOTEM 57247)
Define(GLYPHOFSTONECLAWTOTEM 63298)
Define(GLYPHOFUNLEASHEDLIGHTNING 101052)

# Talents
Define(FOCUSEDINSIGHTTALENT 5567)
Define(FULMINATIONTALENT 11769)
Define(REVERBERATIONTALENT 575)

# Spells
Define(BLOODLUST 2825)
	SpellInfo(BLOODLUST cd=300)
	SpellAddBuff(BLOODLUST BLOODLUST=40)
Define(CHAINHEAL 1064)
Define(CHAINLIGHTNING 421)
	SpellAddBuff(CHAINLIGHTNING MAELSTROMWEAPON=0)
Define(EARTHLIVINGWEAPON 51730)
Define(EARTHQUAKE 61882)
Define(EARTHSHOCK 8042)
	SpellInfo(EARTHSHOCK cd=6 sharedcd=shock)
	SpellInfo(EARTHSHOCK addcd=-1 talent=575) # Reverberation (Rank 2)
	SpellAddTargetDebuff(EARTHSHOCK EARTHSHOCK=8)
Define(ELEMENTALMASTERY 16166)
	SpellInfo(ELEMENTALMASTERY cd=180)
	SpellAddBuff(ELEMENTALMASTERY ELEMENTALMASTERY=30)
Define(FERALSPIRIT 51533)
	SpellInfo(FERALSPIRIT cd=120)
Define(FIRENOVA 1535)
	SpellInfo(FIRENOVA cd=10)
Define(FLAMESHOCK 8050)
	SpellInfo(FLAMESHOCK cd=6 sharedcd=shock duration=18)
	SpellInfo(FLAMESHOCK addcd=-1 talent=575) # Reverberation (Rank 2)
	SpellInfo(FLAMESHOCK addduration=9 glyph=57239) # Glyph of Flame Shock
	SpellAddTargetDebuff(FLAMESHOCK FLAMESHOCK=18)
Define(FLAMETONGUEWEAPON 8024)
Define(FROSTSHOCK 8056)
	SpellInfo(FROSTSHOCK cd=6 sharedcd=shock)
	SpellInfo(FROSTSHOCK addcd=-1 talent=575) # Reverberation (Rank 2)
	SpellAddTargetDebuff(FROSTSHOCK FROSTSHOCK=8)
Define(FROSTBRANDWEAPON 8033)
Define(HEALINGRAIN 73920)
Define(HEROISM 32182)
	SpellInfo(HEROISM cd=300)
	SpellAddBuff(HEROISM HEROISM=40)
Define(LAVABURST 51505)
	SpellInfo(LAVABURST cd=8)
Define(LAVALASH 60103)
	SpellInfo(LAVALASH cd=10)
Define(LIGHTNINGBOLT 403)
	SpellAddBuff(LIGHTNINGBOLT MAELSTROMWEAPON=0)
Define(LIGHTNINGSHIELD 324)
	SpellAddBuff(LIGHTNINGSHIELD LIGHTNINGSHIELD=600)
Define(PRIMALSTRIKE 73899)
	SpellInfo(PRIMALSTRIKE cd=8)
Define(RIPTIDE 61295)
Define(ROCKBITERWEAPON 8017)
Define(SHAMANISTICRAGE 30823)
	SpellInfo(SHAMANISTICRAGE cd=60)
	SpellAddBuff(SHAMANISTICRAGE SHAMANISTICRAGE=15)
Define(SPIRITWALKERSGRACE 79206)
	SpellInfo(SPIRITWALKERSGRACE cd=120)
	SpellAddBuff(SPIRITWALKERSGRACE SPIRITWALKERSGRACE=15)
Define(STORMSTRIKE 17364)
	SpellInfo(STORMSTRIKE cd=8)
	SpellAddTargetDebuff(STORMSTRIKE STORMSTRIKE=15)
Define(THUNDERSTORM 51490)
	SpellInfo(THUNDERSTORM cd=45)
Define(UNLEASHELEMENTS 73680)
	SpellInfo(UNLEASHELEMENTS cd=15)
Define(UNLEASHFLAME 73683)
	SpellInfo(UNLEASHFLAME cd=15)
	SpellAddBuff(UNLEASHFLAME UNLEASHFLAME=8)
Define(WATERSHIELD 52127)
	SpellAddBuff(WATERSHIELD WATERSHIELD=600)
Define(WINDFURYWEAPON 8232)
Define(WINDSHEAR 57994)
	SpellInfo(WINDSHEAR cd=15)
	SpellInfo(WINDSHEAR addcd=-10 talent=575) # Reverberation (Rank 2)

# Fire totems
Define(FIREELEMENTALTOTEM 2894)
Define(FLAMETONGUETOTEM 8227)
Define(MAGMATOTEM 8190)
	SpellInfo(MAGMATOTEM cd=20)
Define(SEARINGTOTEM 3599)
	SpellInfo(SEARINGTOTEM cd=10) # fake CD so it won't be displayed twice
# Water totems
Define(HEALINGSTREAMTOTEM 5394)
Define(MANASPRINGTOTEM 5675)
Define(MANATIDETOTEM 16190)
Define(TOTEMOFTRANQUILMIND 87718)
# Air totems
Define(GROUNDINGTOTEM 8177)
	SpellInfo(GROUNDINGTOTEM cd=25)
	SpellInfo(GROUNDINGTOTEM addcd=35 glyph=57247) # Glyph of Grounding Totem
Define(SPIRITLINKTOTEM 98008)
Define(WINDFURYTOTEM 8512)
Define(WRATHOFAIRTOTEM 3738)
# Earth totems
Define(STONECLAWTOTEM 5730)
	SpellInfo(STONECLAWTOTEM cd=20)
Define(STONESKINTOTEM 8071)
Define(STRENGTHOFEARTHTOTEM 8075)
Define(TREMORTOTEM 8143)
	SpellInfo(TREMORTOTEM cd=60)

# Items
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)

## end defines ##

ScoreSpells(LIGHTNINGSHIELD CHAINLIGHTNING FLAMESHOCK LAVABURST EARTHSHOCK LIGHTNINGBOLT
			STORMSTRIKE LAVALASH UNLEASHELEMENTS)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) mastery=1)
AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL) mastery=2)
AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) mastery=3)
AddCheckBox(aoe L(AOE))
AddListItem(shock none L(None) default talent=5567)
AddListItem(shock earth SpellName(EARTHSHOCK) talent=5567)
AddListItem(shock flame SpellName(FLAMESHOCK) talent=5567)
AddListItem(shock frost SpellName(FROSTSHOCK) talent=5567)

AddIcon help=main mastery=1
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(FLAMETONGUEWEAPON)

	#/flametongue_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	#if ( ! glyph_unleashed_lightning -> ok() ) { /unleash_elements,moving=1 }
	if Speed(more 0) and Glyph(GLYPHOFUNLEASHEDLIGHTNING no) Spell(UNLEASHELEMENTS)
	#/flame_shock,if=!ticking|ticks_remain<2|((buff.bloodlust.react|buff.elemental_mastery.up)&ticks_remain<3)
	if TargetDebuffExpires(FLAMESHOCK 3 haste=spell mine=1) Spell(FLAMESHOCK)
	if BuffPresent(heroism) or BuffPresent(ELEMENTALMASTERY)
		if TargetDebuffExpires(FLAMESHOCK 6 haste=spell mine=1) Spell(FLAMESHOCK)
	#/lava_burst,if=dot.flame_shock.remains>cast_time
	if target.debuffExpires(FLAMESHOCK mine=1) > castTime(LAVABURST) Spell(LAVABURST)
	if TalentPoints(FULMINATIONTALENT more 0)
	{
		#/earth_shock,if=buff.lightning_shield.stack=9
		if BuffPresent(LIGHTNINGSHIELD stacks=9) Spell(EARTHSHOCK)
		#/earth_shock,if=buff.lightning_shield.stack>6&\
		#    dot.flame_shock.remains>cooldown&\
		#    dot.flame_shock.remains<cooldown+action.flame_shock.tick_time
		if BuffPresent(LIGHTNINGSHIELD stacks=7)
			and {target.debuffExpires(FLAMESHOCK mine=1) > spell(EARTHSHOCK)}
			and {target.debuffExpires(FLAMESHOCK mine=1) < spell(EARTHSHOCK) + timeWithHaste(3)}
			Spell(EARTHSHOCK)
	}
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/chain_lightning,if=target.adds>2
	#/lightning_bolt
	Spell(LIGHTNINGBOLT)
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(FLAMETONGUEWEAPON)

	#/flametongue_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	if TotemExpires(fire) Spell(MAGMATOTEM)
	if TalentPoints(FULMINATIONTALENT more 0)
	{
		#/earth_shock,if=buff.lightning_shield.stack=9
		if BuffPresent(LIGHTNINGSHIELD stacks=9) Spell(EARTHSHOCK)
	}
	#/chain_lightning
	Spell(CHAINLIGHTNING)
}

AddIcon help=cd mastery=1
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if IsFeared() Spell(TREMORTOTEM)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(VOLCANICPOTION)
	#/elemental_mastery
	Spell(ELEMENTALMASTERY)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	#/fire_elemental_totem,if=temporary_bonus.intellect>=1600&(!ticking|remains<25)
	Spell(FIREELEMENTALTOTEM)
	#/earth_elemental_totem,if=!ticking
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1,if=cooldown.lava_burst.remains=0&set_bonus.tier12_4pc_caster=0
	if Speed(more 0) and Spell(LAVABURST) and ArmorSetParts(T12 less 4) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=main mastery=2
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(WINDFURYWEAPON)
		if WeaponEnchantExpires(offhand 400) Spell(FLAMETONGUEWEAPON)
	}

	#/windfury_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(WINDFURYWEAPON)
	#/flametongue_weapon,weapon=off
	if WeaponEnchantExpires(offhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/stormstrike
	Spell(STORMSTRIKE)
	#/lava_lash
	Spell(LAVALASH)
	#/lightning_bolt,if=buff.maelstrom_weapon.react=5|(set_bonus.tier13_4pc_melee=1&buff.maelstrom_weapon.react>=4&pet.spirit_wolf.active)
	if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(LIGHTNINGBOLT)
	if ArmorSetParts(T13 more 3) and BuffPresent(MAELSTROMWEAPON stacks=4) and PetPresent(yes) Spell(LIGHTNINGBOLT)
	#/unleash_elements
	Spell(UNLEASHELEMENTS)
	#/flame_shock,if=!ticking|buff.unleash_flame.up
	if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
	#/earth_shock
	Spell(EARTHSHOCK)
	#/lightning_bolt,if=buff.maelstrom_weapon.react>1
	if BuffPresent(MAELSTROMWEAPON) Spell(LIGHTNINGBOLT)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	# Enhancement 4.3 AoE rotation from ElitistJerks:
	#    http://elitistjerks.com/f79/t127416-enhancement_4_3_least_your_old_axe_good_transmog/#post2051733
	if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(CHAINLIGHTNING)
	if TotemExpires(fire) Spell(MAGMATOTEM)
	Spell(UNLEASHELEMENTS)
	if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
	Spell(LAVALASH)
	if OtherDebuffPresent(FLAMESHOCK mine=1) Spell(FIRENOVA)
}

AddIcon help=cd mastery=2
{
	#/tolvir_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(POTIONOFTHETOLVIR)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(POTIONOFTHETOLVIR)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	#/spirit_wolf
	Spell(FERALSPIRIT)
	Spell(FIREELEMENTALTOTEM)
	#/earth_elemental_totem
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=main mastery=3
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(EARTHLIVINGWEAPON)
	if WeaponEnchantExpires(mainhand 2) Spell(EARTHLIVINGWEAPON)
	unless BuffPresent(WATERSHIELD) Spell(WATERSHIELD)
	Spell(RIPTIDE)
}

AddIcon help=aoe mastery=3 checkboxon=aoe
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(EARTHLIVINGWEAPON)
	if WeaponEnchantExpires(mainhand 2) Spell(EARTHLIVINGWEAPON)
	unless BuffPresent(WATERSHIELD) Spell(WATERSHIELD)
	if TotemExpires(water) Spell(HEALINGSTREAMTOTEM)
	if TalentPoints(FOCUSEDINSIGHTTALENT more 0)
	{
		if List(shock earth) and spell(HEALINGRAIN) < castTime(EARTHSHOCK) Spell(EARTHSHOCK)
		if List(shock flame) and spell(HEALINGRAIN) < castTime(FLAMESHOCK) Spell(FLAMESHOCK)
		if List(shock frost) and spell(HEALINGRAIN) < castTime(FROSTSHOCK) Spell(FROSTSHOCK)
	}
	Spell(HEALINGRAIN)
	Spell(CHAINHEAL)
}

AddIcon help=cd mastery=3
{
	#/tolvir_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(POTIONOFTHETOLVIR)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(POTIONOFTHETOLVIR)
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
	#/spiritwalkers_grace,moving=1
	Spell(SPIRITLINKTOTEM)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	Spell(FIREELEMENTALTOTEM)
	Spell(EARTHELEMENTALTOTEM)
}

AddIcon help=mana size=small
{
	Spell(MANATIDETOTEM)
	Spell(SHAMANISTICRAGE)
	Spell(THUNDERSTORM)
}

AddIcon help=cd size=small
{
	# survival CD
	if Glyph(GLYPHOFSTONECLAWTOTEM) Spell(STONECLAWTOTEM)

	unless BuffPresent(heroism)
	{
		#/bloodlust,health_percentage<=25
		#/bloodlust,if=target.time_to_die<=60
		if TargetLifePercent(less 25) or TargetDeadIn(less 60)
		{
			Spell(BLOODLUST)
			Spell(HEROISM)
		}
	}
}