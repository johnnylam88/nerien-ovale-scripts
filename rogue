# Rogue Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.10 2012/01/18 - Only Stealth in Assassination spec if talented into Overkill.
# 4.3.9  2012/01/12 - Don't suggest poisons in the finishers-only icon.
# 4.3.8  2012/01/09 - Warn when rogue is out of melee range by displaying "Pick Herbs" texture.
#                     Checkbox for toggling display of "finishers-only" icon to track which (de)buff needs
#                         refreshing next.
#                     Fix Subtlety rotation by checking for Hemorrhage DoT and refreshing with Hemorrhage.
#                     Remove poisoning weapons from the CD icons.
# 4.3.7  2012/01/08 - Rupture ticks every 2s, so "less than 2 ticks remaining" means "less than 2s".
# 4.3.6  2012/01/08 - Fix typo that was breaking Assassination script.
# 4.3.5  2012/01/08 - Move Shadow Dance to the CD icon and only handle energy pooling (for ShD and Vanish)
#                         in the CD icon. The main icon will continue to show actions for buff mainenance.
# 4.3.4  2012/01/03 - Add some talent checks for various abilities.
# 4.3.3  2012/01/03 - Improve energy pooling and don't suggest Vanishing if already stealthed.
# 4.3.2  2011/12/31 - Treat Vanish as a CD due to the long cooldown and remove some unnecessary functions.
# 4.3.1  2011/12/06 - Initial version based on Ovale default script.
#                     Use rogue profiles from SimulationCraft, Tier 13H:
#                         Assassination (r10526), Combat (r10526), Subtlety (r10633)

## defines ##

# Buffs
Define(DEEPINSIGHT 84747)
Define(MASTEROFSUBTLETY 31223)
Define(MODERATEINSIGHT 84746)
Define(OVERKILL 58426)
Define(SHADOWSTEPBUFF 36563)
Define(SHALLOWINSIGHT 84745)
Define(VANISHBUFF 11327)

# Debuffs
Define(FINDWEAKNESS 91023)
Define(HEMORRHAGEDOT 89775) # Glyph of Hemorrhage

# Glyphs
Define(GLYPHOFHEMORRHAGE 57126)

# Talents
Define(BANDITSGUILETALENT 11174)
Define(CUTTOTHECHASETALENT 2070)
Define(ENERGETICRECOVERYTALENT 11665)
Define(HEMORRHAGETALENT 681)
Define(MURDEROUSINTENTTALENT 6516)
Define(OVERKILLTALENT 281)
Define(SHADOWDANCETALENT 2081)
Define(VENDETTATALENT 2071)

# Abilities
Define(ADRENALINERUSH 13750)
	SpellInfo(ADRENALINERUSH cd=180)
	SpellAddBuff(ADRENALINERUSH ADRENALINERUSH=15)
Define(AMBUSH 8676)
	SpellInfo(AMBUSH combo=2 mana=60)
Define(BACKSTAB 53)
	SpellInfo(BACKSTAB combo=1 mana=60)
Define(BLADEFLURRY 13877)
	SpellAddBuff(BLADEFLURRY BLADEFLURRY=1000 cd=10)
Define(CLOACKOFSHADOWS 31224)
	SpellInfo(CLOACKOFSHADOWS cd=90)
Define(COLDBLOOD 14177)
	SpellInfo(COLDBLOOD cd=120)
	SpellAddBuff(COLDBLOOD COLDBLOOD=100)
Define(ENVENOM 32645)
	SpellInfo(ENVENOM combo=-5)
	SpellAddBuff(ENVENOM ENVENOM=5 mana=35)
Define(EVISCERATE 2098)
	SpellInfo(EVISCERATE combo=-5 mana=35)
Define(GARROTE 703)
	SpellInfo(GARROTE combo=1 mana=45)
	SpellAddTargetDebuff(GARROTE GARROTE=18)
Define(HEMORRHAGE 16511)
	SpellInfo(HEMORRHAGE combo=1 mana=35)
	SpellAddTargetDebuff(HEMORRHAGE HEMORRHAGE=60)
Define(KICK 1766)
Define(KILLINGSPREE 51690)
	SpellInfo(KILLINGSPREE cd=120)
	SpellAddBuff(KILLINGSPREE KILLINGSPREE=2)
Define(MUTILATE 1329)
	SpellInfo(MUTILATE combo=1 mana=60)
Define(PREMEDITATION 14183)
	SpellInfo(PREMEDITATION cd=20 combo=2)
Define(PREPARATION 14185)
	SpellInfo(PREPARATION cd=300)
Define(RECUPERATE 73651)
	SpellInfo(RECUPERATE combo=-5 mana=30)
	SpellAddBuff(RECUPERATE RECUPERATE=30)
Define(REVEALINGSTRIKE 84617)
	SpellInfo(REVEALINGSTRIKE combo=1 mana=40)
	SpellAddTargetDebuff(REVEALINGSTRIKE REVEALINGSTRIKE=15)
Define(RUPTURE 1943)
	SpellInfo(RUPTURE combo=-5 mana=25)
	SpellAddTargetDebuff(RUPTURE RUPTURE=8)
Define(SINISTERSTRIKE 1752)
	SpellInfo(SINISTERSTRIKE combo=1 mana=45)
Define(SHADOWDANCE 51713)
	SpellInfo(SHADOWDANCE cd=60)
	SpellAddBuff(SHADOWDANCE SHADOWDANCE=6)
Define(SHADOWSTEP 36554)
	SpellInfo(SHADOWSTEP cd=20)
	SpellAddBuff(SHADOWSTEP SHADOWSTEPBUFF=10)
Define(SHIV 5938)
	SpellInfo(SHIV combo=1 mana=20)
Define(SLICEANDDICE 5171)
	SpellInfo(SLICEANDDICE combo=-5 mana=25)
	SpellAddBuff(SLICEANDDICE SLICEANDDICE=10)
Define(STEALTH 1784)
Define(TRICKSOFTHETRADE 57934)
	SpellInfo(TRICKSOFTHETRADE cd=30)
Define(VANISH 1856)
	SpellInfo(VANISH cd=180)
	SpellAddBuff(VANISH VANISHBUFF=3)
Define(VENDETTA 79140)
	SpellInfo(VENDETTA cd=120)
	SpellAddTargetDebuff(VENDETTA VENDETTA=30)

# Items
Define(DEADLYPOISON 2892)
Define(INSTANTPOISON 6947)
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)

SpellList(insight 84745 84746 84747)

## end defines ##

ScoreSpells(SLICEANDDICE HUNGERFORBLOOD ENVENOM RUPTURE EVISCERATE MUTILATE SINISTERSTRIKE)

AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL))
AddCheckBox(hemorrhage SpellName(HEMORRHAGE) talent=681)
AddCheckBox(finisher "Separate finisher icon")

# Main Assassination rotation.
AddIcon help=main mastery=1
{
	#/apply_poison,main_hand=instant,off_hand=deadly
	if WeaponEnchantExpires(mainhand 5) Item(INSTANTPOISON)
	if WeaponEnchantExpires(offhand 5) Item(DEADLYPOISON)
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/garrote
	Spell(GARROTE usable=1)
	#/slice_and_dice,if=buff.slice_and_dice.down
	unless BuffPresent(SLICEANDDICE) if ComboPoints(more 0) Spell(SLICEANDDICE)
	#if ( ! talents.cut_to_the_chase -> rank() )
	unless TalentPoints(CUTTOTHECHASETALENT more 0)
	{
		#/pool_energy,if=energy<60&buff.slice_and_dice.remains<5
		if Mana(less 60) and BuffExpires(SLICEANDDICE 5) and ComboPoints(more 0) Spell(SLICEANDDICE nored=1)
		#/slice_and_dice,if=combo_points>=3&buff.slice_and_dice.remains<2
		if ComboPoints(more 2) and BuffExpires(SLICEANDDICE 2) Spell(SLICEANDICE)
	}
	#if ( talents.vendetta -> rank() ) { /rupture,if=(!ticking|ticks_remain<2)&time<6 }
	if TalentPoints(VENDETTATALENT more 0)
	{
		if TargetDebuffExpires(RUPTURE 2 mine=1) and TimeInCombat(less 6) and ComboPoints(more 0) Spell(RUPTURE)
	}
	#/rupture,if=(!ticking|ticks_remain<2)&buff.slice_and_dice.remains>6
	if TargetDebuffExpires(RUPTURE 2 mine=1) and BuffPresent(SLICEANDDICE 6) and ComboPoints(more 0) Spell(RUPTURE)
	#/envenom,if=combo_points>=4&buff.envenom.down
	if ComboPoints(more 3) and BuffExpires(ENVENOM 0) Spell(ENVENOM)
	#/envenom,if=combo_points>=4&energy>90
	if ComboPoints(more 3) and Mana(more 90) Spell(ENVENOM)
	#/envenom,if=combo_points>=2&buff.slice_and_dice.remains<3
	if ComboPoints(more 1) and BuffExpires(SLICEANDDICE 3) Spell(ENVENOM)
	#/backstab,if=combo_points<5&target.health_pct<35
	if TalentPoints(MURDEROUSINTENTTALENT more 0) and ComboPoints(less 5) and TargetLifePercent(less 35) Spell(BACKSTAB)
	#/mutilate,if=combo_points<4&target.health_pct>=35
	if ComboPoints(less 4) and TargetLifePercent(more 35) Spell(MUTILATE)
}

# Main Assassination rotation (same as above) minus Backstab/Mutilate fillers.
AddIcon help=main mastery=1 checkboxon=finisher
{
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/garrote
	Spell(GARROTE usable=1)
	#/slice_and_dice,if=buff.slice_and_dice.down
	unless BuffPresent(SLICEANDDICE) if ComboPoints(more 0) Spell(SLICEANDDICE)
	#if ( ! talents.cut_to_the_chase -> rank() )
	unless TalentPoints(CUTTOTHECHASETALENT more 0)
	{
		#/pool_energy,if=energy<60&buff.slice_and_dice.remains<5
		if Mana(less 60) and BuffExpires(SLICEANDDICE 5) and ComboPoints(more 0) Spell(SLICEANDDICE nored=1)
		#/slice_and_dice,if=combo_points>=3&buff.slice_and_dice.remains<2
		if ComboPoints(more 2) and BuffExpires(SLICEANDDICE 2) Spell(SLICEANDICE)
	}
	#if ( talents.vendetta -> rank() ) { /rupture,if=(!ticking|ticks_remain<2)&time<6 }
	if TalentPoints(VENDETTATALENT more 0)
	{
		if TargetDebuffExpires(RUPTURE 2 mine=1) and TimeInCombat(less 6) and ComboPoints(more 0) Spell(RUPTURE)
	}
	#/rupture,if=(!ticking|ticks_remain<2)&buff.slice_and_dice.remains>6
	if TargetDebuffExpires(RUPTURE 2 mine=1) and BuffPresent(SLICEANDDICE 6) and ComboPoints(more 0) Spell(RUPTURE)
	#/envenom,if=combo_points>=4&buff.envenom.down
	if ComboPoints(more 3) and BuffExpires(ENVENOM 0) Spell(ENVENOM)
	#/envenom,if=combo_points>=4&energy>90
	if ComboPoints(more 3) and Mana(more 90) Spell(ENVENOM)
	#/envenom,if=combo_points>=2&buff.slice_and_dice.remains<3
	if ComboPoints(more 1) and BuffExpires(SLICEANDDICE 3) Spell(ENVENOM)
}

AddIcon help=cd mastery=1
{
	#if ( talents.overkill -> rank() ) { /stealth }
	if TalentPoints(OVERKILLTALENT more 0) unless InCombat() or Stealthed() Spell(STEALTH)
	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/kick
	if TargetIsInterruptible() and TargetInRange(KICK) Spell(KICK)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<30
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 30)} Item(POTIONOFTHETOLVIR)
	#/vendetta (after opening SnD is up)
	unless TargetDebuffPresent(VENDETTA) if BuffPresent(SLICEANDDICE) Spell(VENDETTA)
	#/cold_blood,sync=envenom
	if ComboPoints(more 0) and Spell(ENVENOM) Spell(COLDBLOOD)
	#if ( talents.overkill -> rank() ) { /vanish,if=time>30&energy>50 }
	if TalentPoints(OVERKILLTALENT more 0) and TimeInCombat(more 30) and Mana(more 50) unless BuffPresent(OVERKILL) Spell(VANISH)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

# Main Combat rotation.
AddIcon help=main mastery=2
{
	#/apply_poison,main_hand=instant,off_hand=deadly
	if WeaponEnchantExpires(mainhand 5) Item(INSTANTPOISON)
	if WeaponEnchantExpires(offhand 5) Item(DEADLYPOISON)
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	#/slice_and_dice,if=buff.slice_and_dice.down
	#/slice_and_dice,if=buff.slice_and_dice.remains<2
	if BuffExpires(SLICEANDDICE 2) and ComboPoints(more 0) Spell(SLICEANDDICE)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/eviscerate,if=combo_points=5&buff.bandits_guile.stack>=12
	if TalentPoints(BANDITSGUILETALENT more 0) and ComboPoints(equal 5) and BuffPresent(DEEPINSIGHT) Spell(EVISCERATE)
	#/rupture,if=!ticking&combo_points=5&target.time_to_die>10 (EJ says Rupture is only worth using if target is bleeding)
	unless TargetDebuffPresent(RUPTURE mine=1) if TargetDebuffPresent(bleed) and ComboPoints(equal 5) and TargetDeadIn(more 10) Spell(RUPTURE)
	#/eviscerate,if=combo_points=5
	if ComboPoints(equal 5) Spell(EVISCERATE)
	#/revealing_strike,if=combo_points=4&buff.revealing_strike.down
	unless TargetBuffPresent(REVEALINGSTRIKE mine=1) if ComboPoints(equal 4) Spell(REVEALINGSTRIKE)
	#/sinister_strike,if=combo_points<5
	if ComboPoints(less 5) Spell(SINISTERSTRIKE)
}

# Main Combat rotation (same as above) minus Sinister Strike fillers.
AddIcon help=main mastery=2 checkboxon=finisher
{
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	#/slice_and_dice,if=buff.slice_and_dice.down
	#/slice_and_dice,if=buff.slice_and_dice.remains<2
	if BuffExpires(SLICEANDDICE 2) and ComboPoints(more 0) Spell(SLICEANDDICE)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/eviscerate,if=combo_points=5&buff.bandits_guile.stack>=12
	if TalentPoints(BANDITSGUILETALENT more 0) and ComboPoints(equal 5) and BuffPresent(DEEPINSIGHT) Spell(EVISCERATE)
	#/rupture,if=!ticking&combo_points=5&target.time_to_die>10 (EJ says Rupture is only worth using if target is bleeding)
	unless TargetDebuffPresent(RUPTURE mine=1) if TargetDebuffPresent(bleed) and ComboPoints(equal 5) and TargetDeadIn(more 10) Spell(RUPTURE)
	#/eviscerate,if=combo_points=5
	if ComboPoints(equal 5) Spell(EVISCERATE)
	#/revealing_strike,if=combo_points=4&buff.revealing_strike.down
	unless TargetBuffPresent(REVEALINGSTRIKE mine=1) if ComboPoints(equal 4) Spell(REVEALINGSTRIKE)
	#/sinister_strike,if=combo_points<5
	if ComboPoints(less 5) Spell(SINISTERSTRIKE)
	#/revealing_strike,if=combo_points=4&buff.revealing_strike.down
	unless TargetBuffPresent(REVEALINGSTRIKE mine=1) if ComboPoints(equal 4) Spell(REVEALINGSTRIKE)
}

AddIcon help=cd mastery=2
{
	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/kick
	if TargetIsInterruptible() and TargetInRange(KICK) Spell(KICK)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<30
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 30)} Item(POTIONOFTHETOLVIR)
	#/killing_spree,if=energy<35&buff.slice_and_dice.remains>4&buff.adrenaline_rush.down
	if Mana(less 35) and BuffPresent(SLICEANDDICE 4) unless BuffPresent(ADRENALINERUSH) Spell(KILLINGSPREE)
	#/adrenaline_rush,if=energy<35
	unless BuffPresent(KILLINGSPREE) if Mana(less 35) Spell(ADRENALINERUSH)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

# Main Subtlety rotation.
AddIcon help=main mastery=3
{
	# Maintain raid debuffs.
	if CheckBoxOn(hemorrhage) and TalentPoints(HEMORRHAGETALENT more 0) and TargetDebuffExpires(bleed 2) Spell(HEMORRHAGE)

	#/apply_poison,main_hand=instant,off_hand=deadly
	if WeaponEnchantExpires(mainhand 5) Item(INSTANTPOISON)
	if WeaponEnchantExpires(offhand 5) Item(DEADLYPOISON)
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	#/shadowstep,if=buff.stealthed.up|buff.shadow_dance.up
	if Stealthed() or BuffPresent(SHADOWDANCE) Spell(SHADOWSTEP)
	#/premeditation,if=(combo_points<=3&cooldown.honor_among_thieves.remains>1.75)|combo_points<=2
	if ComboPoints(less 3) Spell(PREMEDITATION usable=1)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/ambush,if=combo_points<=4
	if ComboPoints(less 5) Spell(AMBUSH usable=1)
	#/slice_and_dice,if=buff.slice_and_dice.remains<3&combo_points=5
	if BuffExpires(SLICEANDDICE 3) and ComboPoints(equal 5) Spell(SLICEANDDICE)
	#/rupture,if=combo_points=5&!ticking
	if ComboPoints(equal 5) unless TargetDebuffPresent(RUPTURE mine=1) if TargetDeadIn(more 12) Spell(RUPTURE)
	#/recuperate,if=combo_points=5&remains<3
	if TalentPoints(ENERGETICRECOVERYTALENT more 0) and ComboPoints(equal 5) and BuffExpires(RECUPERATE 3) Spell(RECUPERATE)
	#/eviscerate,if=combo_points=5&dot.rupture.remains>1
	if ComboPoints(equal 5) and {TargetDebuffPresent(RUPTURE 1 mine=1) or TargetDeadIn(less 7)} Spell(EVISCERATE)
	if TalentPoints(HEMORRHAGETALENT more 0) and Glyph(GLYPHOFHEMORRHAGE)
	{
		#/hemorrhage,if=combo_points<4&dot.hemorrhage.remains<4
		if ComboPoints(less 4) and TargetDebuffExpires(HEMORRHAGEDOT 4 mine=1) Spell(HEMORRHAGE nored=1)
		#/hemorrhage,if=combo_points<5&energy>80&dot.hemorrhage.remains<4
		if ComboPoints(less 5) and Mana(more 80) and TargetDebuffExpires(HEMORRHAGEDOT 4 mine=1) Spell(HEMORRHAGE nored=1)
	}
	#/backstab,if=combo_points<4
	if ComboPoints(less 4) Spell(BACKSTAB)
	#/backstab,if=combo_points<5&energy>80
	if ComboPoints(less 5) and Mana(more 80) Spell(BACKSTAB)
}

# Main Subtlety rotation (same as above) minus Backstab fillers.
AddIcon help=main mastery=3 checkboxon=finisher
{
	#/tricks_of_the_trade,if=set_bonus.tier12_4pc_melee|set_bonus.tier13_2pc_melee
	if ArmorSetParts(T12 more 3) or ArmorSetParts(T13 more 1) Spell(TRICKSOFTHETRADE)
	#/shadowstep,if=buff.stealthed.up|buff.shadow_dance.up
	if Stealthed() or BuffPresent(SHADOWDANCE) Spell(SHADOWSTEP)
	#/premeditation,if=(combo_points<=3&cooldown.honor_among_thieves.remains>1.75)|combo_points<=2
	if ComboPoints(less 3) Spell(PREMEDITATION usable=1)
	unless TargetInRange(SHIV) Texture(inv_misc_flower_02)
	#/ambush,if=combo_points<=4
	if ComboPoints(less 5) Spell(AMBUSH usable=1)
	#/slice_and_dice,if=buff.slice_and_dice.remains<3&combo_points=5
	if BuffExpires(SLICEANDDICE 3) and ComboPoints(equal 5) Spell(SLICEANDDICE)
	#/rupture,if=combo_points=5&!ticking
	if ComboPoints(equal 5) unless TargetDebuffPresent(RUPTURE mine=1) if TargetDeadIn(more 12) Spell(RUPTURE)
	#/recuperate,if=combo_points=5&remains<3
	if TalentPoints(ENERGETICRECOVERYTALENT more 0) and ComboPoints(equal 5) and BuffExpires(RECUPERATE 3) Spell(RECUPERATE)
	#/eviscerate,if=combo_points=5&dot.rupture.remains>1
	if ComboPoints(equal 5) and {TargetDebuffPresent(RUPTURE 1 mine=1) or TargetDeadIn(less 7)} Spell(EVISCERATE)
	if TalentPoints(HEMORRHAGETALENT more 0) and Glyph(GLYPHOFHEMORRHAGE)
	{
		#/hemorrhage,if=combo_points<4&dot.hemorrhage.remains<4
		if ComboPoints(less 4) and TargetDebuffExpires(HEMORRHAGEDOT 4 mine=1) Spell(HEMORRHAGE nored=1)
		#/hemorrhage,if=combo_points<5&energy>80&dot.hemorrhage.remains<4
		if ComboPoints(less 5) and Mana(more 80) and TargetDebuffExpires(HEMORRHAGEDOT 4 mine=1) Spell(HEMORRHAGE nored=1)
	}
}

AddIcon help=cd mastery=3
{
	#/stealth
	unless InCombat() or Stealthed() Spell(STEALTH)
	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/kick
	if TargetIsInterruptible() and TargetInRange(KICK) Spell(KICK)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<30
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 30)} Item(POTIONOFTHETOLVIR)
	#/pool_energy,for_next=1
	#/shadow_dance,if=energy>85&combo_points<5&buff.stealthed.down
	if TalentPoints(SHADOWDANCETALENT more 0) and Mana(more 85) and ComboPoints(less 5) and Stealthed(no) Spell(SHADOWDANCE)
	unless TalentPoints(SHADOWDANCETALENT more 0) and Spell(SHADOWDANCE) and ComboPoints(less 5) and Stealthed(no)
	{
		#/pool_energy,for_next=1
		#/vanish,if=time>10&energy>60&combo_points<=1&cooldown.shadowstep.remains<=0&!buff.shadow_dance.up&!buff.master_of_subtlety.up&!buff.find_weakness.up
		if TimeInCombat(more 10) and Mana(more 60) and ComboPoints(less 2) and Spell(SHADOWSTEP) and Stealthed(no)
			and BuffExpires(SHADOWDANCE 0) and BuffExpires(MASTEROFSUBTLETY 0) and TargetDebuffExpires(FINDWEAKNESS 0 mine=1)
				Spell(VANISH)
		unless TimeInCombat(more 10) and ComboPoints(less 2) and Spell(SHADOWSTEP) and Stealthed(no)
			and BuffExpires(SHADOWDANCE 0) and BuffExpires(MASTEROFSUBTLETY 0) and TargetDebuffExpires(FINDWEAKNESS 0 mine=1)
		{
			#/preparation,if=cooldown.vanish.remains>60
			unless 60s before Spell(VANISH) Spell(PREPARATION)
			Item(Trinket0Slot usable=1)
			Item(Trinket1Slot usable=1)
		}
	}
}

AddIcon size=small
{
	Spell(TRICKSOFTHETRADE)
}

AddIcon size=small
{
	Spell(CLOACKOFSHADOWS)
}