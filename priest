### Shadow Priest Ovale script based on SimulationCraft profile
### https://sites.google.com/site/wownerien/ovale/priest/script
### Version: 4.3.7
#
# Shadow:
#	talents=http://www.wowhead.com/talent#priest-032212000000000000000000000000000000000000322032210201222100231
#	glyphs=mind_flay/shadow_word_pain/shadow_word_death/spirit_tap

## defines ##

# Buffs
Define(SHADOWORBS 77487)
Define(MINDSPIKEBUFF 87178)
Define(EVANGELISM 87118)
Define(DARKARCHANGEL 87153)
Define(MINDMELTRANK1 81292)
Define(MINDMELTRANK2 87160)
Define(EMPOWEREDSHADOW 95799)

# Glyphs
Define(GLYPHOFDISPERSION 63229)

# Talents
Define(IMPROVEDDEVOURINGPLAGUETALENT 9062)
Define(IMPROVEDMINDBASTTALENT 9042)
Define(MASOCHISMTALENT 11778)
Define(SHADOWYAPPARITIONTALENT 9070)

# Spells
Define(ARCHANGEL 87151) # Archangel
	SpellInfo(ARCHANGEL cd=90)
	SpellAddBuff(ARCHANGEL DARKARCHANGEL=18)
Define(DEVOURINGPLAGUE 2944) # Devouring Plague
	SpellInfo(DEVOURINGPLAGUE duration=24 tick=3)
	SpellAddTargetDebuff(DEVOURINGPLAGUE DEVOURINGPLAGUE=24)
Define(DISPERSION 47585)
	SpellInfo(DISPERSION cd=120)
	SpellInfo(DISPERSION addcd=-45 glyph=GLYPHOFDISPERSION)
Define(INNERFIRE 588) # Inner Fire
	SpellAddBuff(INNERFIRE INNERFIRE=1800)
Define(INNERWILL 73413) # Inner Will
	SpellAddBuff(INNERWILL INNERWILL=1800)
Define(MINDBLAST 8092) # Mind Blast
	SpellInfo(MINDBLAST cd=8)
	SpellInfo(MINDBLAST addcd=-1.5 talent=IMPROVEDMINDBASTTALENT)
	SpellAddBuff(MINDBLAST EMPOWEREDSHADOW=15 SHADOW_ORBS=0)
Define(MINDFLAY 15407) # Mind Flay
Define(MINDSPIKE 73510) # Mind Spike
	SpellAddBuff(MINDSPIKE MINDSPIKEBUFF=12)
Define(SHADOWFIEND 34433)
	SpellInfo(SHADOWFIEND cd=300)
Define(SHADOWFORM 15473) # Shadowform
Define(SHADOWWORDDEATH 32379) # Shadow Word: Death
Define(SHADOWWORDPAIN 589) # Shadow Word: Pain
	SpellInfo(SHADOWWORDPAIN duration=18 tick=3)
	SpellAddTargetDebuff(SHADOWWORDPAIN SHADOWWORDPAIN=18)
Define(SILENCE 15487) # Silence
	SpellInfo(SILENCE cd=45)
Define(VAMPIRICEMBRACE 15286) # Vampiric Embrace
Define(VAMPIRICTOUCH 34914) # Vampiric Touch
	SpellInfo(VAMPIRICTOUCH duration=15 tick=3)
	SpellAddTargetDebuff(VAMPIRICTOUCH VAMPIRICTOUCH=15)

# Racials & Professions
Define(ARCANETORRENTMANA 28730) # blood elf
	SpellInfo(ARCANETORRENTMANA sharedcd=racial cd=120)
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellAddBuff(STONEFORM STONEFORM=8)

# Items
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)

### end defines ###

ScoreSpells(MINDBLAST SHADOWWORDPAIN VAMPIRICTOUCH DEVOURINGPLAGUE MINDFLAY SHADOWWORDDEATH MINDSPIKE)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL))

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	if ManaPercent(less 91) Spell(ARCANETORRENTMANA)
	Spell(BERSERKING)
}

###
### Shadow
###
AddIcon size=small talent=SHADOWYAPPARITIONTALENT mastery=3
{
	#/stop_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains>=0.2|dot.vampiric_touch.remains<cast_time+2.5
	#/start_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains<=0.1
	if TargetLifePercent(less 25) and {TargetDebuffPresent(SHADOWWORDPAIN mine=1) or OtherDebuffPresent(SHADOWWORDPAIN mine=1)}
	{
		unless TargetDebuffExpires(VAMPIRICTOUCH 4 mine=1 haste=spell) and TargetDeadIn(more 8)
			if 0.2s before Spell(SHADOWWORDDEATH) Texture(ability_druid_dash) # cat dash icon for "move!"
	}
}

AddIcon help=main mastery=3
{
	unless BuffPresent(SHADOWFORM) Spell(SHADOWFORM)
	unless InCombat()
	{
		if BuffExpires(INNERFIRE 300) unless BuffPresent(INNERWILL) Spell(INNERFIRE)
		if BuffExpires(INNERWILL 300) unless BuffPresent(INNERFIRE) Spell(INNERWILL)
		if BuffExpires(VAMPIRICEMBRACE 300) Spell(VAMPIRICEMBRACE)
	}

	#/inner_fire
	unless BuffPresent(INNERWILL) or BuffPresent(INNERFIRE) Spell(INNERFIRE)
	#/vampiric_embrace
	unless BuffPresent(VAMPIRICEMBRACE) Spell(VAMPIRICEMBRACE)

	# Mind Spike rotation on short-lived mobs.
	unless TargetDebuffPresent(SHADOWWORDPAIN mine=1) or TargetDeadIn(more 10)
	{
		if TargetLifePercent(less 25) and LifePercent(more 20) Spell(SHADOWWORDDEATH)
		if BuffPresent(MINDSPIKEBUFF stacks=3) Spell(MINDBLAST)
		if BuffPresent(MINDMELTRANK1 stacks=2) Spell(MINDBLAST)
		if BuffPresent(MINDMELTRANK2 stacks=2) Spell(MINDBLAST)
		Spell(MINDSPIKE)
	}

	#/mind_blast
	if Speed(equal 0) or BuffPresent(MINDMELTRANK2 stacks=2) Spell(MINDBLAST)
	#/shadow_word_pain,if=(!ticking|dot.shadow_word_pain.remains<gcd+0.5)&miss_react
	if TargetDebuffExpires(SHADOWWORDPAIN 2 haste=spell mine=1) and TargetDeadIn(more 10) Spell(SHADOWWORDPAIN)
	#/devouring_plague,if=(!ticking|dot.devouring_plague.remains<gcd+1.0)&miss_react
	unless OtherDebuffPresent(DEVOURINGPLAGUE)
		if TargetDebuffExpires(DEVOURINGPLAGUE 2.5 haste=spell mine=1) and TargetDeadIn(more 8) Spell(DEVOURINGPLAGUE)
	#/stop_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains>=0.2|dot.vampiric_touch.remains<cast_time+2.5
	#/vampiric_touch,if=(!ticking|dot.vampiric_touch.remains<cast_time+2.5)&miss_react
	if TargetDebuffExpires(VAMPIRICTOUCH 4 mine=1 haste=spell) and TargetDeadIn(more 8) unless Speed(more 0) Spell(VAMPIRICTOUCH)
	#/start_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains<=0.1
	#/shadow_word_death,health_percentage<=25
	if TargetLifePercent(less 25) and LifePercent(more 20) Spell(SHADOWWORDDEATH)
	#/shadow_word_death,if=mana_pct<10
	if TalentPoints(MASOCHISMTALENT more 0) and ManaPercent(less 10) Spell(SHADOWWORDDEATH)
	if Speed(more 0)
	{
		#/shadow_word_death,moving=1
		Spell(SHADOWWORDDEATH)
		#/devouring_plague,moving=1,if=mana_pct>10
		if TalentPoints(IMPROVEDDEVOURINGPLAGUETALENT more 0) and ManaPercent(more 10) Spell(DEVOURINGPLAGUE)
	}
	#/mind_flay
	Spell(MINDFLAY)
}

AddIcon help=cd mastery=3
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if TargetIsInterruptible(yes) Spell(SILENCE)
	#init_use_item_actions()
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(VOLCANICPOTION)
	#init_use_racial_actions()
	UseRacialActions()
	#/archangel,if=buff.dark_evangelism.stack>=5&dot.vampiric_touch.remains>5&dot.devouring_plague.remains>5
	if BuffPresent(EVANGELISM stacks=5)
	{
		if TargetDeadIn(less 10) Spell(ARCHANGEL)
		if TargetDebuffPresent(VAMPIRICTOUCH 5 mine=1) and TargetDebuffPresent(DEVOURINGPLAGUE 5 mine=1) Spell(ARCHANGEL)
	}
	#/shadow_fiend
	if TargetDeadIn(more 8) Spell(SHADOWFIEND)
}

AddIcon size=small mastery=3
{
	#/dispersion
	Spell(DISPERSION)
}