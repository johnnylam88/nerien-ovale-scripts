# Shadow Priest Ovale script based on SimulationCraft profile
#
# Revision History
# 4.3.6 (2012/01/26)
#   - Add racial and profession CD abilities, and move item CD abilities to the correct priority.
# 4.3.5 (2012/01/19)
#   - Code simplifications.
# 4.3.4 (2011/12/16)
#   - Use actual potion name in checkbox for potion use.
#   - Allow for non-talented Mind Blast.
# 4.3.3 (2011/12/08)
#   - More accurately transcribe the DoT rules under haste.
#   - Fix Mind Blast cooldown and allow for Silence to interrupt targets.
#   - Checkbox for using potions.
#   - Coding style changes.
# 4.3.2 (2011/12/08)
#   - Move SW:D at low mana levels to mana icon, and Evangelism to CD icon.
# 4.3.1 (2011/12/07)
#   - Initial version using shadow priest profile from SimulationCraft, Tier 13H (r10402).
#   - Mind Spike usage from default Ovale script.
#
# Shadow:
#	talents=http://www.wowhead.com/talent#priest-032212000000000000000000000000000000000000322032210201222100231
#	glyphs=mind_flay/shadow_word_pain/shadow_word_death/spirit_tap

## defines ##

# Buffs
Define(SHADOWORBS 77487)
Define(MINDSPIKEEFFECT 87178)
Define(EVANGELISM 87118)
Define(DARKARCHANGEL 87153)
Define(MINDMELT 81292)
Define(MINDMELTRANK2 87160)
Define(EMPOWEREDSHADOW 95799)

# Talents
Define(IMPROVEDDEVOURINGPLAGUETALENT 9062)
Define(IMPROVEDMINDBASTTALENT 9042)
Define(MASOCHISMTALENT 11778)

# Spells
Define(ARCHANGEL 87151) # Archangel
	SpellInfo(ARCHANGEL cd=90)
	SpellAddBuff(ARCHANGEL DARKARCHANGEL=18)
Define(DEVOURINGPLAGUE 2944) # Devouring Plague
	SpellInfo(DEVOURINGPLAGUE duration=24)
	SpellAddTargetDebuff(DEVOURINGPLAGUE DEVOURINGPLAGUE=24)
Define(DISPERSION 47585)
	SpellInfo(DISPERSION cd=120)
	SpellInfo(DISPERSION addcd=-45 glyph=63229) # Glyph of Dispersion
Define(INNERFIRE 588) # Inner Fire
	SpellAddBuff(INNERFIRE INNERFIRE=1800)
Define(INNERWILL 73413) # Inner Will
	SpellAddBuff(INNERWILL INNERWILL=1800)
Define(MINDBLAST 8092) # Mind Blast
	SpellInfo(MINDBLAST cd=8)
	SpellInfo(MINDBLAST addcd=-1.5 talent=9042) # Improved Mind Blast (Rank 2)
	SpellAddBuff(MINDBLAST SHADOW_ORBS=0)
	SpellAddBuff(MINDBLAST EMPOWEREDSHADOW=15)
Define(MINDFLAY 15407) # Mind Flay
Define(MINDSPIKE 73510) # Mind Spike
	# TODO : add talent condition for MINDMELT
	SpellAddBuff(MINDSPIKE MINDSPIKEEFFECT=12 MINDMELT=6)
Define(SHADOWFIEND 34433)
	SpellInfo(SHADOWFIEND cd=300)
Define(SHADOWFORM 15473) # Shadowform
Define(SHADOWWORDDEATH 32379) # Shadow Word: Death
Define(SHADOWWORDPAIN 589) # Shadow Word: Pain
	SpellInfo(SHADOWWORDPAIN duration=18)
	SpellAddTargetDebuff(SHADOWWORDPAIN SHADOWWORDPAIN=18)
Define(SILENCE 15487) # Silence
	SpellInfo(SILENCE cd=45)
Define(VAMPIRICEMBRACE 15286) # Vampiric Embrace
Define(VAMPIRICTOUCH 34914) # Vampiric Touch
	SpellInfo(VAMPIRICTOUCH duration=15)
	SpellAddTargetDebuff(VAMPIRICTOUCH VAMPIRICTOUCH=15)

# Racials & Professions
Define(ARCANETORRENTMANA 28730) # blood elf
	SpellInfo(ARCANETORRENTMANA sharedcd=racial cd=120)
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellInfo(STONEFORM STONEFORM=8)

# Items
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)

### end defines ###

ScoreSpells(MINDBLAST SHADOWWORDPAIN VAMPIRICTOUCH DEVOURINGPLAGUE MINDFLAY SHADOWWORDDEATH MINDSPIKE)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL))

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	if ManaPercent(less 91) Spell(ARCANETORRENTMANA)
	Spell(BERSERKING)
}

AddIcon help=main mastery=3
{
	unless BuffPresent(SHADOWFORM) Spell(SHADOWFORM usable=1)
	unless InCombat()
	{
		if BuffExpires(INNERFIRE 300) unless BuffPresent(INNERWILL) Spell(INNERFIRE)
		if BuffExpires(INNERWILL 300) unless BuffPresent(INNERFIRE) Spell(INNERWILL)
		if BuffExpires(VAMPIRICEMBRACE 300) Spell(VAMPIRICEMBRACE)
	}

	# if the rotation isn't set up and the target has only a few seconds to live, use Mind Spike instead of normal rotation
	if TargetDebuffExpires(SHADOWWORDPAIN 0 mine=1) and TargetDeadIn(less 10)
	{
		if BuffPresent(MINDSPIKEEFFECT stacks=3) or BuffPresent(MINDMELT stacks=2) Spell(MINDBLAST)
		Spell(MINDSPIKE)
	}

	#/inner_fire
	unless BuffPresent(INNERWILL) or BuffPresent(INNERFIRE) Spell(INNERFIRE)
	#/vampiric_embrace
	unless BuffPresent(VAMPIRICEMBRACE) Spell(VAMPIRICEMBRACE)
	#/mind_blast
	if Speed(more 0) and BuffPresent(MINDMELTRANK2 stacks=2) Spell(MINDBLAST)
	unless Speed(more 0) Spell(MINDBLAST)
	#/shadow_word_pain,if=(!ticking|dot.shadow_word_pain.remains<gcd+0.5)&miss_react
	if TargetDebuffExpires(SHADOWWORDPAIN 2 haste=spell mine=1) Spell(SHADOWWORDPAIN)
	#/devouring_plague,if=(!ticking|dot.devouring_plague.remains<gcd+1.0)&miss_react
	unless OtherDebuffPresent(DEVOURINGPLAGUE)
		if TargetDebuffExpires(DEVOURINGPLAGUE 2 haste=spell mine=1) Spell(DEVOURINGPLAGUE)
	#/vampiric_touch,if=(!ticking|dot.vampiric_touch.remains<cast_time+2.5)&miss_react
	if target.debuffExpires(VAMPIRICTOUCH mine=1) < {castTime(VAMPIRICTOUCH) + 2.5} Spell(VAMPIRICTOUCH)
	#/shadow_word_death,health_percentage<=25
	if TargetLifePercent(less 25) and LifePercent(more 15) Spell(SHADOWWORDDEATH)
	if Speed(more 0)
	{
		#/shadow_word_death,moving=1
		Spell(SHADOWWORDDEATH)
		#/devouring_plague,moving=1,if=mana_pct>10
		if TalentPoints(IMPROVEDDEVOURINGPLAGUETALENT more 0) and ManaPercent(more 10) Spell(DEVOURINGPLAGUE)
	}
	#/mind_flay
	Spell(MINDFLAY)
}

AddIcon help=cd mastery=3
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if TargetIsInterruptible(yes) Spell(SILENCE)
	#init_use_item_actions()
	UseItemActions()
	#init_use_profession_actions()
	UseProfessionActions()
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(VOLCANICPOTION)
	#init_use_racial_actions()
	UseRacialActions()
	#/archangel,if=buff.dark_evangelism.stack>=5&dot.vampiric_touch.remains>5&dot.devouring_plague.remains>5
	if BuffPresent(EVANGELISM stacks=5)
		and TargetDebuffPresent(VAMPIRICTOUCH 5 mine=1) and TargetDebuffPresent(DEVOURINGPLAGUE 5 mine=1)
		Spell(ARCHANGEL)
	#/shadow_fiend
	if TargetDeadIn(more 8) Spell(SHADOWFIEND)
}

AddIcon help=mana mastery=3
{
	#/shadow_word_death,if=mana_pct<10
	if TalentPoints(MASOCHISMTALENT more 0) and ManaPercent(less 10) Spell(SHADOWWORDDEATH)
	#/dispersion
	Spell(DISPERSION)
}