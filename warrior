# Warrior Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.1 2011/12/15 - Initial version based on Ovale default script.
#                    Use warrior profiles from sc-430-2, Tier 13H.

# Buffs
Define(BATTLETRANCE 12964)
Define(BLOODSURGE 46916)
Define(EXECUTIONER 90806)
Define(INCITE 86627)
Define(RENDDEBUFF 94009)
Define(SLAUGHTER 84584)
Define(SUNDERARMORDEBUFF 58567)
Define(TASTEFORBLOOD 60503)
Define(THUNDERSTRUCK 87096)
Define(VICTORIOUS 32216)

# Glyphs
Define(GLYPHOFBERSERKERRAGE 58096)

# Talents
Define(IMPROVEDSLAMTALENT 2233)
Define(SUDDENDEATHTALENT 52437)
Define(TITANSGRIPTALENT 9658)

# Spells
Define(BATTLESHOUT 6673)
	SpellInfo(BATTLESHOUT cd=30)
	SpellAddBuff(BATTLESHOUT BATTLESHOUT=120)
Define(BATTLESTANCE 2457)
Define(BERSERKERRAGE 18499)
	SpellInfo(BERSERKERRAGE cd=30)
Define(BERSERKERSTANCE 2458)
Define(BLADESTORM 46924)
	SpellInfo(BLADESTORM cd=90)
Define(BLOODTHIRST 23881)
	SpellInfo(BLOODTHIRST cd=3)
Define(CHARGE 100)
	SpellInfo(CHARGE cd=15)
Define(CLEAVE 845)
	SpellInfo(CLEAVE cd=3)
Define(COLOSSUSSMASH 86346)
	SpellInfo(COLOSSUSSMASH cd=20)
	SpellAddTargetDebuff(COLOSSUSSMASH COLOSSUSSMASH=6 SUNDERARMORDEBUFF=30)
Define(COMMANDINGSHOUT 469)
	SpellInfo(COMMANDINGSHOUT cd=30)
	SpellAddBuff(COMMANDINGSHOUT COMMANDINGSHOUT=120)
Define(CONCUSSIONBLOW 12809)
	SpellInfo(CONCUSSIONBLOW cd=30)
Define(DEADLYCALM 85730)
	SpellInfo(DEADLYCALM cd=120)
	SpellAddBuff(DEADLYCALM DEADLYCALM=10)
Define(DEATHWISH 12292)
	SpellInfo(DEATHWISH cd=180)
	SpellAddBuff(DEATHWISH DEATHWISH=30)
Define(DEFENSIVESTANCE 71)
Define(DEMOSHOUT 1160)
	SpellAddTargetDebuff(DEMOSHOUT DEMOSHOUT=45)
Define(DEVASTATE 20243)
	SpellAddTargetDebuff(DEVASTATE SUNDERARMORDEBUFF=30)
Define(EXECUTE 5308)
	SpellAddBuff(EXECUTE EXECUTIONER=9)
Define(HEROICLEAP 6544)
	SpellInfo(HEROICLEAP cd=60)
Define(HEROICSTRIKE 78)
	SpellInfo(HEROICSTRIKE cd=3)
Define(HEROICTHROW 57755)
	SpellInfo(HEROICTHROW cd=60)
Define(HEROICFURY 60970)
	SpellInfo(HEROICFURY cd=60)
Define(INNERRAGE 1134)
	SpellInfo(INNERRAGE cd=30)
	SpellAddBuff(INNERRAGE INNERRAGE=15)
Define(INTERCEPT 20252)
	SpellInfo(INTERCEPT cd=30)
Define(INTERVENE 3411)
	SpellInfo(INTERVENE cd=30)
Define(LASTSTAND 12975)
	SpellInfo(LASTSTAND cd=180)
Define(MORTALSTRIKE 12294)
	SpellInfo(MORTALSTRIKE cd=4.5)
Define(OVERPOWER 7384)
	SpellInfo(OVERPOWER cd=1)
	SpellAddBuff(OVERPOWER TASTEFORBLOOD=0)
Define(PUMMEL 6552)
	SpellInfo(PUMMEL cd=10)
Define(RAGINGBLOW 85288)
	SpellInfo(RAGINGBLOW cd=6)
Define(RECKLESSNESS 1719)
	SpellInfo(RECKLESSNESS cd=300)
	SpellAddBuff(RECKLESSNESS RECKLESSNESS=12)
Define(REND 772)
	SpellAddTargetDebuff(REND RENDDEBUFF=15)
Define(RETALIATION 20230)
	SpellInfo(RETALIATION cd=300)
	SpellAddBuff(RETALIATION RETALIATION=12)
Define(REVENGE 6572)
	SpellInfo(REVENGE cd=5)
Define(SHATTERINGTHROW 64382)
	SpellInfo(SHATTERINGTHROW cd=300)
	SpellAddTargetDebuff(SHATTERINGTHROW SHATTERINGTHROW=10)
Define(SHIELDBLOCK 2565)
	SpellInfo(SHIELDBLOCK cd=60)
	SpellAddBuff(SHIELDBLOCK SHIELDBLOCK=10)
Define(SHIELDWALL 871)
	SpellInfo(SHIELDWALL cd=300)
	SpellAddBuff(SHIELDWALL SHIELDWALL=12)
Define(SHIELDSLAM 23922)
	SpellInfo(SHIELDSLAM cd=6)
Define(SHOCKWAVE 46968)
	SpellInfo(SHOCKWAVE cd=20)
	SpellAddBuff(SHOCKWAVE THUNDERSTRUCK=0)
Define(SLAM 1464)
	SpellAddBuff(SLAM BLOODSURGE=-1)
Define(STRIKE 88161)
	SpellInfo(STRIKE cd=3)
Define(SUNDERARMOR 7386)
	SpellAddTargetDebuff(SUNDERARMOR SUNDERARMORDEBUFF=30)
Define(SWEEPINGSTRIKES 12328)
	SpellInfo(SWEEPINGSTRIKES cd=60)
Define(THUNDERCLAP 6343)
	SpellInfo(THUNDERCLAP cd=6)
	SpellAddTargetDebuff(THUNDERCLAP THUNDERCLAP=30)
Define(VICTORYRUSH 34428)
	SpellAddBuff(VICTORYRUSH VICTORIOUS=0)
Define(WHIRLWIND 1680)
	SpellInfo(WHIRLWIND cd=10)

# Items
Define(EARTHENPOTION 58090)
Define(GOLEMBLOODPOTION 58146)

ScoreSpells(DEADLYCALM COLOSSUSSMASH RAGINGBLOW OVERPOWER VICTORYRUSH BLOODTHIRST SLAM REND MORTALSTRIKE EXECUTE SHIELDSLAM REVENGE
			DEVASTATE)

AddCheckBox(aoe L(AOE) default)
AddCheckBox(demo SpellName(DEMOSHOUT))
AddCheckBox(sunder SpellName(SUNDERARMOR) default)
AddCheckBox(dancing SpellName(BERSERKERSTANCE) default mastery=1)
AddCheckBox(armspotions SpellName(GOLEMBLOODPOTION) mastery=1)
AddCheckBox(furypotions SpellName(GOLEMBLOODPOTION) mastery=2)
AddCheckBox(protectionpotions SpellName(EARTHENPOTION) mastery=3)
AddListItem(shout none L(None))
AddListItem(shout battle SpellName(BATTLESHOUT) default)
AddListItem(shout command SpellName(COMMANDINGSHOUT))

AddIcon help=main mastery=1
{
	# Maintain raid buffs and debuffs.
	if List(shout command) and {Mana(less 20) or BuffExpires(stamina 3)} Spell(COMMANDINGSHOUT nored=1)
	if List(shout battle) and {Mana(less 20) or BuffExpires(strengthagility 3)} Spell(BATTLESHOUT nored=1)
	if TargetClassification(worldboss) and CheckBoxOn(demo) and TargetDebuffExpires(lowerphysicaldamage 2) Spell(DEMOSHOUT nored=1)
	if CheckBoxOn(sunder) and TargetDebuffExpires(lowerarmor 0 mine=0)
	{
		unless TargetDebuffPresent(SUNDERARMORDEBUFF 3 mine=1 stacks=3) Spell(SUNDERARMOR nored=1)
	}

	if CheckBoxOn(dancing)
	{
		#/stance,choose=berserker,if=(buff.taste_for_blood.down&rage<75),use_off_gcd=1
		#/stance,choose=battle,if=(dot.rend.remains=0|\
		#	((buff.overpower.up|buff.taste_for_blood.up)&cooldown.mortal_strike.remains>1)&rage<=75),use_off_gcd=1
		unless Stance(3) if Mana(less 75) unless BuffPresent(TASTEFORBLOOD) Spell(BERSERKERSTANCE)
		unless Stance(1)
		{
			if TargetDebuffExpires(RENDDEBUFF 0 mine=1) Spell(BATTLESTANCE)
			if {BuffPresent(OVERPOWER) or BuffPresent(TASTEFORBLOOD)} and {spell(MORTALSTRIKE) > 1} and Mana(less 76) Spell(BATTLESTANCE)
		}
	}
	if CheckBoxOff(dancing) unless Stance(1) Spell(BATTLESTANCE)

	#/berserker_rage,if=!buff.deadly_calm.up&rage<70
	if Glyph(GLYPHOFBERSERKERRAGE) and Mana(less 71) unless BuffPresent(DEADLYCALM) Spell(BERSERKERRAGE)
	#/overpower,if=buff.taste_for_blood.remains<=1.5
	if BuffExpires(TASTEFORBLOOD 1.5) Spell(OVERPOWER)
	#/mortal_strike,if=target.health_pct>20|rage>=30
	if TargetLifePercent(more 20) or Mana(more 29) Spell(MORTALSTRIKE)
	#/execute,if=buff.battle_trance.up
	if BuffPresent(BATTLESTANCE) spell(EXECUTE usable=1)
	#/rend,if=!ticking
	if Stance(1) unless TargetDebuffPresent(RENDDEBUFF mine=1) Spell(REND)
	#/colossus_smash,if=!buff.colossus_smash.up
	unless TargetDebuffPresent(COLOSSUSSMASH mine=1) Spell(COLOSSUSSMASH)
	#/execute,if=(buff.deadly_calm.up|buff.recklessness.up)
	if BuffPresent(DEADLYCALM) or BuffPresent(RECKLESSNESS) Spell(EXECUTE usable=1)
	#/mortal_strike
	Spell(MORTALSTRIKE)
	#/overpower
	Spell(OVERPOWER)
	#/execute
	Spell(EXECUTE usable=1)
	#/colossus_smash,if=buff.colossus_smash.remains<=1.5
	if TargetDebuffExpires(COLOSSUSSMASH 1.5 mine=1) Spell(COLOSSUSSMASH)
	#/slam,if=(cooldown.mortal_strike.remains>=1.5&(rage>=35|buff.deadly_calm.up|buff.colossus_smash.up))|\
	#	(cooldown.mortal_strike.remains>=1.2&buff.colossus_smash.remains>0.5&rage>=35)
	if {spell(MORTALSTRIKE) > 1.5} and {Mana(more 34) or BuffPresent(DEADLYCALM) or TargetDebuffPresent(COLOSSUSSMASH mine=1)} Spell(SLAM)
	if {spell(MORTALSTRIKE) > 1.2) and TargetDebuffExpires(COLOSSUSSMASH 0.5) and Mana(more 34) Spell(SLAM)
	#/battle_shout,if=rage<20
	if Mana(less 20) Spell(BATTLESHOUT)
}

AddIcon help=offgcd mastery=1
{
	#/inner_rage,if=target.adds=0&!buff.deadly_calm.up&cooldown.deadly_calm.remains>15&\
	#	((rage>=75&target.health_pct>=20)|\
	#		((buff.incite.up|buff.colossus_smash.up)&\
	#			((rage>=40&target.health_pct>=20)|(rage>=65&target.health_pct<20)))
	#	)
	if BuffPresent(DEADLYCALM) and {spell(DEADLYCALM) > 15}
	{
		if TargetLifePercent(more 20) and Mana(more 74) Spell(INNERRAGE)
		if BuffPresent(INCITE) or TargetDebuffPresent(COLOSSUSSMASH mine=1)
		{
			if TargetLifePercent(more 20) and Mana(more 39) Spell(INNERRAGE)
			unless TargetLifePercent(more 20) if Mana(more 64) Spell(INNERRAGE)
		}
	}
	#/heroic_strike,if=(((rage>=85|(set_bonus.tier13_2pc_melee&buff.inner_rage.up&rage>=75))&target.health_pct>=20)|\
	#	buff.deadly_calm.up|\
	#	buff.battle_trance.up|\
	#	((buff.incite.up|buff.colossus_smash.up)&\
	#		(((rage>=50|(set_bonus.tier13_2pc_melee&buff.inner_rage.up&rage>=40))&target.health_pct>=20)|\
	#		 ((rage>=75|(set_bonus.tier13_2pc_melee&buff.inner_rage.up&rage>=65))&target.health_pct<20))
	#	)),use_off_gcd=1
	if TargetLifePercent(more 20)
	{
		if Mana(more 84) Spell(HEROICSTRIKE)
		if Mana(more 74) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
	}
	if BuffPresent(DEADLYCALM) Spell(HEROICSTRIKE)
	if BuffPresent(BATTLETRANCE) Spell(HEROICSTRIKE)
	if BuffPresent(INCITE) or TargetDebuffPresent(COLOSSUSSMASH mine=1)
	{
		if TargetLifePercent(more 20)
		{
			if Mana(more 49) Spell(HEROICSTRIKE)
			if Mana(more 39) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
		}
		unless TargetLifePercent(more 20)
		{
			if Mana(more 74) Spell(HEROICSTRIKE)
			if Mana(more 64) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
		}
	}
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	#/sweeping_strikes,if=target.adds>0
	Spell(SWEEPINGSTRIKES)
	#/bladestorm,if=target.adds>0&!buff.deadly_calm.up&!buff.sweeping_strikes.up
	unless BuffPresent(DEADLYCALM) or BuffPresent(SWEEPINGSTRIKES) Spell(BLADESTORM)
	#/cleave,if=target.adds>0,use_off_gcd=1
	Spell(CLEAVE)
}

AddIcon help=cd mastery=1
{
	#/golemblood_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(furypotions) Item(GOLEMBLOODPOTION)
	if TargetIsInterruptible() and TargetInRange(PUMMEL) Spell(PUMMEL)
	#/golemblood_potion,if=buff.recklessness.up|target.time_to_die<26
	if CheckBoxOn(furypotions) and {BuffPresent(RECKLESSNESS) or TargetDeadIn(less 26)} Item(GOLEMBLOODPOTION)
	#/recklessness,if=((target.health_pct>20&target.time_to_die>320)|target.health_pct<=20),use_off_gcd=1
	if TargetLifePercent(more 20) and TargetDeadIn(more 320) Spell(RECKLESSNESS)
	unless TargetLifePercent(more 20) Spell(RECKLESSNESS)
	#/deadly_calm,if=rage<30,use_off_gcd=1
	if Mana(less 30) Spell(DEADLYCALM usable=1)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=2
{
	# Maintain raid buffs and debuffs.
	if List(shout command) and {Mana(less 20) or BuffExpires(stamina 3)} Spell(COMMANDINGSHOUT nored=1)
	if List(shout battle) and {Mana(less 20) or BuffExpires(strengthagility 3)} Spell(BATTLESHOUT nored=1)
	if TargetClassification(worldboss) and CheckBoxOn(demo) and TargetDebuffExpires(lowerphysicaldamage 2) Spell(DEMOSHOUT nored=1)
	if CheckBoxOn(sunder) and TargetDebuffExpires(lowerarmor 0 mine=0)
	{
		unless TargetDebuffPresent(SUNDERARMORDEBUFF 3 mine=1 stacks=3) Spell(SUNDERARMOR nored=1)
	}

	#/stance,choose=berserker
	unless Stance(3) Spell(BERSERKERSTANCE)
	#/execute,if=buff.executioner_talent.remains<1.5
	if BuffExpires(EXECUTIONER 1.5) Spell(EXECUTE usable=1)
	#/colossus_smash
	Spell(COLOSSUSSMASH)
	#/execute,if=buff.executioner_talent.stack<5
	unless BuffPresent(EXECUTIONER stacks=5) Spell(EXECUTE usable=1)
	#/bloodthirst
	Spell(BLOODTHIRST)
	unless TalentPoints(TITANSGRIPTALENT more 0)
	{
		#/slam,if=buff.bloodsurge.react
		if BuffPresent(BLOODSURGE) Spell(SLAM)
		#/execute,if=rage>=50
		if Mana(more 49) Spell(EXECUTE usable=1)
	}
	#/berserker_rage,if=!(buff.death_wish.up|buff.enrage.up|buff.unholy_frenzy.up)&rage>15&cooldown.raging_blow.remains<1
	if BuffExpires(enrage 0) and Mana(more 15) and 1s before Spell(RAGINGBLOW) Spell(BERSERKERRAGE)
	#/raging_blow
	Spell(RAGINGBLOW usable=1)
	if TalentPoints(TITANSGRIPTALENT more 0)
	{
		#/slam,if=buff.bloodsurge.react
		if BuffPresent(BLOODSURGE) Spell(SLAM)
		#/execute,if=rage>=50
		if Mana(more 49) Spell(EXECUTE usable=1)
	}
	#/battle_shout,if=rage<70
	if Mana(less 70) Spell(BATTLESHOUT)
}

AddIcon help=offgcd mastery=2
{
	#/inner_rage,use_off_gcd=1,if=target.adds=0&((rage>=75&target.health_pct>=20)|\
	#	((buff.incite.up|buff.colossus_smash.up)&\
	#		((rage>=40&target.health_pct>=20)|
	#		 (rage>=65&target.health_pct<20))
	#	))
	if TargetLifePercent(more 20) and Mana(more 74) Spell(INNERRAGE)
	if BuffPresent(INCITE) or TargetDebuffPresent(COLOSSUSSMASH mine=1)
	{
		if TargetLifePercent(more 20) and Mana(more 39) Spell(INNERRAGE)
		unless TargetLifePercent(more 20) if Mana(more 64) Spell(INNERRAGE)
	}
	#/heroic_strike,use_off_gcd=1,if=(((rage>=85|(set_bonus.tier13_2pc_melee&buff.inner_rage.up&rage>=75))&target.health_pct>=20)|\
	#	buff.battle_trance.up|\
	#	((buff.incite.up|buff.colossus_smash.up)&\
	#		(((rage>=50|(rage>=40&set_bonus.tier13_2pc_melee&buff.inner_rage.up))&target.health_pct>=20)|\
	#		 ((rage>=75|(rage>=65&set_bonus.tier13_2pc_melee&buff.inner_rage.up))&target.health_pct<20))
	#	))
	if TargetLifePercent(more 20)
	{
		if Mana(more 84) Spell(HEROICSTRIKE)
		if Mana(more 74) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
	}
	if BuffPresent(BATTLETRANCE) Spell(HEROICSTRIKE)
	if BuffPresent(INCITE) or TargetDebuffPresent(COLOSSUSSMASH mine=1)
	{
		if TargetLifePercent(more 20)
		{
			if Mana(more 49) Spell(HEROICSTRIKE)
			if Mana(more 39) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
		}
		unless TargetLifePercent(more 20)
		{
			if Mana(more 74) Spell(HEROICSTRIKE)
			if Mana(more 64) and ArmorSetParts(T13 more 1) and BuffPresent(INNERRAGE) Spell(HEROICSTRIKE)
		}
	}
}

AddIcon help=cd mastery=2
{
	#/golemblood_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(furypotions) Item(GOLEMBLOODPOTION)
	if TargetIsInterruptible() and TargetInRange(PUMMEL) Spell(PUMMEL)
	#/golemblood_potion,if=buff.bloodlust.react
	if CheckBoxOn(furypotions) and BuffPresent(heroism) Item(GOLEMBLOODPOTION)
	#/recklessness,use_off_gcd=1
	Spell(RECKLESSNESS)
	#/death_wish,use_off_gcd=1
	Spell(DEATHWISH)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	#/cleave,if=target.adds>0,use_off_gcd=1
	Spell(CLEAVE) 
	#/whirlwind,if=target.adds>0
	Spell(WHIRLWIND)
}

# Protection warrior rotation from EJ for 4.2:
# http://elitistjerks.com/f81/t110315-cataclysm_protection_warrior/#post1813079
AddIcon help=main mastery=3
{
	if Stance(3) Spell(DEFENSIVESTANCE)
	if List(shout command) and {Mana(less 20) or BuffExpires(stamina 3)} Spell(COMMANDINGSHOUT nored=1)
	if List(shout battle) and {Mana(less 20) or BuffExpires(strengthagility 3)} Spell(BATTLESHOUT nored=1)

	if LifePercent(less 75) and BuffPresent(VICTORIOUS) Spell(VICTORYRUSH usable=1)

	if CheckBoxOn(sunder) and TargetDebuffExpires(lowerarmor 0 mine=0)
	{
		unless TargetDebuffPresent(SUNDERARMORDEBUFF 3 mine=1 stacks=3) Spell(DEVASTATE)
	}

	if 1s before Spell(SHIELDSLAM) Spell(SHIELDSLAM usable=1)

	if CheckBoxOn(demo) and TargetDebuffExpires(lowerphysicaldamage 4) Spell(DEMOSHOUT)
	if TargetDebuffExpires(meleeslow 2) Spell(THUNDERCLAP)

	Spell(VICTORYRUSH usable=1)
	Spell(REVENGE usable=1)

	# A single Rend application is only worth using on a 30%-bleed debuffed single target if it ticks at
	# least four times. Unbuffed, it must tick all six times. Never refresh an existing Rend -- always
	# wait for it to run its full duration or else you lose the initial application tick.
	#
	unless TargetDebuffPresent(RENDDEBUFF mine=1) {
		if TargetDebuffPresent(bleed) and TargetDeadIn(more 9) or TargetDeadIn(more 15) {
			Spell(REND)
		}
	}
	Spell(DEVASTATE)
}

AddIcon help=offgcd mastery=3
{
	if TargetIsInterruptible() and TargetInRange(PUMMEL) Spell(PUMMEL)
	if Mana(more 44) Spell(HEROICSTRIKE)
}

AddIcon help=aoe mastery=3 checkboxon=aoe
{
	if TargetDebuffExpires(RENDDEBUFF mine=1) Spell(REND)
	Spell(THUNDERCLAP)
	Spell(SHOCKWAVE)
	Spell(CLEAVE)
}

AddIcon help=cd mastery=3
{
	unless InCombat() if CheckBoxOn(protectionpotions) Item(EARTHENPOTION)
	if Stance(2)
	{
		Spell(SHIELDBLOCK usable=1)
		Spell(SHIELDWALL usable=1)
		Spell(LASTSTAND)
	}
	if Stance(1) Spell(RECKLESSNESS)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}